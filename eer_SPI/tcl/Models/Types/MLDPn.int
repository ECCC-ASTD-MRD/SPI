#============================================================================
# Environnement Canada - Service meteorologique du Canada
# Centre meteorologique canadien
# 2121 Route Trans-canadienne
# Dorval, Quebec
# H9P 1J3
#
# Projet     : Interface pour la gestion des experiences.
# Fichier    : <MLDPn.int>
# Creation   : Octobre 1999 - J.P. Gauthier - CMC/CMOE
#
# Description: Description des interfaces et procedures relatives a
#              celles-ci pour le module MLDP.
#
# Remarques  :
#
#============================================================================

package require OneTimeBubble

#----------------------------------------------------------------------------
# Nom        : <MLDPn::New>
# Creation   : Octobre 1999 - J.P. Gauthier - CMC/CMOE -
#
# But        : Creation d'une nouvelle simulation MLDP.
#
# Parametres :
#   <Tab>    : Boite d'onglet dans laquelle ajoute les onglets
#   <Auto>   : Mode Auto
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::ParamsNew { Tab { Auto False } } {
   global GDefs
   variable Sim
   variable Msg
   variable Lbl
   variable Bubble

   wm geom [winfo toplevel $Tab] =400x425
   wm resizable .modelnew 1 1

   #----- Model Tab.
   set tabframe [TabFrame::Add $Tab 1 "[lindex $Lbl(Model) $GDefs(Lang)]" False]
   set Sim(TabframeModel) $tabframe

   #----- Simulation Starting Time.
   if { !$Auto } {
      labelframe $tabframe.time -text "[lindex $Lbl(Start) $GDefs(Lang)]"

         #----- Simulation Date Hour and minutes.
         Calendar::Create $tabframe.time.date [lindex $Lbl(Date) $GDefs(Lang)] MLDPn::Sim(Secs) -1 ""
         Clock::Create $tabframe.time.date.hour "" MLDPn::Sim(Hour) MLDPn::Sim(Min)
         pack $tabframe.time.date -side top -anchor w -padx 2 -fill x -expand True
         pack $tabframe.time.date.hour -side right -fill y

         #----- Mode avant ou arriere.
         frame  $tabframe.time.mode
            label $tabframe.time.mode.lbl -text [lindex $Lbl(Mode) $GDefs(Lang)]
            frame  $tabframe.time.mode.opt -relief sunken -bd 1
               radiobutton $tabframe.time.mode.opt.forward  -text [lindex $Lbl(Forward) $GDefs(Lang)]  -bd 1 -indicatoron False -variable MLDPn::Sim(Backward) -value False -selectcolor $GDefs(ColorLight)
               radiobutton $tabframe.time.mode.opt.backward -text [lindex $Lbl(Backward) $GDefs(Lang)] -bd 1 -indicatoron False -variable MLDPn::Sim(Backward) -value True -selectcolor $GDefs(ColorLight)
               pack $tabframe.time.mode.opt.forward $tabframe.time.mode.opt.backward -side left -fill x -expand True
            pack $tabframe.time.mode.lbl -side left
            pack $tabframe.time.mode.opt -side left -fill x -expand True
         pack $tabframe.time.mode -side top -fill x -padx 2

         Bubble::Create $tabframe.time.date $Bubble(StartDate)
         Bubble::Create $tabframe.time.date.hour $Bubble(StartHour)
         Bubble::Create $tabframe.time.mode $Bubble(Mode)

      pack $tabframe.time -side top -padx 5 -pady 5 -fill x
      Bubble::Create $tabframe.time $Bubble(StartTime)
   }

   #----- Model Integration (data output time step and model time step).
   labelframe $tabframe.integ -text "[lindex $Lbl(Integration) $GDefs(Lang)]"

      #----- Noyeau de diffusion.
      Option::Create $tabframe.integ.kernel [lindex $Lbl(DiffKernel) $GDefs(Lang)] MLDPn::Sim(DiffKernel) 1 -1 $MLDPn::Sim(ListDiffKernel) "MLDPn::InitKernel"
      pack $tabframe.integ.kernel -side top -fill x -padx 2
      Bubble::Create $tabframe.integ.kernel $Bubble(DiffKernel)

      #----- Simulation duration [hr].
      Option::Create $tabframe.integ.dur [lindex $Lbl(SimDuration) $GDefs(Lang)] MLDPn::Sim(Duration) 1 -1 $MLDPn::Sim(ListSimDuration) "MLDPn::CheckFileSize"
      pack $tabframe.integ.dur -side top -fill x -padx 2
      Bubble::Create $tabframe.integ.dur $Bubble(SimDuration)
      set MLDPn::Sim(SimDurationEnt) $tabframe.integ.dur.e

      #----- Data output time step [min].
      Option::Create $tabframe.integ.ots [lindex $Lbl(OutputTimeStepMin) $GDefs(Lang)] MLDPn::Sim(OutputTimeStepMin) 1 -1 $MLDPn::Sim(ListOutputTimeStep) "MLDPn::CheckFileSize"
      pack $tabframe.integ.ots -side top -fill x -padx 2
      Bubble::Create $tabframe.integ.ots $Bubble(OutputTimeStepMin)
      set MLDPn::Sim(OutputTimeStepEnt) $tabframe.integ.ots.e

      #----- Model time step [min].
      Option::Create $tabframe.integ.mts [lindex $Lbl(ModelTimeStepMin) $GDefs(Lang)] MLDPn::Sim(ModelTimeStepMin) 1 -1 $MLDPn::Sim(ListModelTimeStep) ""
      pack $tabframe.integ.mts -side top -fill x -padx 2
      Bubble::Create $tabframe.integ.mts $Bubble(ModelTimeStepMin)
      set MLDPn::Sim(ModelTimeStepEnt) $tabframe.integ.mts.e

   pack $tabframe.integ -side top -padx 5 -fill x
   Bubble::Create $tabframe.integ $Bubble(SimIntegration)

   #----- Other parameters.
   labelframe $tabframe.others -text "[lindex $Lbl(OtherParameters) $GDefs(Lang)]"

      #----- Virus Source Type.
      if { $Sim(SrcType) == "VIRUS" } {
         Option::Create $tabframe.others.virus [lindex $Lbl(Virus) $GDefs(Lang)] { MLDPn::Sim(VirusName) MLDPn::Sim(VirusSymbol) } 0 -1 [lindex $MLDPn::Sim(ListVirusName) $GDefs(Lang)] "" $Sim(ListVirusSymbol)
         pack $tabframe.others.virus -side top -ipady 1 -fill x -padx 2
         Bubble::Create $tabframe.others.virus $Bubble(Virus)
      }

      #----- Meteorological Model.
      frame $tabframe.others.met
      Option::Create $tabframe.others.met.model [lindex $Lbl(Meteo) $GDefs(Lang)] MLDPn::Sim(Meteo) 0 -1 $MLDPn::Sim(ListMeteoModel) ""
      Option::Create $tabframe.others.met.delta [lindex $Lbl(MeteoDelta) $GDefs(Lang)] MLDPn::Sim(Delta) 0 6 $MLDPn::Sim(ListMeteoDelta) "MLDPn::CheckFileSize"
      button $tabframe.others.met.path -image OPEN -relief flat -bd 0 -overrelief raised -command "Model::ParamsMetPath"
      pack $tabframe.others.met.model -side left -ipady 1 -fill x -expand True
      pack $tabframe.others.met.delta -side left -ipady 1
      pack $tabframe.others.met.path  -side left
      pack $tabframe.others.met       -side top -fill x -padx 2

      #----- Horizontal wind velocity variance for mesoscale fluctuations [m2/s2].
      Option::Create $tabframe.others.varmesoscale [lindex $Lbl(VarMesoscale) $GDefs(Lang)] MLDPn::Sim(VarMesoscale) 1 -1 $MLDPn::Sim(ListVarMesoscale) ""
      set MLDPn::Sim(VarMesoscaleEnt) $tabframe.others.varmesoscale.e
      pack $tabframe.others.varmesoscale -side top -ipady 1 -fill x -padx 2

      #----- Lagrangian time scale for mesoscale fluctuations [s].
      Option::Create $tabframe.others.timescale [lindex $Lbl(Timescale) $GDefs(Lang)] MLDPn::Sim(Timescale) 1 -1 $MLDPn::Sim(ListTimescale) ""
      set MLDPn::Sim(TimescaleEnt) $tabframe.others.timescale.e
      pack $tabframe.others.timescale -side top -ipady 1 -fill x -padx 2

      #----- Reflection level [hyb|eta|sig].
      Option::Create $tabframe.others.reflect [lindex $Lbl(ReflectionLevel) $GDefs(Lang)] MLDPn::Sim(ReflectionLevel) 1 -1 $MLDPn::Sim(ListReflectionLevel) "MLDPn::UpdateListVerticalLevels"
      set MLDPn::Sim(ReflectionLevelEnt) $tabframe.others.reflect.e
      pack $tabframe.others.reflect -side top -ipady 1 -fill x -padx 2
      bind $tabframe.others.reflect.e <Key-Tab>  "MLDPn::UpdateListVerticalLevels"
      bind $tabframe.others.reflect.e <Button-1> "MLDPn::UpdateListVerticalLevels"

      #----- Seed.
      Option::Create $tabframe.others.seed [lindex $Lbl(Seed) $GDefs(Lang)] MLDPn::Sim(Seed) 1 -1 $MLDPn::Sim(ListSeed) ""
      pack $tabframe.others.seed -side top -ipady 1 -fill x -padx 2

      #----- Aerosol.
      Option::Create $tabframe.others.aero [lindex $Lbl(Aerosol) $GDefs(Lang)] MLDPn::Sim(Aerosol) 1 -1 $MLDPn::Sim(ListAerosol) ""
      pack $tabframe.others.aero -side top -ipady 1 -fill x -padx 2

      #----- Wet scavenging.
      Option::Create $tabframe.others.sca [lindex $Lbl(WetSca) $GDefs(Lang)] MLDPn::Sim(WetScaMode) 1 -1 $MLDPn::Sim(ListJianMode) ""
      pack $tabframe.others.sca -side top -ipady 1 -fill x -padx 2

      #----- Dry deposition.
      Option::Create $tabframe.others.dep [lindex $Lbl(DryDep) $GDefs(Lang)] MLDPn::Sim(DryDepMode) 1 -1 $MLDPn::Sim(ListJianMode) ""
      pack $tabframe.others.dep -side top -ipady 1 -fill x -padx 2

   pack $tabframe.others -side top -padx 5 -pady 5 -fill x

   Bubble::Create $tabframe.others               $Bubble(OtherParameters)
   Bubble::Create $tabframe.others.grid.src      $Bubble(MoveGrid1)
   Bubble::Create $tabframe.others.grid.sel      $Bubble(MoveGrid2)
   Bubble::Create $tabframe.others.met.model     $Bubble(Meteo)
   Bubble::Create $tabframe.others.met.delta     $Bubble(MeteoDelta)
   Bubble::Create $tabframe.others.met.path      $Bubble(ModifyPath)
   Bubble::Create $tabframe.others.varmesoscale  $Bubble(VarMesoscale)
   Bubble::Create $tabframe.others.timescale     $Bubble(Timescale)
   Bubble::Create $tabframe.others.reflect       $Bubble(ReflectionLevel)
   Bubble::Create $tabframe.others.seed          $Bubble(Seed)
   Bubble::Create $tabframe.others.aero          $Bubble(Aerosol)
   
   set tabframe [TabFrame::Add $Tab 1 "[lindex $Lbl(Output) $GDefs(Lang)]" False]
   set Sim(TabframeOut) $tabframe

   #----- Output parameters.
   labelframe $tabframe.out -text "[lindex $Lbl(Parameters) $GDefs(Lang)]"

      #----- Grid Resolution.
      frame $tabframe.out.grid
      Option::Create $tabframe.out.grid.res [lindex $Lbl(OutGrid) $GDefs(Lang)] MLDPn::Sim(Scale) 0 7 $MLDPn::Sim(Grids) "Model::ParamsGridDefine MLDPn ; MLDPn::ValidateModelTimeStepGrid"
      Option::Create $tabframe.out.grid.src "" MLDPn::Sim(GridSrc) 0 -1 $Sim(Pos) \
         "set MLDPn::Sim(GridLat) \[lindex \$MLDPn::Sim(GridSrc) 1\];
            set MLDPn::Sim(GridLon) \[lindex \$MLDPn::Sim(GridSrc) 2\];
            Model::ParamsGridDefine MLDPn"

      checkbutton $tabframe.out.grid.sel -variable Page::Data(ToolMode) -onvalue MLDPn -offvalue SPI \
         -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat \
         -command { SPI::ToolMode $Page::Data(ToolMode) Data True } -selectcolor $GDefs(ColorFrame)
      pack $tabframe.out.grid.res -side left -ipady 1
      pack $tabframe.out.grid.src -side left -ipady 1 -fill x -expand True
      pack $tabframe.out.grid.sel -side left
      pack $tabframe.out.grid     -side top -fill x -padx 2

      #----- Output delta [hr].
      Option::Create $tabframe.out.dt [lindex $Lbl(OutDT) $GDefs(Lang)] MLDPn::Sim(OutDT) 1 -1 $MLDPn::Sim(OutDTs) MLDPn::ParamsLevels
      pack $tabframe.out.dt -side top -fill x -padx 2

      #----- Output var.
      Option::Create $tabframe.out.var [lindex $Lbl(OutVAR) $GDefs(Lang)] MLDPn::Sim(OutVar) -1 -1 $MLDPn::Sim(OutVars) MLDPn::ParamsLevels
      pack $tabframe.out.var -side top -fill x -padx 2
 
      #----- Vertical levels CV [m].
      Option::Create $tabframe.out.cvlevels [lindex $Lbl(OutCV) $GDefs(Lang)] MLDPn::Sim(OutCV) 1 -1 $MLDPn::Sim(OutCVs) MLDPn::UpdateListVerticalLevels
      pack $tabframe.out.cvlevels -side top -ipady 1 -fill x -padx 2
      bind $tabframe.out.cvlevels.e <Key-Tab>  "MLDPn::UpdateListVerticalLevels"
      bind $tabframe.out.cvlevels.e <Button-1> "MLDPn::UpdateListVerticalLevels"

      #----- Vertical levels AV [m].
      Option::Create $tabframe.out.avlevels [lindex $Lbl(OutAV) $GDefs(Lang)] MLDPn::Sim(OutAV) 1 -1  $MLDPn::Sim(OutAVs) ""
      pack $tabframe.out.avlevels -side top -ipady 1 -fill x -padx 2

      Bubble::Create $tabframe.out.grid.res $Bubble(OutGrid)
      Bubble::Create $tabframe.out.dt       $Bubble(OutDT)
      Bubble::Create $tabframe.out.var      $Bubble(OutVAR)
      Bubble::Create $tabframe.out.cvlevels $Bubble(OutCV)
      Bubble::Create $tabframe.out.avlevels $Bubble(OutAV)

   MLDPn::ParamsLevels
   pack $tabframe.out -side top -padx 5 -pady 5 -fill x
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::ParamsLevels>
# Creation   : Decembre 2013 - J.P. Gauthier - CMC/CMOE
#
# But        : Activer/Desactiver la liste des niveaux selon l'activation des variables.
#
# Parametres :
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------
proc MLDPn::ParamsLevels { } {
   variable Sim
   
   if { [lsearch -exact $Sim(OutVar) CV]==-1 } {
      Option::Disable $Sim(TabframeOut).out.cvlevels
   } else {
      Option::Enable $Sim(TabframeOut).out.cvlevels   
   }
   
   if { [lsearch -exact $Sim(OutVar) AV]==-1 } {
      Option::Disable $Sim(TabframeOut).out.avlevels
   } else {
      Option::Enable $Sim(TabframeOut).out.avlevels
   }
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::ParamsEmission>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Cree l'onglet concernant le scenario d'emission.
#
# Parametres :
#   <Tab>    : Boite d'onglet dans laquelle ajouter les onglets
#   <Auto>   : Mode Auto
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::ParamsEmission { Tab { Mode NEW } } {
   global GDefs
   variable Sim
   variable Msg
   variable Lbl
   variable Bubble

   #----- Get the list of available scenarios.
   if { [MLDPn::ScenarioList] } {
      MLDPn::ScenarioSelect
   }

   set tabframe [TabFrame::Add $Tab 1 "[lindex $Lbl(Emission) $GDefs(Lang)]" False]
   set Sim(TabframeEmission) $tabframe
   set Sim(ScenarioTabNo) [TabFrame::Current $Tab]

   #----- Scenario management
   set Sim(ScenarioFrame) $tabframe.scenariomgnt
   labelframe $tabframe.scenariomgnt -text "[lindex $Lbl(ScenarioMgnt) $GDefs(Lang)]"
      #----- Release scenario name.
      frame $tabframe.scenariomgnt.name
         label $tabframe.scenariomgnt.name.lbl -relief flat -anchor w -text "[lindex $Lbl(ScenarioName) $GDefs(Lang)]"
         ComboBox::Create $tabframe.scenariomgnt.name.ent MLDPn::Sim(EmScenario) noedit sorted nodouble -1 $MLDPn::Sim(EmList) 10 3 "MLDPn::ScenarioSelect"
         pack $tabframe.scenariomgnt.name.lbl -side left -padx 2
         pack $tabframe.scenariomgnt.name.ent -side left -padx 2 -fill x -expand true
         Bubble::Create $tabframe.scenariomgnt.name.lbl $Bubble(ScenarioName)
         Bubble::Create $tabframe.scenariomgnt.name.ent $Bubble(ScenarioSelect)
      pack $tabframe.scenariomgnt.name -side top -anchor w -padx 2 -fill x

      #----- Buttons.
      frame $tabframe.scenariomgnt.button
         button $tabframe.scenariomgnt.button.new -text "[lindex $Lbl(Clear) $GDefs(Lang)]" -bd 1 -command "MLDPn::ScenarioClear"
         button $tabframe.scenariomgnt.button.save -text "[lindex $Lbl(Save) $GDefs(Lang)]" -bd 1 -command "MLDPn::ScenarioPromptSave True"
         button $tabframe.scenariomgnt.button.delete -text "[lindex $Lbl(Suppress) $GDefs(Lang)]" -bd 1 -command "MLDPn::ScenarioDelete"
         pack $tabframe.scenariomgnt.button.new $tabframe.scenariomgnt.button.save -side right
         pack $tabframe.scenariomgnt.button.delete -side left
         Bubble::Create $tabframe.scenariomgnt.button.new $Bubble(ScenarioClear)
         Bubble::Create $tabframe.scenariomgnt.button.save $Bubble(ScenarioSave)
         Bubble::Create $tabframe.scenariomgnt.button.delete $Bubble(ScenarioDelete)
      pack $tabframe.scenariomgnt.button -side top -anchor w -padx 5 -pady 10 -fill x -expand true
   pack $tabframe.scenariomgnt -side top -padx 5 -fill x -pady 5
   Bubble::Create $tabframe.scenariomgnt $Bubble(Scenario)

   #----- Emission Scenario
   labelframe $tabframe.scenario -text "[lindex $Lbl(Scenario) $GDefs(Lang)]"
      #----- Button + Slider
      frame $tabframe.scenario.ctrl
         #----- Add button
         button $tabframe.scenario.ctrl.add -text "[lindex $Lbl(Add) $GDefs(Lang)]" -bd 1 -command "MLDPn::EmissionIntervalAdd"
         pack $tabframe.scenario.ctrl.add -side right -anchor e
         Bubble::Create $tabframe.scenario.ctrl.add $Bubble(EmissionIntervalAdd)
         Bubble::Create $tabframe.scenario.ctrl.zoom $Bubble(GraphZoom)
         #----- Interval picker
         set Sim(PickInterval) [lindex $Lbl(EditInter) $GDefs(Lang)]
         ComboBox::Create $tabframe.scenario.ctrl.pick MLDPn::Sim(PickInterval) noedit unsorted nodouble -1 [MLDPn::GetEmissionIntervalNumberList] \
            20 5 "MLDPn::EmissionIntervalEdit \[expr \$MLDPn::Sim(PickInterval)-1\]; set MLDPn::Sim(PickInterval) \"[lindex $Lbl(EditInter) $GDefs(Lang)]\""
         pack $tabframe.scenario.ctrl.pick -side right -anchor e -padx 2
         Bubble::Create $tabframe.scenario.ctrl.pick $Bubble(PickInterval)
         trace add variable MLDPn::Sim(EmNbIntervals) write MLDPn::UpdateIntervalPickerList
         #----- Slider
         scale $tabframe.scenario.ctrl.zoom -orient horizontal -showvalue False -length 45 -sliderlength 10 -variable MLDPn::Sim(Zoom) -resolution 0.1 -from 1 -to 10 -command "MLDPn::GraphZoom"
         pack $tabframe.scenario.ctrl.zoom -side left -anchor w -fill x -expand True
      pack $tabframe.scenario.ctrl -side bottom -padx 5 -fill x -pady 5

      #---- Canvas (graph)
      glcanvas $tabframe.scenario.graph -xscrollcommand "$tabframe.scenario.scrollx set"
      pack $tabframe.scenario.graph -side top -fill both -expand True -padx 5 -pady 2
      set Sim(ESCanvas) $tabframe.scenario.graph
      #----- Scrollbar
      scrollbar $tabframe.scenario.scrollx -orient horizontal -bd 1 -width 10 -command "$tabframe.scenario.graph xview"
      pack $tabframe.scenario.scrollx -side top -fill x -padx 5
   pack $tabframe.scenario -side top -padx 5 -pady 5 -fill both -expand True
   #Bubble::Create $tabframe.scenario $Bubble(EmissionScenario)

   if { $Mode=="CONT" } {
      #----- Disable scenario management buttons

      $Sim(ScenarioFrame).button.new configure -state disabled
      $Sim(ScenarioFrame).button.delete configure -state disabled
      ComboBox::Disable $Sim(ScenarioFrame).name.ent
   }

   #----- Create histogram

   if { ![graphitem is ES_SIMDUR] } {
      graphaxis create ES_AXISX
      graphaxis create ES_AXISY

      vector    create ES_INTERX
      vector    create ES_INTERY
      graphitem create ES_INTER

      vector    create ES_SIMDURX
      vector    create ES_SIMDURY
      graphitem create ES_SIMDUR
   }

   graphitem configure ES_INTER -type HISTOGRAM -outline black -width 1 -font XFONT12 -fill white \
      -xaxis ES_AXISX -yaxis ES_AXISY -xdata ES_INTERX -ydata ES_INTERY
   graphitem configure ES_SIMDUR -type HISTOGRAM -outline #BAECFF -width 1 -font XFONT12 -fill #E6F8FF -stipple @$GDefs(Dir)/share/bitmap/stipple8-32.xbm \
      -xaxis ES_AXISX -yaxis ES_AXISY -xdata ES_SIMDURX -ydata ES_SIMDURY

   graphaxis configure ES_AXISX -format NONE -width 1 -highoffset 2 -gridcolor "#000000" -gridwidth 1 -dash . -format T+HHMM -spacing 20
   graphaxis configure ES_AXISY -format INTEGER -width 1 -unit "[lindex $Lbl(AxisY) $GDefs(Lang)]" -highoffset 2

   $Sim(ESCanvas) create graph -x 0 -y 0 -anchor nw -legend False -item { ES_INTER ES_SIMDUR } -tags ES_GRAPH -command GRAPH_CMD

   set Sim(BubbleId) [OneTimeBubble::Create]

   bind $Sim(ESCanvas) <Double-Button-1> "MLDPn::GraphAction dblclk %x %y %X %Y"
   bind $Sim(ESCanvas) <Motion> "MLDPn::GraphAction motion %x %y %X %Y"
   bind $Sim(ESCanvas) <Leave> "OneTimeBubble::Hide $MLDPn::Sim(BubbleId); set MLDPn::Sim(BubbleShowingId) -1"
   bind $Sim(ESCanvas) <Configure> "$Sim(ESCanvas) configure -width %w -height %h; $Sim(ESCanvas) itemconfigure ES_GRAPH -width %w -height %h"
   bind $Sim(ESCanvas) <ButtonPress-4>   { set MLDPn::Sim(Zoom) [expr $MLDPn::Sim(Zoom)+0.1]; MLDPn::GraphZoom $MLDPn::Sim(Zoom) }
   bind $Sim(ESCanvas) <ButtonPress-5>   { set MLDPn::Sim(Zoom) [expr $MLDPn::Sim(Zoom)-0.1]; MLDPn::GraphZoom $MLDPn::Sim(Zoom) }

   graphaxis configure ES_AXISX -highlight $Sim(RestartDeltas) -highlightcolor #FF0000 -highlightwidth 2

   MLDPn::GraphUpdate
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::ParamsCont>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Interface pour la continuation d'une simulation (restart).
#
# Parametres :
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::ParamsCont { Parent } {
   variable Sim

   #----- Model tab

   MLDPn::ParamsNew $Parent

   #----- Disable params that shouldn't be modified

   #----- Start time
   Calendar::Disable $Sim(TabframeModel).time.date
   Clock::Disable $Sim(TabframeModel).time.date.hour
   
   $Sim(TabframeModel).time.mode.opt.forward   configure -state disabled
   $Sim(TabframeModel).time.mode.opt.backward  configure -state disabled

   Option::Create  $Sim(TabframeModel).time.restart "Restart" MLDPn::Sim(Restart) 0 -1 $Sim(Restarts) \
      { set MLDPn::Sim(RestartDelta) [lindex $MLDPn::Sim(RestartDeltas) [lsearch -exact $MLDPn::Sim(Restarts) $MLDPn::Sim(Restart)]]; MLDPn::GraphUpdate }
   pack  $Sim(TabframeModel).time.restart -side bottom -fill x -padx 2

   #----- Integration
   Option::Disable $Sim(TabframeModel).integ.ots
   Option::Disable $Sim(TabframeModel).integ.mts

   #----- Other
   if { $Sim(SrcType) == "VIRUS" } {
      Option::Disable $Sim(TabframeModel).others.virus
   }

#   Option::Disable $Sim(TabframeModel).others.met.model
#   Option::Disable $Sim(TabframeModel).others.met.delta
#   $Sim(TabframeModel).others.met.path configure -state disabled
   Option::Disable $Sim(TabframeModel).others.varmesoscale
   Option::Disable $Sim(TabframeModel).others.timescale
   Option::Disable $Sim(TabframeModel).others.reflect
   Option::Disable $Sim(TabframeModel).others.seed 
   Option::Disable $Sim(TabframeModel).others.aero 
   Option::Disable $Sim(TabframeModel).others.sca
   Option::Disable $Sim(TabframeModel).others.dep

   $Sim(TabframeOut).out.grid.sel configure -state disabled
   Option::Disable $Sim(TabframeOut).out.grid.res
   Option::Disable $Sim(TabframeOut).out.grid.src
   Option::Disable $Sim(TabframeOut).out.dt
   Option::Disable $Sim(TabframeOut).out.var
   Option::Disable $Sim(TabframeOut).out.cvlevels
   Option::Disable $Sim(TabframeOut).out.avlevels
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::IsEmissionToggle>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Met a jour l'interface graphique des details d'un interval
#              en cachant/montrant les parametres variables d'emission.
#
# Parametres :
#   <Id>     : Id de l'interval d'emission a mettre a jour.
#   <Base>   : Path de base dans l'arborescence Tk
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::IsEmissionToggle { Id Base } {
   variable Tmp
   variable Sim

   if { $Tmp(EmIsEm.$Id) } {
      pack $Base.emdetails -after $Base.isemission

      if { $Sim(SrcType) == "ACCIDENT" } {
         pack .emintdetails.frm.iso -after .emintdetails.frm.emint -side top -anchor w -padx 5 -fill x -pady 5
      }
   } else {
      pack forget $Base.emdetails

      if { $Sim(SrcType) == "ACCIDENT" } {
         pack forget .emintdetails.frm.iso

         #----- Set rates to 0 because it is not an emission interval
         for { set i 0 } { $i < $Tmp(EmNbIso) } { incr i } {
            set Tmp(EmRate.$Id.$i) 0.0
         }
      }
   }
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::EmInterModeToggle>
# Creation   : Aout 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Change le mode ("(h)" ou "(s)") de la durée de l'intervalle et
#              met a jour l'interface graphique.
#
# Parametres :
#   <Base>   : Path de base dans l'arborescence Tk
#   <Id>     : Id de l'intervalle d'emission a mettre a jour.
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::EmInterModeToggle { Base Id } {
   global GDefs
   variable Sim
   variable Tmp
   variable Lbl

   if { $Sim(EmInterMode)==3600 } {
      #----- (h) to (s)
      set Sim(EmInterMode) 1
      set Tmp(EmInter.$Id) [expr int($Tmp(EmInter.$Id)*3600)]
   } else {
      #----- (s) to (h)
      set Sim(EmInterMode) 3600
      set Tmp(EmInter.$Id) [expr $Tmp(EmInter.$Id)/3600.0]
   }

   $Base.unt configure -text [lindex $Lbl(EmInterUnit$Sim(EmInterMode)) $GDefs(Lang)]
   update idletasks
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::EmRateModeToggle>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Change le mode ("unité" ou "unité/h") du taux d'émission et
#              met a jour l'interface graphique.
#
# Parametres :
#   <Base>   : Path de base dans l'arborescence Tk
#   <Id>     : Id de l'interval d'emission a mettre a jour.
#   <Idx>    : Index de l'isotope a mettre à jour.
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::EmRateModeToggle { Base Id { Idx "" } } {
   global GDefs
   variable Tmp
   variable Lbl

   if { $Idx == "" } {
      set tag  $Id
      set base $Base
   } else {
      set tag  $Id.$Idx
      set base $Base.$Idx
   }

   #----- toggle between the two mode
   set Tmp(EmRateMode.$tag) [expr ($Tmp(EmRateMode.$tag) + 1) % 2]

   #----- Update value
   if { $Tmp(EmRateMode.$tag) } {
      #----- Change value from "unit/h" to "unit"
      set Tmp(EmRate.$tag) [format "%.7g" [expr double($Tmp(EmRate.$tag)) * $Tmp(EmInter.$Id)]]
   } else {
      #----- Change value from "unit" to "unit/h"
      set Tmp(EmRate.$tag) [format "%.7g" [expr double($Tmp(EmRate.$tag)) / $Tmp(EmInter.$Id)]]
   }

   #----- Update toggle button's text
   $base.unt configure -text [lindex $Lbl(ReleaseRateUnit$Tmp(EmRateMode.$tag)) $GDefs(Lang)]
   update idletasks
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::IsoDetailsUpdate>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Effectue la mise a jour de l'interface graphique des isotopes.
#
# Parametres :
#   <Id> : Id de l'interval d'emission a mettre a jour.
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::IsoDetailsUpdate { Id } {
   global GDefs
   variable Bubble
   variable Tmp
   variable Lbl

   set base .emintdetails.frm.iso
   if { ![winfo exists $base] } {
      return
   }

   for { set i 0 } { $i < $Tmp(EmNbIso) } { incr i } {
      #----- If frame already exists, update it
      if { [winfo exists $base.$i] } {
         $base.$i.btn configure -text $Tmp(EmIso$i)
         $base.$i.unt configure -text "[lindex $Lbl(ReleaseRateUnit$Tmp(EmRateMode.$Id.$i)) $GDefs(Lang)]"
      } else {
         #----- Create the frame
         frame $base.$i
            button $base.$i.del -relief flat -anchor w -bitmap @$GDefs(Dir)/share/bitmap/cvdel.xbm -command "MLDPn::IsoDelete $i $Id; MLDPn::IsoDetailsUpdate $Id"
            button $base.$i.btn -relief flat -anchor w -text $Tmp(EmIso$i) -command "IsoBox::Create .emintdetails \"MLDPn::IsoUpdate $i\"; MLDPn::IsoDetailsUpdate $Id" -width 8
            entry $base.$i.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmRate.${Id}.$i)
            button $base.$i.unt -relief flat -text "[lindex $Lbl(ReleaseRateUnit$Tmp(EmRateMode.$Id.$i)) $GDefs(Lang)]" -command "MLDPn::EmRateModeToggle $base $Id $i" -width 7
            pack $base.$i.del $base.$i.btn -side left -padx 2 -anchor w
            pack $base.$i.unt -side right -anchor e -padx 2
            pack $base.$i.ent -side left -anchor center
            Bubble::Create $base.$i.del $Bubble(IsoDelete)
            Bubble::Create $base.$i.btn $Bubble(Isotope)
            Bubble::Create $base.$i.ent $Bubble(ReleaseRateIso)
         pack $base.$i -before $base.button -side top -anchor w
      }
   }

   #----- Destroy unused frames
   while { [winfo exists $base.$i] } {
      destroy $base.$i
      incr i
   }
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::EmissionDetails>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Création de l'interface graphique d'ajout/d'édition des
#              intervals d'émission.
#
# Parametres :
#   <Parent> : Path du parent dans l'arborescence graphique.
#   <Id>     : Id de l'interval d'emission.
#   <Type>   : Type de fenetre ("Edit" rajoute le bouton de suppression de
#              l'interval)
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::EmissionDetails { Parent Id { Type "" } } {
   global GDefs
   variable Sim
   variable Tmp
   variable Msg
   variable Lbl
   variable Bubble

   toplevel     .emintdetails
   wm title     .emintdetails "[lindex $Lbl(Scenario) $GDefs(Lang)] : [lindex $Lbl(Interval) $GDefs(Lang)] #[expr $Id+1]"
   wm transient .emintdetails $Parent
   wm resizable .emintdetails 0 0
   wm geom      .emintdetails =320x410+[expr [winfo rootx $Parent]+330]+[expr [winfo rooty $Parent]+30]
   wm protocol  .emintdetails WM_DELETE_WINDOW { }
   bind .emintdetails <Unmap> "grab release .emintdetails"

   frame .emintdetails.frm

   #----- Emission global parameters.
   set base .emintdetails.frm.glb
   set Sim(EmissionGlobalFrame) $base
   labelframe .emintdetails.frm.glb -text "[lindex $Lbl(GlobalParameters) $GDefs(Lang)]"

      if { $MLDPn::Sim(SrcType) == "VOLCANO" || $MLDPn::Sim(SrcType) == "FIRE" } {
         #----- Particles size distribution.
         Option::Create  $base.size [lindex $Lbl(EmSizeDist) $GDefs(Lang)] MLDPn::Tmp(EmSizeDist) \
            0 -1 [lindex $MLDPn::Sim(ListEmSizeDist) $GDefs(Lang)] ""
         pack $base.size -side top -anchor w -padx 2 -fill x
         Bubble::Create $base.size $Bubble(EmSizeDist)

         #----- Density [microgram/m3].
         frame $base.density
            label $base.density.lbl -relief flat -anchor w -text "[lindex $Lbl(EmDensity) $GDefs(Lang)]"
            entry $base.density.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmDensity) -width 60
            pack $base.density.lbl $base.density.ent -side left
         pack $base.density -side top -anchor w -padx 2 -fill x
         Bubble::Create $base.density $Bubble(EmDensity)
      }

      #----- Source height (MAGL).
      frame $base.height
         label $base.height.lbl -relief flat -anchor w -text "[lindex $Lbl(SrcHeight) $GDefs(Lang)]"
         entry $base.height.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(Height) -width 60
         pack $base.height.lbl -side left
         pack $base.height.ent -side left -fill x -expand 1
      pack $base.height -side top -anchor w -padx 2 -fill x
      Bubble::Create $base.height $Bubble(Height)

      #----- Plume vertical distribution.
      Option::Create  $base.vertical [lindex $Lbl(EmVerticalDist) $GDefs(Lang)] MLDPn::Tmp(EmVerticalDist) \
         0 -1 [lindex $MLDPn::Sim(ListEmVerticalDist) $GDefs(Lang)] ""
      pack $base.vertical -side top -anchor w -padx 2 -fill x
      Bubble::Create $base.vertical $Bubble(EmVerticalDist)

      if { $Sim(Restart)=="" } {
         #----- Automatic distribution of particules
         checkbutton $base.isautonp -anchor w -text "[lindex $Lbl(EmIsAutoNP) $GDefs(Lang)]" -variable MLDPn::Tmp(EmIsAutoNP) -indicatoron True -selectcolor $GDefs(ColorLight) \
            -command "if { \$MLDPn::Tmp(EmIsAutoNP) } {
                        pack $base.nbpart -after $base.isautonp -side top -anchor w -padx 2 -fill x
                        Bubble::Create $base.nbpart \{ $MLDPn::Bubble(EmNumberParticles)] \}
                        .emintdetails.frm.emint.emdetails.nbpart.ent configure -state disabled
                     } else {
                        pack forget $base.nbpart
                        .emintdetails.frm.emint.emdetails.nbpart.ent configure -state normal
                     }"
         pack $base.isautonp -side top -anchor w -padx 2
         Bubble::Create $base.isautonp $Bubble(EmIsAutoNP)
      }

      #----- Global number of particles to be distributed amongst emission intervals
      frame $base.nbpart
         label $base.nbpart.lbl -relief flat -anchor w -text "[lindex $Lbl(EmNumberParticles) $GDefs(Lang)]"
         entry $base.nbpart.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmNumberParticles) -width 60
         pack $base.nbpart.lbl $base.nbpart.ent -side left
         Bubble::Create $base.nbpart $Bubble(EmNumberParticles)

      if { $Sim(EmIsAutoNP) } {
         pack $base.nbpart -side top -anchor w -padx 2 -fill x
         Bubble::Create $base.nbpart $Bubble(EmNumberParticles)
      }
   pack $base -side top -padx 5 -pady 5 -fill x
   Bubble::Create $base $Bubble(EmissionGlobal)

   #----- Emission interval details
   labelframe .emintdetails.frm.emint -text "[lindex $Lbl(IntervalParameters) $GDefs(Lang)]"
   set base .emintdetails.frm.emint
   set Sim(EmissionIntervalFrame) $base

      #----- Duration
      frame $base.duration
         label $base.duration.lbl -relief flat -anchor w -text "[lindex $Lbl(EmInter) $GDefs(Lang)]"
         entry $base.duration.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmInter.$Id) -width 60
         button $base.duration.unt -relief flat -width 3 -text "[lindex $Lbl(EmInterUnit$Sim(EmInterMode)) $GDefs(Lang)]" -command "MLDPn::EmInterModeToggle $base.duration $Id"
         pack $base.duration.lbl -side left
         pack $base.duration.unt -side right
         pack $base.duration.ent -side left -fill x -expand 1
      pack $base.duration -side top -anchor w -padx 2 -fill x
      Bubble::Create $base.duration $Bubble(DurationInter)

      #----- IsEmission
      set Tmp(EmIsEmLabel.$Id) [lindex [lindex $Sim(ListEmissionLabel) $GDefs(Lang)] [expr $Tmp(EmIsEm.$Id) ? 0 : 1]]
      Option::Create $base.isemission "[lindex $Lbl(EmIsEm) $GDefs(Lang)]" [list MLDPn::Tmp(EmIsEmLabel.$Id) MLDPn::Tmp(EmIsEm.$Id)] False -1 \
         [lindex $Sim(ListEmissionLabel) $GDefs(Lang)] "MLDPn::IsEmissionToggle $Id $base" $Sim(ListEmissionValue)
      pack $base.isemission -side top -anchor w -padx 2 -fill x
      Bubble::Create $base.isemission $Bubble(EmIsEm)

      frame $base.emdetails
         #----- Number of particles.
         frame $base.emdetails.nbpart
            label $base.emdetails.nbpart.lbl -relief flat -anchor w -text "[lindex $Lbl(EmNumberParticles) $GDefs(Lang)]"
            entry $base.emdetails.nbpart.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmNumberParticles.$Id) -width 60
            if { $Sim(EmIsAutoNP) } {
               $base.emdetails.nbpart.ent configure -state disabled
            }
            bind $base.emdetails.nbpart.ent <Button-1> "MLDPn::ComputeMass $Id"
            pack $base.emdetails.nbpart.lbl $base.emdetails.nbpart.ent -side left
         pack $base.emdetails.nbpart -side top -anchor w -padx 2 -fill x
         Bubble::Create $base.emdetails.nbpart $Bubble(EmNumberParticles)

         #----- Maximum plume height [m].
         Option::Create  $base.emdetails.height [lindex $Lbl(EmHeight) $GDefs(Lang)] [list MLDPn::Tmp(EmHeight.$Id) MLDPn::Tmp(EmHeightMode)] \
             1 -1 [lindex $MLDPn::Sim(ListEmHeightMode) $GDefs(Lang)] "set  MLDPn::Tmp(EmHeight.$Id) \[Model::ComputeKaboomHeight\]; MLDPn::ComputeMass $Id"
         pack $base.emdetails.height -side top -anchor w -padx 2 -fill x
         Bubble::Create $base.emdetails.height $Bubble(EmHeight)

         #----- Column radius [m].
         frame $base.emdetails.radius
            label $base.emdetails.radius.lbl -relief flat -anchor w -text "[lindex $Lbl(EmRadius) $GDefs(Lang)]"
            entry $base.emdetails.radius.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmRadius.$Id) -width 60
            pack $base.emdetails.radius.lbl $base.emdetails.radius.ent -side left
         pack $base.emdetails.radius -side top -anchor w -padx 2 -fill x
         Bubble::Create $base.emdetails.radius $Bubble(EmRadius)

         #----- Total released mass [microgram].
         if { $Sim(SrcType) == "VOLCANO" || $Sim(SrcType) == "FIRE" } {
            Option::Create $base.emdetails.mass [lindex $Lbl(EmMass) $GDefs(Lang)] [list MLDPn::Tmp(EmMass.$Id) MLDPn::Tmp(EmMassMode.$Id)] \
                0 -1 [lindex $MLDPn::Sim(ListEmMassMode) $GDefs(Lang)] "MLDPn::EnableDisableMass $base.emdetails.mass $Id"
            #----- Configure background and foreground entry wigdet when disabled.
            $base.emdetails.mass.e configure -state disabled -disabledbackground $GDefs(ColorFrame) -disabledforeground $GDefs(ColorOff)
            bind $base.emdetails.mass.e <Button-1> "MLDPn::ComputeMass $Id"
            bind $base.emdetails.mass.b <Button-1> "MLDPn::ComputeMass $Id"
            pack $base.emdetails.mass -side top -anchor w -padx 2 -fill x
            Bubble::Create $base.emdetails.mass $Bubble(MassMode)
         } elseif { $Sim(SrcType) == "VIRUS" } {
            frame $base.emdetails.rate
               label $base.emdetails.rate.lbl -relief flat -anchor w -text "[lindex $Lbl(ReleaseRate) $GDefs(Lang)]"
               entry $base.emdetails.rate.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmRate.$Id)
               button $base.emdetails.rate.unt -relief flat -text "[lindex $Lbl(ReleaseRateUnit$Tmp(EmRateMode.$Id)) $GDefs(Lang)]" -command "MLDPn::EmRateModeToggle $base.emdetails.rate $Id" -width 7
               pack $base.emdetails.rate.lbl -side left -anchor w
               pack $base.emdetails.rate.unt -side right -anchor e
               pack $base.emdetails.rate.ent -side left -anchor center
            pack $base.emdetails.rate -side top -anchor w -padx 2 -fill x
         }
      if { $Tmp(EmIsEm.$Id) } {
         pack $base.emdetails
      }

   pack $base -side top -anchor w -padx 5 -fill x -pady 7
   Bubble::Create $base $Bubble(EmissionDetails)

   #----- Isotopes release rate
   if { $Sim(SrcType) == "ACCIDENT" } {
      labelframe .emintdetails.frm.iso -text "Isotopes"
      set base .emintdetails.frm.iso
      set Sim(EmissionIsoFrame) $base

      #----- Isotop chain.
      checkbutton $base.chain -anchor w -text "[lindex $Lbl(IsoChain) $GDefs(Lang)]" -variable MLDPn::Sim(IsoChain) -indicatoron True -selectcolor $GDefs(ColorLight)
      pack $base.chain -side top -anchor w -padx 2 -fill x
      Bubble::Create $base.chain $Bubble(IsoChain)

      for { set i 0 } { $i < $Tmp(EmNbIso) } { incr i } {
         frame $base.$i
            button $base.$i.del -relief flat -anchor w -bitmap @$GDefs(Dir)/share/bitmap/cvdel.xbm -command "MLDPn::IsoDelete $i $Id; MLDPn::IsoDetailsUpdate $Id"
            button $base.$i.btn -relief flat -anchor w -text $Tmp(EmIso$i) -command "IsoBox::Create .emintdetails \"MLDPn::IsoUpdate $i\"; MLDPn::IsoDetailsUpdate $Id" -width 8
            entry  $base.$i.ent -relief sunken -bd 1 -bg $GDefs(ColorLight) -textvariable MLDPn::Tmp(EmRate.${Id}.$i)
            button $base.$i.unt -relief flat -text "[lindex $Lbl(ReleaseRateUnit$Tmp(EmRateMode.$Id.$i)) $GDefs(Lang)]" -command "MLDPn::EmRateModeToggle $base $Id $i" -width 7
            pack $base.$i.del $base.$i.btn -side left -padx 2 -anchor w
            pack $base.$i.unt -side right -anchor e -padx 2
            pack $base.$i.ent -side left -anchor center
            Bubble::Create $base.$i.del $Bubble(IsoDelete)
            Bubble::Create $base.$i.btn $Bubble(Isotope)
            Bubble::Create $base.$i.ent $Bubble(ReleaseRateIso)
         pack $base.$i -side top -anchor w
      }
      frame $base.button
         button $base.button.add -text "[lindex $Lbl(Add) $GDefs(Lang)]" -bd 1 -command "IsoBox::Create .emintdetails MLDPn::IsoAdd; MLDPn::IsoDetailsUpdate $Id"
         pack $base.button.add -side right
         Bubble::Create $base.button.add $Bubble(IsoAdd)
      pack $base.button -side top -anchor w -padx 2 -fill x -pady 2

      if { $Tmp(EmIsEm.$Id) } {
         pack $base -side top -anchor w -padx 5 -fill x -pady 5
         Bubble::Create $base $Bubble(Isotopes)
      }
   }

   pack .emintdetails.frm -side top

   #----- 'Apply' and 'Cancel' buttons
   frame .emintdetails.buttons
      button .emintdetails.buttons.cancel -text "[lindex $Lbl(Cancel) $GDefs(Lang)]" -bd 1 -command "destroy .emintdetails"
      button .emintdetails.buttons.apply -text "[lindex $Lbl(Apply) $GDefs(Lang)]" -bd 1 \
         -command "if { \[MLDPn::ValidateEmissionIntervalDetails $Id\] } {
                      MLDPn::EmissionIntervalApply $Id
                      destroy .emintdetails
                   }"
      if { $Type == "Edit" } {
         button .emintdetails.buttons.suppress -text "[lindex $Lbl(Suppress) $GDefs(Lang)]" -bd 1 -command "MLDPn::EmissionIntervalDelete $Id; destroy .emintdetails"
         pack .emintdetails.buttons.suppress -side left
      }
      pack .emintdetails.buttons.apply .emintdetails.buttons.cancel -side right
      Bubble::Create .emintdetails.buttons.cancel $Bubble(Cancel)
      Bubble::Create .emintdetails.buttons.apply $Bubble(Apply)
   pack .emintdetails.buttons -side bottom -anchor w -padx 5 -fill x -pady 5

   #----- Disable some options if it is a restart
   if { $Sim(Restart)!="" } {
      if { $Sim(SrcType) == "ACCIDENT" } {
         for { set j 0 } { $j < $Sim(EmNbIso) } { incr j } {
            $base.$j.del configure -state disabled
            $base.$j.btn configure -state disabled
         }
         $base.button.add configure -state disabled
      }
   }

   update idletasks
   grab .emintdetails

   MLDPn::ComputeMass $Id
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::GraphAction>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Gere les differentes actions accomplies sur le graphique des
#              intervals d'emission.
#
# Parametres :
#   <Type>   : Le type d'action accomplie (motion, mouse double-click)
#   <X>      : La coordonnee en X de l'action sur le canvas.
#   <Y>      : La coordonnee en Y de l'action sur le canvas.
#   <RootX>  : La coordonnee en X par rapport a la racine de l'ecran.
#   <RootY>  : La coordonnee en Y par rapport a la racine de l'ecran.
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::GraphAction { Type X Y RootX RootY } {
   global GDefs
   variable Sim
   variable Lbl

   #----- Get x value on the graph
   set x [lindex [split [GRAPH_CMD -unproject $X $Y False ES_INTER]] 0]
   if { $x == "" } {
      OneTimeBubble::Hide $Sim(BubbleId)
      set Sim(BubbleShowingId) -1
      return
   }

   #----- Search id of the item under the cursor
   set dt   [expr $Sim(AccSecs)-$Sim(Sim0Secs)]
   set time $dt
   set id 0
   while { $id < $Sim(EmNbIntervals) } {
      set time [expr $time + $Sim(EmInter.$id)]
      if { $x>$dt && $x <= $time } {
         break
      }
      incr id
   }

   #----- Is id valid ?
   if { $id >= $Sim(EmNbIntervals) } {
      OneTimeBubble::Hide $Sim(BubbleId)
      set Sim(BubbleShowingId) -1
      return
   }

   switch $Type {
      "dblclk" {
         #----- Show edit window if interval is editable
         if { $id >= $Sim(EmInterStart) } {
            OneTimeBubble::Hide $Sim(BubbleId)
            set Sim(BubbleShowingId) -1
            MLDPn::EmissionIntervalEdit $id
         }
      }
      "motion" {
         #----- If this is what the bubble is already showing
         if { $Sim(BubbleShowingId) == $id } {
            OneTimeBubble::Show $Sim(BubbleId) $RootX $RootY
            return
         }

         #----- Generate bubble text
         set bubtxt ""
         append bubtxt "*** [lindex $Lbl(GlobalParameters) $GDefs(Lang)] ***\n"
         if { $Sim(SrcType) == "VOLCANO" || $Sim(SrcType) == "FIRE" } {
            append bubtxt "[lindex $Lbl(EmSizeDist) $GDefs(Lang)] : $Sim(EmSizeDist)\n"
            append bubtxt "[lindex $Lbl(EmDensity) $GDefs(Lang)] : $Sim(EmDensity)\n"
         }
         append bubtxt "[lindex $Lbl(EmVerticalDist) $GDefs(Lang)] : $Sim(EmVerticalDist)\n"
         if { $Sim(EmIsAutoNP) && $id>=$Sim(EmInterStart) } {
            append bubtxt "[lindex $Lbl(EmNumberParticles) $GDefs(Lang)] : $Sim(EmNumberParticles)\n"
         }

         append bubtxt "\n*** [lindex $Lbl(IntervalParameters) $GDefs(Lang)] #[expr $id+1] ***\n"
         if { $Sim(EmInterMode)==3600 } {
            append bubtxt "[format "%-.22s" [lindex $Lbl(EmInter) $GDefs(Lang)]](h) : [expr $Sim(EmInter.$id)/$Sim(EmInterMode)]\n"
         } else {
            append bubtxt "[format "%-.22s" [lindex $Lbl(EmInter) $GDefs(Lang)]](s) : $Sim(EmInter.$id)]\n"
         }
         if { $Sim(EmIsEm.$id) } {
            append bubtxt "[lindex $Lbl(EmNumberParticles) $GDefs(Lang)] : $Sim(EmNumberParticles.$id)\n"
            append bubtxt "[lindex $Lbl(EmHeight) $GDefs(Lang)] : $Sim(EmHeight.$id)\n"
            append bubtxt "[lindex $Lbl(EmRadius) $GDefs(Lang)] : $Sim(EmRadius.$id)\n"
            switch $Sim(SrcType) {
               "FIRE"    -
               "VOLCANO" {
                  append bubtxt "[lindex $Lbl(EmMass) $GDefs(Lang)] : [format %.7g $Sim(EmMass.$id)]\n"
               }
               "VIRUS" {
                  append bubtxt "[format %.20s [lindex $Lbl(ReleaseRate) $GDefs(Lang)]](u/h) : [format %.7g $Sim(EmRate.$id)]\n"
               }
               "ACCIDENT" {
                  set units   [lindex $Lbl(ReleaseRateUnit0) $GDefs(Lang)]
                  set length  [expr 25 - [string length $units]]
                  for { set i 0 } { $i < $Sim(EmNbIso) } { incr i } {
                     append bubtxt "[format %-*s $length $Sim(EmIso$i)]$units : [format %.7g $Sim(EmRate.$id.$i)]\n"
                  }
               }
            }
         } else {
            append bubtxt "[lindex $Lbl(EmIsEm) $GDefs(Lang)] : [lindex [lindex $Sim(ListEmissionLabel) $GDefs(Lang)] 1]"
         }

         #----- Show bubble
         OneTimeBubble::Show $Sim(BubbleId) $RootX $RootY $bubtxt
         set Sim(BubbleShowingId) $id
      }
   }
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::GraphZoom>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Multiplie la taille du graph en X par le facteur specifie en
#              parametre creant ainsi un effet de zoom.
#
# Parametres :
#  <Factor>  : Le facteur par lequel multiplier la taille du graph en X.
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::GraphZoom { Factor } {
   variable Sim

   set width   [expr [$Sim(ESCanvas) cget -width] * $Factor]
   set height  [$Sim(ESCanvas) cget -height]

   $Sim(ESCanvas) configure -scrollregion "0.0 0.0 $width $height" -xscrollincrement 10 -yscrollincrement 10
   $Sim(ESCanvas) itemconfigure ES_GRAPH -width $width -height $height

   update idletasks
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::GraphUpdate>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Effectue la mise a jour du graphique des intervals d'emission.
#
# Parametres :
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::GraphUpdate { } {
   global GDefs
   variable Sim
   variable Lbl

   #----- This case can happen if the .modelnew window was closed before the .emintdetails one
   if { ![vector is ES_INTERX] || ![winfo exists $Sim(ESCanvas)] } {
      return
   }

   vector clear ES_INTERX
   vector clear ES_INTERY
   vector clear ES_SIMDURX
   vector clear ES_SIMDURY

   if { $Sim(EmNbIntervals)<1 } {
      $Sim(ESCanvas) itemconfigure ES_GRAPH -update 1
      update idletasks
      return
   }
   
   MLDPn::ValidateScenarioDuration
   
   set totTime    [expr $Sim(AccSecs)-$Sim(Sim0Secs)]
   set times      $totTime
   set totNP      0
   set totMass    0
   set colors     {}
   set heights    {}
   set nplst      {}
   set curnplst   [MLDPn::DistributeParcels]

   #----- Make the lists of data points
   for { set i 0 } { $i < $Sim(EmNbIntervals) } { incr i } {

      #----- Height (Y axis)
      if { $Sim(EmIsEm.$i) } {
         lappend heights $Sim(EmHeight.$i)
      } else {
         lappend heights 0.0
      }

      #----- Time (X axis)
      set totTime [expr $totTime + $Sim(EmInter.$i)]
      set totNP   [expr $totNP + $Sim(EmNumberParticles.$i)]

      lappend times $totTime
      lappend nplst $Sim(EmNumberParticles.$i)
   }

   #----- Set grey intensity depending on particles repartition (more particules = closer to black)
   set idx 0
   set i 0
   foreach np $nplst {
      if { $Sim(EmIsEm.$i) } {
         set ratio 0.0
         if { $totNP != 0 } {
            set ratio [expr double($np)/double($totNP)]
         }
         set color [expr 255 - int($ratio*255.0)]
      } else {
         set color 255
      }

      lappend colors [format "#%02X%02X%02X" $color $color $color]
      incr i
   }

   #----- refill vectors with updated values
   vector append ES_INTERX $times
   vector append ES_INTERY $heights

   #----- Set simulation length indicator
   if { $Sim(Restart)!="" } {
      lappend times [set time [expr $Sim(RestartDelta) + $Sim(Duration)*3600]]
      vector append ES_SIMDURX [list 0.0 $Sim(RestartDelta) $time]
      vector append ES_SIMDURY [list [vector stats ES_INTERY -max] [vector stats ES_INTERY -max]]
      set durcolors { #00B7FF #D9F5FF }
   } else {
      lappend times [set time [expr $Sim(Sim0Secs) + $Sim(Duration)*3600 - $Sim(AccSecs)]]
      vector append ES_SIMDURX [list 0.0 $time]
      vector append ES_SIMDURY [vector stats ES_INTERY -max]
      set durcolors {}
   }
   lappend heights 0.0

   #----- Keep only unique values and get maximum values for both axis

   set times   [concat $times $Sim(RestartDeltas)]
   set times   [lsort -real -unique $times]
   set heights [lsort -real -unique $heights]

   #----- Update axis intervals and histogram bar's color
   graphaxis configure ES_AXISX -intervals $times -grid [lrange $times 0 end-1] -min 0.0 -max [lindex $times end] \
      -unit "[lindex $Lbl(AxisX) $GDefs(Lang)]\n[clock format $Sim(Sim0Secs) -format "%Y-%m-%d %H:%M" -gmt True]"
   graphaxis configure ES_AXISY -intervals $heights -min 0.0 -max [lindex $heights end]
   graphitem configure ES_INTER  -colors $colors
   graphitem configure ES_SIMDUR -colors $durcolors

   #----- Force graph update
   $Sim(ESCanvas) itemconfigure ES_GRAPH -update 1
   update idletasks
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::ParamsClean>
# Creation   : Juillet 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Liberer les ressources associees au modele.
#
# Parametres :
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::ParamsClean { } {

   graphitem free ES_INTER ES_SIMDUR
   graphaxis free ES_AXISX ES_AXISY
   vector    free ES_INTERX ES_INTERY ES_SIMDURX ES_SIMDURY

   trace remove variable MLDPn::Sim(EmNbIntervals) write MLDPn::UpdateIntervalPickerList
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::EnableDisableMass>
# Creation   : 27 September 2005 - A. Malo - CMC/CMOE
#
# But        : Enable or disable total mass released entry widget according
#              on which mode is selected for mass calculation.
#
# Parametres :
#     <Id>  : Id of the selected interval.
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::EnableDisableMass { Widget Id } {
   global GDefs
   variable Tmp

   if { $Tmp(EmMassMode.$Id) == 0 ||  $Tmp(EmMassMode.$Id) == 1 } {
      #----- Total released mass is calculated according to empirical
      #----- formula of Sparks et al. (1997), so disable entry widget.
      $Widget.e configure -state disabled -disabledbackground $GDefs(ColorFrame) -disabledforeground $GDefs(ColorOff)
      #----- Compute total mass according to empirical formula of Sparks et al. (1997).
      MLDPn::ComputeMass $Id

   } else {

      #----- Because we wouldn't want 'Edit' or 'Edition' as the starting mass.
      set Tmp(EmMass.$Id) $Tmp(EmMassOld)
      #----- Total released mass can be edited so enable entry widget.
      focus $Widget.e ; #----- Focus on this entry widget.
      $Widget.e configure -state normal -disabledbackground $GDefs(ColorFrame) -disabledforeground $GDefs(ColorOff)
   }
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::GetEmissionIntervalNumberList>
# Creation   : Aout 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Return a list of all current interval numbers, starting from 1.
#
# Parametres :
#
# Retour     :
#     <list> : A list of all current interval numbers, starting from 1.
#
# Remarques  :
#     - This function is only used to provide the list of intervals for the
#       "jump to/edit interval emission" combobox on the scenario tab.
#
#----------------------------------------------------------------------------

proc MLDPn::GetEmissionIntervalNumberList { } {
   variable Sim

   set lst {}
   for { set i [expr $Sim(EmInterStart)+1] } { $i <= $Sim(EmNbIntervals) } { incr i } {
      lappend lst $i
   }

   return $lst
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::UpdateIntervalPickerList>
# Creation   : Aout 2010 - E. Legault-Ouellet - CMC/CMOE
#
# But        : Met a jour la liste des intervals du combobox.
#
# Parametres :
#     <Varname>   : Le nom de la variable --> 'Sim'
#     <Itemname>  : Le deuxieme nom --> 'EmNbIntervals'
#     <Op>        : Le type d'acces a la variable --> 'write'
#
# Retour     :
#
# Remarques  :
#     - Cette procedure est un callback d'un trace sur la variable
#       'Sim(EmNbIntervals'
#
#----------------------------------------------------------------------------
proc MLDPn::UpdateIntervalPickerList { Varname Itemname Op } {
   global GDefs
   variable Sim
   variable Lbl

   set target $Sim(TabframeEmission).scenario.ctrl.pick

   if { ![winfo exists $target] } {
      trace remove variable MLDPn::${Varname}($Itemname) $Op MLDPn::UpdateIntervalPickerList
   } else {
      ComboBox::DelAll $target
      ComboBox::AddList $target [MLDPn::GetEmissionIntervalNumberList]
      set Sim(PickInterval) [lindex $Lbl(EditInter) $GDefs(Lang)]
   }
}

#----------------------------------------------------------------------------
# Nom        : <MLDPn::PopUp>
# Creation   : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But        : Afficher le popup contextuel des simulations.
#
# Parametres :
#    <X>        : ...
#    <Y>        : ...
#
# Retour     :
#
# Remarques  :
#
#----------------------------------------------------------------------------

proc MLDPn::PopUp { X Y } {
   global   GDefs
   variable Sim
   variable Tmp
   variable Lbl

   set MLDPn::Sim(Model) MLDPn

   if { ![winfo exists .mldpnpopup] } {
      menu .mldpnpopup -tearoff 0 -bd 1 -type normal -activeborderwidth 1
         .mldpnpopup add command -label MLDPn -background $GDefs(ColorHighLight) \
            -activebackground $GDefs(ColorHighLight)
         .mldpnpopup add cascade -label [lindex $Lbl(Product) $GDefs(Lang)] -menu .mldpnpopup.product
         .mldpnpopup add cascade -label [lindex $Lbl(Result) $GDefs(Lang)] -menu .mldpnpopup.res
         .mldpnpopup add separator
         .mldpnpopup add command -label [lindex $Lbl(Continue) $GDefs(Lang)] \
            -command { Model::ParamsWindow $MLDPn::Sim(Model) CONT } -state disabled
         .mldpnpopup add separator
         .mldpnpopup add command -label [lindex $Lbl(Suppress) $GDefs(Lang)] \
             -command { Model::Delete $Exp::Data(SelectSim) }

      menu .mldpnpopup.product -tearoff 0 -bd 1 -type normal -activeborderwidth 1
          .mldpnpopup.product add command -label "VAAC" -command { Model::Product $Page::Data(Frame) MLDPn result VAAC }
          .mldpnpopup.product add command -label "RSMC" -command { Model::Product $Page::Data(Frame) MLDPn result RSMC True }
          .mldpnpopup.product add command -label "CHEM" -command { Model::Product $Page::Data(Frame) MLDPn result CHEM True }
          .mldpnpopup.product add command -label "FIRE" -command { Model::Product $Page::Data(Frame) MLDPn result FIRE True }
          .mldpnpopup.product add command -label "INFO" -command { Model::Product $Page::Data(Frame) MLDPn result INFO }
          .mldpnpopup.product add command -label "METF" -command { Model::Product $Page::Data(Frame) MLDPn metf   METF }
          .mldpnpopup.product add separator
          .mldpnpopup.product add command -label "SPI" -command { Model::Product $Page::Data(Frame) MLDPn "" SPI }

      menu .mldpnpopup.res -tearoff 0 -bd 1 -type normal -activeborderwidth 1
         .mldpnpopup.res add command -label "All" -command "MLDPn::Result all"
         .mldpnpopup.res add separator
         .mldpnpopup.res add command -label "Dispersion" -command "MLDPn::Result result"
         .mldpnpopup.res add command -label "MetFields" -command "MLDPn::Result metf"
         .mldpnpopup.res add command -label "Restart" -command "MLDPn::Result restart"
         .mldpnpopup.res add separator
         .mldpnpopup.res add command -label "Meteo" -command "MLDPn::Result meteo"
   }

   #----- Verifier l'etat de la simulation et configurer le menu en consequence
   switch --  [Info::Strip $Exp::Data(SelectSim) State] {

      -1 {                                            #----- Error
         .mldpnpopup entryconfigure 1 -state disabled   ;#Product
         .mldpnpopup entryconfigure 2 -state disabled   ;#Data
         .mldpnpopup entryconfigure 4 -state disabled   ;#Continue
         .mldpnpopup entryconfigure 6 -state normal     ;#Suppress
      }
      0 {                                             #----- Continuable
         .mldpnpopup entryconfigure 1 -state normal     ;#Product
         .mldpnpopup entryconfigure 2 -state normal     ;#Data
         .mldpnpopup entryconfigure 4 -state normal     ;#Continue
         .mldpnpopup entryconfigure 6 -state normal     ;#Suppress
      }
      1 {                                             #----- Etat Termine
         .mldpnpopup entryconfigure 1 -state normal     ;#Product
         .mldpnpopup entryconfigure 2 -state normal     ;#Data
         .mldpnpopup entryconfigure 4 -state disabled   ;#Continue
         .mldpnpopup entryconfigure 6 -state normal     ;#Suppress
      }
      2 {                                              #----- Etat d'execution
         .mldpnpopup entryconfigure 1 -state disabled   ;#Product
         .mldpnpopup entryconfigure 2 -state disabled   ;#Data
         .mldpnpopup entryconfigure 4 -state disabled   ;#Continue
         .mldpnpopup entryconfigure 6 -state normal     ;#Suppress
      }
   }

   tk_popup .mldpnpopup $X $Y 0
}
