#===============================================================================
# Environnement Canada
# Centre Meteorologique Canadian
# 2100 Trans-Canadienne
# Dorval, Quebec
#
# Projet   : Librairie d'outils SPI.
# Fichier  : Console.int
# Creation : Janvier 2006 - J.P.Gauthier - CMC/CMOE
#
# Description:
#    Permet d'afficher une console de commande.
#
# Fonctions:
#
#
# Remarques :
#
#===============================================================================

#----------------------------------------------------------------------------
# Nom      : <Console::Window>
# Creation : Janvier 2006 - J.P. Gauthier - CMC/CMOE
#
# But      : Affiche une boite permettant de selectionner des fichiers geotiff.
#
# Parametres   :
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc Console::Window { } {
   global GDefs
   variable Lbl
   variable Bubble
   variable Data
   variable Param

   set Data(Active) 1
   set Data(Canvas) $Page::Data(Canvas)
   set Data(Frame)  $Page::Data(Frame)

   set Param(Window) .console

   if { ![winfo exists .console] } {

      if { $Param(Dock) } {
         frame .console
         SPI::Dock .console
      } else {
         toplevel     .console
         wm title     .console "[lindex $SPI::Title(SPI) $GDefs(Lang)] $GDefs(Version) ([lindex $Param(Title) $GDefs(Lang)] $Param(Version))"
         eval wm geometry  .console $Param(Geom)
         wm resizable .console 1 1
         wm protocol  .console WM_DELETE_WINDOW { Console::Close }

         if { $SPI::Param(Window) } { wm transient .console . }
      }

      TabFrame::Create .console.tab 1 ""
      set Data(Tab) [TabFrame::Add .console.tab 1 [lindex $Param(Title) $GDefs(Lang)] True]
      pack .console.tab -side top -fill both -expand true

      frame $Data(Tab).head
         label $Data(Tab).head.lbl -text "" -relief flat -anchor w
         pack $Data(Tab).head.lbl -side left -fill x -ipady 2
      pack $Data(Tab).head -side top

      frame $Data(Tab).text
         listbox $Data(Tab).text.histo -relief sunken -bd 1 -width 20 -height 1 -bg white -yscrollcommand "$Data(Tab).text.scroll set" \
            -listvariable Console::Data(History)
         frame  $Data(Tab).text.text -relief sunken -bd 1
            label $Data(Tab).text.text.tic -text ">" -bg white -relief flat -width 1 -anchor nw
            text $Data(Tab).text.text.text -relief flat -bg white -height 5 -width 20 -wrap word
            pack $Data(Tab).text.text.tic -side left -fill y
            pack $Data(Tab).text.text.text -side left -fill x -expand true
         scrollbar $Data(Tab).text.scroll -relief sunken -command "$Data(Tab).text.histo yview" -bd 1 -width 10
         pack $Data(Tab).text.scroll -side right -fill y -anchor ne
         pack $Data(Tab).text.text -side top -fill x
         pack $Data(Tab).text.histo -side top -fill both -expand true
         pack $Data(Tab).text -side top -fill both -expand true

      frame .console.dock -relief raised -bd 1
         button .console.dock.sel -image DOCK -anchor w -relief flat -bd 0 -overrelief raised -command { SPI::DockTool Console }
         button .console.dock.del -image DOCKDELETE -anchor w -relief flat -bd 0 -overrelief raised -command Console::Close
         label .console.dock.info -relief sunken -bd 1 -anchor w -width 21 -bg $GDefs(ColorLight)
         pack .console.dock.sel .console.dock.del -side left
         pack .console.dock.info -side left -fill x -expand true
      pack .console.dock -side bottom -fill x

      frame .console.cmd

      Bubble::Create .console.dock.sel $Bubble(Dock)
      Bubble::Create .console.dock.del $Bubble(Close)

      bind $Data(Tab).text.text.text <Return>   "Console::Eval $Data(Tab).text.text.text True"
      bind $Data(Tab).text.text.text <Key-Up>   "Console::History $Data(Tab).text.text.text \[incr Console::Data(HistoryIndex) -1\]"
      bind $Data(Tab).text.text.text <Key-Down> "Console::History $Data(Tab).text.text.text \[incr Console::Data(HistoryIndex)  1\]"

      bind $Data(Tab).text.histo <Double-ButtonRelease-1> "Console::History $Data(Tab).text.text.text \[set Console::Data(HistoryIndex) \[$Data(Tab).text.histo curselection\]\]; Console::Eval $Data(Tab).text.text.text False"
   }
   raise .console
}
