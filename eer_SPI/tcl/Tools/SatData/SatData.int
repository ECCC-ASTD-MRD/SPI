#===============================================================================
# Environnement Canada
# Centre Meteorologique Canadian
# 2100 Trans-Canadienne
# Dorval, Quebec
#
# Projet   : Package d'interface pour SPI
# Fichier  : SatData.int
# Creation : Octobre 2000
#
# Description:
#    Permet l'obtention de messages meteorologiques
#
#===============================================================================

#----------------------------------------------------------------------------
# Nom      : <SatData::Window>
# Creation : Mai 2000 - J.P. Gauthier - CMC/CMOE
#
# But      : Initialise le package des donnees satellitaires
#
# Parametres :
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc SatData::Window { } {
   global GDefs
   variable Lbl
   variable Data
   variable Param

   if { ![winfo exists .satdata] } {

      toplevel .satdata

      wm title         .satdata "[lindex $SPI::Title(SPI) $GDefs(Lang)] $GDefs(Version) ([lindex $Param(Title) $GDefs(Lang)] $Param(Version))"
      wm resizable     .satdata 0 0
      eval wm geometry .satdata $Param(Geom)
      wm protocol      .satdata WM_DELETE_WINDOW { SatData::Close }

      if { $SPI::Param(Window) } { wm transient .satdata . }

      SatData::DomainRead
      SatData::WinDef
      SatData::ResApply

      set Data(Active) 1
      trace variable SPI::Src(Info) w { SatData::Source }
   }

   set Data(Canvas) $Page::Data(Canvas)
   set Data(Frame)  $Page::Data(Frame)
   set Data(VP)     $Viewport::Data(VP)
}

#----------------------------------------------------------------------------
# Nom      : <SatData::WinDef>
# Creation : Octobre 2000 - J.P. Gauthier - CMC/CMOE
#
# But      : Initialise l'affichage de la fenetre
#
# Parametres :
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc SatData::WinDef { } {
   global GDefs env
   variable Data
   variable Lbl
   variable Bubble

   set SatData::Data(Name) ""

   #----- Selection du domaine

   frame .satdata.dom -relief raised -bd 1
      label .satdata.dom.lbl -text [lindex $Lbl(Domain) $GDefs(Lang)]
      ComboBox::Create .satdata.dom.name SatData::Data(Name) edit sorted nodouble -1 $SatData::Data(LName) 33 10 \
         "SatData::DomainShow"
      ComboBox::Create .satdata.dom.res SatData::Data(Res) noedit sorted nodouble -1 $SatData::Data(LRes) 4 10 \
         "SatData::ResApply"
      button .satdata.dom.save -image FOLDIN -command "SatData::DomainSave .satdata.dom.name" -bd 0
      button .satdata.dom.del -image FOLDOUT -command "SatData::DomainDelete .satdata.dom.name" -bd 0

      pack .satdata.dom.lbl -side left -fill y
      pack .satdata.dom.name -side left -fill both -expand true
      pack .satdata.dom.save .satdata.dom.del .satdata.dom.res -side left -fill y
   pack .satdata.dom -side top -anchor w

   #----- Selection du fichier de sortie

   frame .satdata.out -relief raised -bd 1
      label .satdata.out.lbl -text [lindex $Lbl(Out) $GDefs(Lang)]
      entry .satdata.out.path -textvariable SatData::Data(ResultFile) -bg $GDefs(ColorLight) -bd 1
      button .satdata.out.in -image FOLDUP -bd 0 \
         -command { set tmp [FileBox::Create . [file dirname $SatData::Data(ResultFile)] Save "" [file tail $SatData::Data(ResultFile)]]; if { [string length $tmp] > 0 } { set SatData::Data(ResultFile) $tmp }}
      pack .satdata.out.lbl -side left
      pack .satdata.out.path -side left -fill both -expand true
      pack .satdata.out.in -side left
   pack .satdata.out -side top -fill x

   #----- Selection des donnees satellites.

   frame .satdata.sel
   pack .satdata.sel -side top -fill x -expand true

   #----- Selection des donnees operationnelles.

   label .satdata.sel.lbl -text [lindex $Lbl(Data) $GDefs(Lang)] -relief raised -bd 1
   pack .satdata.sel.lbl -side left -fill y -ipadx 2

   foreach sat { GOES15 GOES13 } no { 0 1 } {

      frame .satdata.sel.s$sat
         checkbutton .satdata.sel.s$sat.lbl -text [lindex $Lbl($sat) $GDefs(Lang)] -selectcolor $Data(Color${sat}) -relief raised -bd 1 \
            -variable SatData::Data(View$sat) -command "SatData::UpdateItems \$SatData::Data(Frame)"
         pack .satdata.sel.s$sat.lbl -fill x

         Bubble::Create .satdata.sel.s$sat.lbl $Bubble(Sat)

         frame .satdata.sel.s$sat.ch
         foreach c { nV } {
            frame .satdata.sel.s$sat.ch.c$c
               scrollbar .satdata.sel.s$sat.ch.c$c.scroll -orient vertical \
                        -command [list .satdata.sel.s$sat.ch.c$c.list yview] -bd 1
               listbox .satdata.sel.s$sat.ch.c$c.list -exportselection false -height 30 -width 13 -bd 1\
                        -yscroll ".satdata.sel.s$sat.ch.c$c.scroll set" -bg $GDefs(ColorLight) \
                        -selectbackground $Data(Color${sat}) -selectmode extended
               pack .satdata.sel.s$sat.ch.c$c.scroll -side right -fill y
               pack .satdata.sel.s$sat.ch.c$c.list -side left -expand true -fill both
            pack .satdata.sel.s$sat.ch.c$c -side left
         }
         pack .satdata.sel.s$sat.ch -side top
   }

   pack .satdata.sel.sGOES15 .satdata.sel.sGOES13 -side left

   #----- Selection des donnees de l'usager.

   frame .satdata.seluser -relief raised -bd 1
      checkbutton .satdata.seluser.lbl -text [lindex $Lbl(User) $GDefs(Lang)] -relief raised -bd 1 -indicatoron false \
         -variable SatData::Data(ViewUSER) -command "if { \$SatData::Data(ViewUSER) } {
                pack .satdata.seluser.ch -side top -fill both -expand true
             } else {
                pack forget .satdata.seluser.ch
             }"
      pack .satdata.seluser.lbl -side top -fill x -expand true

      Bubble::Create .satdata.seluser.lbl $Bubble(User)

      frame .satdata.seluser.ch
         frame .satdata.seluser.ch.path
            label .satdata.seluser.ch.path.lbl -text [lindex $Lbl(Path) $GDefs(Lang)]
            entry .satdata.seluser.ch.path.path -textvariable SatData::Data(UserPath) -bg $GDefs(ColorLight) -bd 1
            button .satdata.seluser.ch.path.in -image FOLDUP -bd 0 \
               -command { SatData::GetUserFiles [FileBox::Create . "$SatData::Data(UserPath)" Path ""] }
            pack .satdata.seluser.ch.path.lbl -side left
            pack .satdata.seluser.ch.path.path -side left -fill both -expand true
            pack .satdata.seluser.ch.path.in -side left

         scrollbar .satdata.seluser.ch.scroll -orient vertical \
                  -command [list .satdata.seluser.ch.list yview] -bd 1
         listbox .satdata.seluser.ch.list -exportselection false -height 6 -bd 1\
                  -yscroll ".satdata.seluser.ch.scroll set" -bg $GDefs(ColorLight) \
                  -selectbackground $GDefs(ColorHighLight) -selectmode extended
         pack .satdata.seluser.ch.path -fill x
         pack .satdata.seluser.ch.scroll -side right -fill y
         pack .satdata.seluser.ch.list -side left -expand true -fill both
#   pack .satdata.seluser -side top -fill x -expand true

   #----- Commande d'execution

   frame .satdata.cmd -relief raised -bd 1
      label .satdata.cmd.lbl -text [lindex $Lbl(Cmd) $GDefs(Lang)]
      frame .satdata.cmd.l -relief sunken -bd 1
         button .satdata.cmd.l.extract -text [lindex $Lbl(Extract) $GDefs(Lang)] -relief raised -bd 1\
            -command "SatData::DataGet"
         button .satdata.cmd.l.close -text [lindex $Lbl(Close) $GDefs(Lang)] -relief raised -bd 1 \
            -command "SatData::Close"
         pack .satdata.cmd.l.extract .satdata.cmd.l.close -side left -fill x -expand true
      pack .satdata.cmd.lbl -side left
      pack .satdata.cmd.l -side left -fill x -expand true
   pack .satdata.cmd -side top -fill x

   #----- Frame d'information

   frame .satdata.info -relief raised -bd 1
      checkbutton .satdata.info.mode -variable Page::Data(ToolMode) -onvalue SatData -offvalue SPI -image ARROW -relief sunken \
         -bd 1 -overrelief raised -offrelief flat -indicatoron 0 -width 50 -selectcolor $GDefs(ColorFrame) \
         -command { SPI::ToolMode $Page::Data(ToolMode) Data True }
      InfoFrame::Create .satdata.info.msg  SatData::Data(Msg) 20
      pack .satdata.info.mode -side left
      pack .satdata.info.msg  -side left -fill x -expand true
   pack .satdata.info -side top -fill x


   SatData::GetFiles

   Bubble::Create .satdata.dom.name.select $Bubble(Domain)
   Bubble::Create .satdata.dom.res.select  $Bubble(Resol)
   Bubble::Create .satdata.dom.save        $Bubble(Save)
   Bubble::Create .satdata.dom.del         $Bubble(Del)
   Bubble::Create .satdata.info.mode       $Bubble(Mode)
   Bubble::Create .satdata.cmd.extract     $Bubble(Extract)
   Bubble::Create .satdata.out.path        $Bubble(File)
   Bubble::Create .satdata.out.in          $Bubble(SelFile)
}
