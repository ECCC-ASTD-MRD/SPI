#===============================================================================
# Environnement Canada
# Centre Meteorologique Canadian
# 2100 Trans-Canadienne
# Dorval, Quebec
#
# Projet   : Librairie de "Widget" Tk.
# Fichier  : Mapper.int
# Creation : Juillet 2004 - J.P.Gauthier - CMC/CMOE
#
# Description:
#    Permet d'afficher un selecteur de donnees geographiques.
#
# Remarques :
#
#===============================================================================

#----------------------------------------------------------------------------
# Nom      : <Mapper::Window>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Affiche une boite permettant de selectionner des fichiers geotiff.
#
# Parametres   :
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc Mapper::Window { } {
   global GDefs
   variable Lbl
   variable Bubble
   variable Data
   variable Param

   set Data(Active) 1
   set Data(Canvas) $Page::Data(Canvas)
   set Data(Frame)  $Page::Data(Frame)

   set Param(Window) .mapper

   if { ![winfo exists .mapper] } {

      if { $Param(Dock) } {
         frame .mapper
         SPI::Dock .mapper
      } else {
         toplevel         .mapper
         wm title         .mapper "[lindex $SPI::Title(SPI) $GDefs(Lang)] $GDefs(Version) ([lindex $Param(Title) $GDefs(Lang)] $Param(Version))"
         eval wm geometry .mapper $Param(Geom)
         wm resizable     .mapper 1 1
         wm protocol      .mapper WM_DELETE_WINDOW { Mapper::Close }
      }

      frame .mapper.dock -relief raised -bd 1
         button .mapper.dock.sel -image DOCK -anchor w -relief flat -bd 1 -overrelief raised -command { SPI::DockTool Mapper }
         button .mapper.dock.del -image DOCKDELETE -anchor w -relief flat -bd 1 -overrelief raised -command Mapper::Close
         label .mapper.dock.info -textvariable Mapper::Data(Job) -relief sunken -bd 1 -anchor w -width 31 -bg $GDefs(ColorLight)
         pack .mapper.dock.sel .mapper.dock.del -side left
         pack .mapper.dock.info -side left -fill x -expand true
      pack .mapper.dock -side bottom -fill x

      panedwindow .mapper.data -orient vertical -showhandle False -opaqueresize True -bd 0
      pack .mapper.data -side top -fill both -expand true

      TabFrame::Create .mapper.data.tab1 1 ""
      TabFrame::Create .mapper.data.tab2 1 ""
      set Data(Tab1) [TabFrame::Add .mapper.data.tab1 1 [lindex $Lbl(Layers) $GDefs(Lang)] True]
      set Data(Tab2) [TabFrame::Add .mapper.data.tab2 1 [lindex $Lbl(Dig) $GDefs(Lang)] True]

      frame $Data(Tab2).head
         checkbutton $Data(Tab2).head.mode -variable Page::Data(ToolMode) -onvalue Mapper::DepotWare -offvalue SPI \
            -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
            -command  { SPI::ToolMode $Page::Data(ToolMode) Data True }
         button $Data(Tab2).head.reset -image RESET -bd 1 -relief flat -overrelief raised -command "Mapper::DepotWare::Reset True"
         button $Data(Tab2).head.params -image PARAMS -bd 1 -relief flat -overrelief raised -command "Mapper::DepotWare::Params"
         button $Data(Tab2).head.add -image PLUS -bd 1 -relief flat -overrelief raised -command "Mapper::DepotWare::Window"
         button $Data(Tab2).head.del -image DELETE -bd 1 -relief flat -overrelief raised -command "Mapper::DepotWare::Del \$Mapper::DepotWare::Data(Select)"
         pack $Data(Tab2).head.mode $Data(Tab2).head.reset $Data(Tab2).head.params -side left -padx 2
         pack $Data(Tab2).head.del $Data(Tab2).head.add -side right -padx 2
      pack $Data(Tab2).head -side top -fill x

      frame $Data(Tab2).list
         canvas $Data(Tab2).list.canvas -bg white -relief sunken -bd 1 -yscrollcommand "$Data(Tab2).list.scrolly set" \
           -xscrollcommand "$Data(Tab2).list.scrollx set" -scrollregion "1 1 250 500" -width 1 -height 1
         scrollbar $Data(Tab2).list.scrolly -orient vertical -bd 1 -width 10 -command "$Data(Tab2).list.canvas yview"
         scrollbar $Data(Tab2).list.scrollx -orient horizontal -bd 1 -width 10 -command "$Data(Tab2).list.canvas xview"
         pack $Data(Tab2).list.scrollx -side bottom -fill x
         pack $Data(Tab2).list.canvas -side left -fill both -expand true
         pack $Data(Tab2).list.scrolly -side left -fill y
      pack $Data(Tab2).list -side top -fill both -expand true

      bind $Data(Tab2).list.canvas <Button-4> "$Data(Tab2).list.canvas yview scroll -1 units"
      bind $Data(Tab2).list.canvas <Button-5> "$Data(Tab2).list.canvas yview scroll 1 units"

      frame $Data(Tab1).head
         checkbutton $Data(Tab1).head.mode -variable Page::Data(ToolMode) -onvalue Mapper -offvalue SPI -width 28 \
            -image ARROW -indicatoron 0 -relief sunken -bd 1 -anchor w -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
            -command  {
               if { $Page::Data(ToolMode)=="Mapper" } {
                  if { $Mapper::OGR::Data(Edit) } {
                     set Mapper::OGR::Data(Edit) False
                     Mapper::OGR::VertexInit $Page::Data(Frame) $Mapper::Data(Object) ""
                  }
                  SPI::ToolMode $Page::Data(ToolMode) Data True $Mapper::Data(PickSize)
               } else {
                  SPI::ToolMode $Page::Data(ToolMode) Data True
               } }
         menubutton $Data(Tab1).head.mode.down -image OPTIONS -relief flat -bd 0 -menu $Data(Tab1).head.mode.down.menu
         place $Data(Tab1).head.mode.down -relx 1.0 -rely 0.0 -anchor ne -relheight 1.0
         menu $Data(Tab1).head.mode.down.menu
         $Data(Tab1).head.mode.down.menu add radiobutton -label [lindex $Lbl(Select) $GDefs(Lang)] -variable Mapper::Data(PickSize) -value -1 -command  { set Page::Data(ToolMode) Mapper; SPI::ToolMode $Page::Data(ToolMode) Data True }
         $Data(Tab1).head.mode.down.menu add command -label [lindex $Lbl(SelectClear) $GDefs(Lang)] -command  { Mapper::OGR::SelectClear $Mapper::Data(Object) }
         $Data(Tab1).head.mode.down.menu add separator
         $Data(Tab1).head.mode.down.menu add radiobutton -label "[lindex $Lbl(Identify) $GDefs(Lang)] ([lindex $Lbl(Pixel) $GDefs(Lang)])"  -variable Mapper::Data(PickSize) -value 0 -command  { set Page::Data(ToolMode) Mapper; SPI::ToolMode $Page::Data(ToolMode) Data True $Mapper::Data(PickSize) }
         $Data(Tab1).head.mode.down.menu add radiobutton -label "[lindex $Lbl(Identify) $GDefs(Lang)] ([lindex $Lbl(Small) $GDefs(Lang)])"  -variable Mapper::Data(PickSize) -value 2 -command  { set Page::Data(ToolMode) Mapper; SPI::ToolMode $Page::Data(ToolMode) Data True $Mapper::Data(PickSize) }
         $Data(Tab1).head.mode.down.menu add radiobutton -label "[lindex $Lbl(Identify) $GDefs(Lang)] ([lindex $Lbl(Medium) $GDefs(Lang)])" -variable Mapper::Data(PickSize) -value 5 -command  { set Page::Data(ToolMode) Mapper; SPI::ToolMode $Page::Data(ToolMode) Data True $Mapper::Data(PickSize) }
         $Data(Tab1).head.mode.down.menu add radiobutton -label "[lindex $Lbl(Identify) $GDefs(Lang)] ([lindex $Lbl(Large) $GDefs(Lang)])"  -variable Mapper::Data(PickSize) -value 10 -command  { set Page::Data(ToolMode) Mapper; SPI::ToolMode $Page::Data(ToolMode) Data True $Mapper::Data(PickSize) }

         button $Data(Tab1).head.open -image OPEN -bd 1 -relief flat -width 28 -anchor w -overrelief raised -command "Mapper::Read \[FileBox::Create . \"\" LoadPath \[concat \[list \$FileBox::Type(ALL)\] \$Mapper::GDAL::Data(Formats) \$Mapper::OGR::Data(Formats)\]\]"
         menubutton $Data(Tab1).head.open.down -image OPTIONS -relief flat -bd 0 -menu $Data(Tab1).head.open.down.menu
         place $Data(Tab1).head.open.down -relx 1.0 -rely 0.0 -anchor ne -relheight 1.0
         menu $Data(Tab1).head.open.down.menu
         $Data(Tab1).head.open.down.menu add command -label [lindex $Lbl(NewLayer) $GDefs(Lang)] -command { Mapper::OGR::New }

         button $Data(Tab1).head.params -image PARAMS -bd 1 -relief flat -overrelief raised -command "Mapper::Params True"
         button $Data(Tab1).head.loc -image FINGER -bd 1 -relief flat -overrelief raised -command "Mapper::Locate"
         button $Data(Tab1).head.zoom -image MODEZOOM -bd 1 -relief flat -overrelief raised -command "Mapper::Zoom"
         checkbutton $Data(Tab1).head.ref -variable Mapper::Data(GeoRef) -offvalue "" -command "Mapper::SetGeoRef" \
            -image RESET -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame)
         pack $Data(Tab1).head.mode $Data(Tab1).head.open $Data(Tab1).head.params $Data(Tab1).head.loc $Data(Tab1).head.zoom -side left -padx 2

         frame $Data(Tab1).head.idx
            button $Data(Tab1).head.idx.down -image UP -bd 1 -width 12 -relief flat -overrelief raised -command "Mapper::Scroll -1"
            button $Data(Tab1).head.idx.up -image DOWN -bd 1 -width 12 -relief flat -overrelief raised -command "Mapper::Scroll 1"
            pack $Data(Tab1).head.idx.down $Data(Tab1).head.idx.up -side top -fill y -expand true -ipadx 3
         button $Data(Tab1).head.front -image FRONT -bd 1 -relief flat -overrelief raised -command "Mapper::Scroll F"
         button $Data(Tab1).head.back -image BACK -bd 1 -relief flat -overrelief raised -command "Mapper::Scroll B"
         button $Data(Tab1).head.del -image DELETE -bd 1 -relief flat -overrelief raised -command "Mapper::Del"
         pack $Data(Tab1).head.del $Data(Tab1).head.idx $Data(Tab1).head.back $Data(Tab1).head.front -side right -padx 2
      pack $Data(Tab1).head -side top -fill x

      frame $Data(Tab1).select
         listbox $Data(Tab1).select.list -relief sunken -bd 1 -selectmode single -width 1 -height 1 -background white \
            -yscrollcommand "$Data(Tab1).select.scrolly set" -xscrollcommand "$Data(Tab1).select.scrollx set" \
            -listvar Viewport::Data(Data) -selectbackground $GDefs(ColorHighLight) -selectforeground black
         scrollbar $Data(Tab1).select.scrolly -relief sunken -command "$Data(Tab1).select.list yview" -bd 1 -width 10
         scrollbar $Data(Tab1).select.scrollx -relief sunken -command "$Data(Tab1).select.list xview" -bd 1 -width 10 -orient horizontal
         pack $Data(Tab1).select.scrollx -side bottom -fill x
         pack $Data(Tab1).select.list -side left -fill both -expand true
         pack $Data(Tab1).select.scrolly -side left -fill y
      pack $Data(Tab1).select -side top -fill both -expand true

      .mapper.data add .mapper.data.tab1 -height 400
      .mapper.data add .mapper.data.tab2

      Mapper::DepotWare::Create
   }

   if { ![winfo exists .mappermenu] } {
      menu .mappermenu -type normal
         .mappermenu add command -image FINGER -compound left -label [lindex $Lbl(Locate) $GDefs(Lang)] -command "Mapper::Locate"
         .mappermenu add command -image MODEZOOM -compound left -label [lindex $Lbl(Zoom) $GDefs(Lang)] -command "Mapper::Zoom"
         .mappermenu add command -image LOCATION -compound left -label [lindex $Lbl(ZoomAll) $GDefs(Lang)] -command "Mapper::Zoom True"
         .mappermenu add command -image MODEZOOM -compound left -label [lindex $Lbl(ZoomFull) $GDefs(Lang)] -command "Mapper::ZoomFull"
         .mappermenu add command -image WORLD  -compound left -label [lindex $Lbl(GeoRef) $GDefs(Lang)] -command "Mapper::SetGeoRef"
         .mappermenu add separator
         .mappermenu add checkbutton -label [lindex $Lbl(Active) $GDefs(Lang)] -variable Mapper::Data(Active) -onvalue 1 -offvalue 0 \
            -command "Mapper::Toggle"
         .mappermenu add separator
         .mappermenu add cascade -image CALC   -compound left -label [lindex $Lbl(GeoProcess) $GDefs(Lang)] -menu .mappermenu.process
         .mappermenu add command -image SAVE   -compound left -label "[lindex $Lbl(SaveAs) $GDefs(Lang)] ..." -command "Mapper::Write \$Mapper::Data(Object)"
         .mappermenu add command -image PARAMS -compound left -label "[lindex $Lbl(Params) $GDefs(Lang)] ..." -command "Mapper::Params True"
         .mappermenu add separator
         .mappermenu add command -image DELETE -compound left -label [lindex $Lbl(Del) $GDefs(Lang)] -command "Mapper::Del"
         
      menu .mappermenu.process
   }

   bind $Mapper::Data(Tab1).select.list <Button-3>               { Mapper::PopUp %X %Y %y }
   bind $Mapper::Data(Tab1).select.list <ButtonRelease-1>        { Mapper::Params }
   bind $Mapper::Data(Tab1).select.list <Double-ButtonRelease-1> { Mapper::Params True }

   Bubble::Create .mapper.dock.sel          $Bubble(Dock)
   Bubble::Create .mapper.dock.del          $Bubble(Close)
   Bubble::Create $Data(Tab2).head.mode     $Bubble(DepotSelect)
   Bubble::Create $Data(Tab2).head.reset    $Bubble(DepotReset)
   Bubble::Create $Data(Tab2).head.add      $Bubble(DepotAdd)
   Bubble::Create $Data(Tab2).head.del      $Bubble(DepotDel)
   Bubble::Create $Data(Tab2).head.params   $Bubble(DepotParams)

   Bubble::Create $Data(Tab1).head.idx.up   $Bubble(Down)
   Bubble::Create $Data(Tab1).head.idx.down $Bubble(Up)
   Bubble::Create $Data(Tab1).head.del      $Bubble(Del)
   Bubble::Create $Data(Tab1).head.front    $Bubble(Front)
   Bubble::Create $Data(Tab1).head.back     $Bubble(Back)
   Bubble::Create $Data(Tab1).head.params   $Bubble(Params)
   Bubble::Create $Data(Tab1).head.loc      $Bubble(Locate)
   Bubble::Create $Data(Tab1).head.zoom     $Bubble(Zoom)
   Bubble::Create $Data(Tab1).head.mode     $Bubble(Mode)
   Bubble::Create $Data(Tab1).head.open     $Bubble(File)

   trace add variable Viewport::Data(Data) write Mapper::Toggler

   Mapper::DepotWare::Reset
   Viewport::FollowerAdd Mapper
}

#----------------------------------------------------------------------------
# Nom      : <Mapper::PopUp>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : configure et affiche le menu contextuel selon le type de la donnee.
#
# Parametres:
#   <X>     : Position X
#   <Y>     : Position Y
#   <y>     : Position Y locale
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc Mapper::PopUp { X Y y } {
   variable Data

   $Data(Tab1).select.list selection clear 0 end;
   $Data(Tab1).select.list selection set [$Data(Tab1).select.list nearest $y]

   Mapper::Params

   .mappermenu.process delete 0 end
   
   set Data(Object) [$Data(Tab1).select.list get [$Data(Tab1).select.list nearest $y]]

   if { [ogrlayer is $Data(Object)] } {
      set Data(Active) [ogrlayer configure $Data(Object) -active]

      .mappermenu entryconfigure 9 -state normal
      foreach op $Mapper::OGR::Data(Ops) {
         .mappermenu.process add command -label "$op ..." -command "Mapper::GeoProcessParams $Data(Object) $op"
      }      
      
   } elseif { [gdalband is $Data(Object)] } {
      set Data(Active) [gdalband configure $Data(Object) -active]
      
     .mappermenu entryconfigure 9 -state disabled
     foreach op $Mapper::GDAL::Data(Ops) {
         .mappermenu.process add command -label "$op ..." -command "Mapper::GeoProcessParams $Data(Object) $op"
      }      
   } elseif { [model is $Data(Object)] } {
      set Data(Active) [model configure $Data(Object) -active]
   } else {
      set Data(Active) 0
   }
   tk_popup .mappermenu $X $Y 0
}

#----------------------------------------------------------------------------
# Nom      : <Mapper::GeoProcessParams>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Fenetre de parametres du geotraitement
#
# Parametres:
#   <Object>: Objet a traiter
#   <Op>    : Operateur
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc Mapper::GeoProcessParams { Object Op } {
   global GDefs
   variable Data
   variable Lbl
   variable Msg
  
   if { ![winfo exists .mapperprocess] } {
      toplevel .mapperprocess

      wm title     .mapperprocess "[lindex $Lbl(GeoProcess) $GDefs(Lang)]"
      wm transient .mapperprocess .
      wm geometry  .mapperprocess 300x100+[winfo rootx .mapper]+[winfo rooty .mapper]

      label .mapperprocess.info -text [lindex $Msg(GeoProcessInfo) $GDefs(Lang)] -justify center -relief raised -bd 1
      pack .mapperprocess.info -side top -fill both -expand True
      
      frame .mapperprocess.params0 -relief raised -bd 1
      frame .mapperprocess.params1 -relief raised -bd 1
      frame .mapperprocess.params2 -relief raised -bd 1


      set Mapper::Data(ProcessParam0)  ""
      set Mapper::Data(ProcessParam1)  ""
      set Mapper::Data(ProcessParam2)  ""
    
      switch $Op {
         buffer       { label .mapperprocess.params0.lbl -text [lindex $Lbl(Distance) $GDefs(Lang)] -width 18 -anchor w 
                        ComboBox::Create  .mapperprocess.params0.val Mapper::Data(ProcessParam0) editclose unsorted nodouble -1 [ogrlayer define $Object -field] 1 8 { }
                        label .mapperprocess.params1.lbl -text [lindex $Lbl(SegmentCurve) $GDefs(Lang)] -width 18 -anchor w 
                        entry .mapperprocess.params1.val -textvariable Mapper::Data(ProcessParam1) -width 2 -bd 1 -relief sunken -bg $GDefs(ColorLight)
                        pack .mapperprocess.params0.lbl -side left -fill x
                        pack .mapperprocess.params0.val -side left -fill x -expand True
                        pack .mapperprocess.params1.lbl -side left -fill x
                        pack .mapperprocess.params1.val -side left -fill x -expand True
                      }
         difference   { label .mapperprocess.params0.lbl -text [lindex $Lbl(WithLayer) $GDefs(Lang)] -width 18 -anchor w
                        ComboBox::Create  .mapperprocess.params0.val Mapper::Data(ProcessParam0) noedit unsorted nodouble -1 [ogrlayer all] 1 8 { }
                        pack .mapperprocess.params0.lbl -side left -fill x
                        pack .mapperprocess.params0.val -side left -fill x -expand True
                     }
         intersection { label .mapperprocess.params0.lbl -text [lindex $Lbl(WithLayer) $GDefs(Lang)] -width 18 -anchor w
                        ComboBox::Create .mapperprocess.params0.val Mapper::Data(ProcessParam0) noedit unsorted nodouble -1 [ogrlayer all] 1 8 { }
                        pack .mapperprocess.params0.lbl -side left -fill x
                        pack .mapperprocess.params0.val -side left -fill x -expand True
                     }
         simplify     { label .mapperprocess.params0.lbl -text [lindex $Lbl(Tolerance) $GDefs(Lang)] -width 18 -anchor w
                        entry .mapperprocess.params0.val -textvariable Mapper::Data(ProcessParam0) -width 2 -bd 1 -relief sunken -bg $GDefs(ColorLight)
                        pack .mapperprocess.params0.lbl -side left -fill x
                        pack .mapperprocess.params0.val -side left -fill x -expand True
                      }
         segmentize   { label .mapperprocess.params0.lbl -text [lindex $Lbl(SegmentLength) $GDefs(Lang)] -width 18 -anchor w 
                        entry .mapperprocess.params0.val -textvariable Mapper::Data(ProcessParam0) -width 2 -bd 1 -relief sunken -bg $GDefs(ColorLight)
                        pack .mapperprocess.params0.lbl -side left -fill x
                        pack .mapperprocess.params0.val -side left -fill x -expand True
                      }
      }
      
      frame .mapperprocess.com
         button .mapperprocess.com.apply  -text [lindex $Lbl(Apply) $GDefs(Lang)] -bd 1 -command "Mapper::GeoProcess $Object $Op \"\$Mapper::Data(ProcessParam0) \$Mapper::Data(ProcessParam1) \$Mapper::Data(ProcessParam2)\"; destroy .mapperprocess"
         button .mapperprocess.com.cancel -text [lindex $Lbl(Close) $GDefs(Lang)] -bd 1 -command "destroy .mapperprocess"
         pack .mapperprocess.com.cancel .mapperprocess.com.apply -side right -fill x -expand True
      pack .mapperprocess.params0 .mapperprocess.params1 .mapperprocess.params2 .mapperprocess.com -side top -fill x
   }
}

#----------------------------------------------------------------------------
# Nom      : <Mapper::GeoProcess>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Effectuer le geotraitement
#
# Parametres:
#   <Object>: Objet a traiter
#   <Op>    : Operateur
#   <Param> : Parametre de l'operateur
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc Mapper::GeoProcess { Object Op Param } {

   set new ${Object}_$Op

   if { [ogrlayer is $Object] } {
      if { [ogrlayer is $new] } {
         ogrlayer free $new
      }
      eval ogrlayer stats $Object -$Op $Param $new

      Mapper::OGR::Config $Page::Data(Frame) $new
      Mapper::UpdateData $Page::Data(Frame)
      Mapper::Progress $new
   }
}

proc Mapper::Toggle { } {
   variable Data

   if { [ogrlayer is $Data(Object)] } {
      ogrlayer configure $Data(Object) -active $Data(Active)
   } elseif { [gdalband is $Data(Object)] } {
      gdalband configure $Data(Object) -active $Data(Active)
   } elseif { [model is $Data(Object)] } {
      model configure $Data(Object) -active $Data(Active)
   }
   Mapper::Toggler 0 0 0
   Page::Update $Page::Data(Frame)
}

proc Mapper::Toggler { Var1 Var2 Op } {
   variable Data

   set i      0
   set active 0
   set changed 0

   #----- Force listbox refreshsince the trace happens before it
   catch { $Data(Tab1).select.list configure -listvar Viewport::Data(Data) }

   foreach obj $Viewport::Data(Data) {
      if { [ogrlayer is $obj] } {
         set changed [ogrlayer ischanged $obj]
         set active  [ogrlayer configure $obj -active]
      } elseif { [gdalband is $obj] } {
         set active [gdalband configure $obj -active]
      } elseif { [model is $obj] } {
         set active [model configure $obj -active]
      }
      
      if { $changed } {
         if { $active } {
            catch { $Data(Tab1).select.list itemconfigure $i -foreground red -selectforeground red }
         } else {
            catch { $Data(Tab1).select.list itemconfigure $i -foreground pink -selectforeground pink }
         }
      } else {
         if { $active } {
            catch { $Data(Tab1).select.list itemconfigure $i -foreground black -selectforeground black }
         } else {
            catch { $Data(Tab1).select.list itemconfigure $i -foreground gray75 -selectforeground gray75 }
         }
      }
      
      incr i
   }
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::ParamsClose>
# Creation :  2007 - Maxime Samuel - CMC/CMOE -
#
# But      : Détruire la fenetre des paramètres
#
# Parametres :
#
#-------------------------------------------------------------------------------

proc Mapper::ParamsClose { } {
    variable Data

    if { $Page::Data(ToolMode)=="Mapper::GeoLocator" } {
      SPI::ToolMode SPI Zoom
    }

    if { $Mapper::OGR::Data(Edit) } {
       set Mapper::OGR::Data(Edit) False
       Mapper::OGR::VertexStop $Page::Data(Frame) $Data(Object)
    }
    
    catch { [Page::Canvas $Data(Frame)] delete MAPPERGEOLOCATOR MAPPERVERTEX MAPPERRANGE }
    Viewport::FollowerRemove Mapper::GeoLocator
    Page::Destroy $Data(Frame4).georef.hfrm.map
    Page::Update  $Data(Frame)
    destroy .mapperparams
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::Cursor>
# Creation : Fevrier 2011 - J.P. Gauthier - CMC/CMOE -
#
# But      : Changer le curseur
#
# Parametres :
#   <Cursor> : curseur a utiliser
#
#-------------------------------------------------------------------------------

proc Mapper::Cursor { { Cursor left_ptr } } {
    variable Data

   if { [winfo exists .mapper] } {
      catch { $Data(Canvas) configure -cursor $Cursor }
      catch { .mapperparams configure -cursor $Cursor }
      .mapper configure -cursor $Cursor
      . configure -cursor $Cursor
      update idletasks
   }
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::Params>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE -
#
# But      : Selectionner la fenetre de parametres selon le type de donnees
#
# Parametres  :
#   <Display> : Afficher les parametres
#
#-------------------------------------------------------------------------------

proc Mapper::Params { { Display False } { Object "" } } {
   variable Data

   if { ![winfo exists .mapperparams] && !$Display } {
      return
   }

   set Data(Init) True

   Mapper::Cursor watch

   if { $Object!="" || [set idx [$Data(Tab1).select.list curselection]]!="" } {
      if { $Object=="" } {
          #----- If no object is specified, use the list selected item
         set Object [$Data(Tab1).select.list get $idx]
      } elseif { [string is integer -strict $Object] } {
         #----- if the object is a number, use the list item at thie index
         set Object [$Data(Tab1).select.list get $Object]
      }
      if { $Object!=$Data(Object) || ![winfo exists .mapperparams] } {
         if { $Mapper::OGR::Data(Edit) } {
            set Mapper::OGR::Data(Edit) False
            Mapper::OGR::VertexStop $Page::Data(Frame) $Data(Object)
         }
      
         set Data(Object) $Object

         if { [gdalband is $Data(Object)] } {
            Mapper::GDAL::ParamsGet $Data(Object)
            Mapper::GDAL::Params $Data(Object) 0
         } elseif { [ogrlayer is $Data(Object)] } {
            Mapper::OGR::ParamsGet $Data(Object)
            Mapper::OGR::Params $Data(Object) 0
         } elseif { [model is $Data(Object)] } {
            Mapper::MDL::ParamsGet $Data(Object)
            Mapper::MDL::Params $Data(Object) 0
         }
      }
   } else {
      destroy .mapperparams
   }

   set Data(Init) False
   Mapper::Cursor left_ptr
}

proc Mapper::Apply { Object } {

   if { [gdalband is $Object] } {
      Mapper::GDAL::Apply $Object
   } elseif { [ogrlayer is $Object] } {
      Mapper::OGR::Apply $Object
   }
   Page::Update $Page::Data(Frame)
}
