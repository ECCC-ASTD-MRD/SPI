#!/bin/sh
# SH stuff \
DIR=$0
# SH stuff \
DIR=${DIR%Setup}../
# Let's start up with the proper wish \
exec $DIR/wish "$0" "$@"

proc InstallPath { Var Path } {
   global GDefs

   if { $Path!= "" } {
      set $Var $Path
   }
}

#-------------------------------------------------------------------------------
# Nom      : <GetPrevious>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Lire les definitions precedentes.
#
# Parametres :
#
# Remarques :
#
# Modifications :
#
#    Nom         : -
#    Date        : -
#    Description : -
#
#-------------------------------------------------------------------------------

proc GetPrevious { } {
   global GDefs
   global env
   global tcl_platform

   if { ![file exists $env(HOME)/.eer_ToolDefs] } {
      file mkdir $env(HOME)/.eer_ToolDefs
   } else {
      if { [set file [lindex [lsort -dictionary -increasing [glob -nocomplain $env(HOME)/.eer_ToolDefs/.eer_Defs-*]] end]]!="" } {
         source $file
      }
   }

   set GDefs(Printer) [lindex $GDefs(Printers) 0]

   if { $GDefs(Lang) } {
      set GDefs(Langue) English
   } else {
      set GDefs(Langue) Francais
   }
}

#-------------------------------------------------------------------------------
# Nom      : <GetPrinters>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Recuperer la liste des imprimantes disponibles.
#
# Parametres :
#
# Remarques :
#   -On utilise le fichier /etc/printcap
#
# Modifications :
#
#    Nom         : -
#    Date        : -
#    Description : -
#
#-------------------------------------------------------------------------------

proc GetPrinters { } {
   global GDefs

   set GDefs(Printers) ""

   if { [file exists /etc/printcap] && [file readable /etc/printcap] } {

      set f [open /etc/printcap r]

      while { ![eof $f] } {
         gets $f printer

         if { [string index $printer 0]!="#" && $printer!="" } {
            lappend GDefs(Printers) [lindex [split [lindex [split $printer :] 0] |] 0]
         }
      }
      close $f
   }
}

#-------------------------------------------------------------------------------
# Nom      : <InstallWindow>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Creer la fenetre principale d'installation.
#
# Parametres :
#
# Remarques :
#
# Modifications :
#
#    Nom         : -
#    Date        : -
#    Description : -
#
#-------------------------------------------------------------------------------

proc InstallWindow { } {
   global GDefs

   wm title     . "EBox Installer ($GDefs(Ver))"
   wm geometry  . 300x170
   wm resizable . 0 0

   TabFrame::Create .tab 1 ""
   pack .tab -side top -fill both -expand true -padx 5 -pady 5

   WidgetOther [TabFrame::Add .tab 1 Options False]
   WidgetPath  [TabFrame::Add .tab 1 Modeles False]

   frame .cmd
      button .cmd.install -text Install -bd 1 -highlightthickness 0 \
         -command "InstallOutput; destroy ."
      button .cmd.cancel -text Cancel -bd 1 -highlightthickness 0 \
         -command "destroy ."
      pack .cmd.install .cmd.cancel -side left -fill x -expand true
   pack .cmd -side top -fill x -padx 5 -pady 5

   TabFrame::Select .tab 0
}

#-------------------------------------------------------------------------------
# Nom      : <WidgetPath>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Creer l'onglet des repertoires.
#
# Parametres :
#
# Remarques :
#
# Modifications :
#
#    Nom         : -
#    Date        : -
#    Description : -
#
#-------------------------------------------------------------------------------

proc WidgetEnable { Frame } {
   global GDefs

   if { $GDefs(Model) } {
      $Frame.loc.ent configure -state normal -fg black
      $Frame.loc.but configure -state normal

      $Frame.host.ent configure -state normal -fg black
   } else {
      $Frame.loc.ent configure -state disabled -fg $GDefs(ColorOff)
      $Frame.loc.but configure -state disabled

      $Frame.host.ent configure -state disabled -fg $GDefs(ColorOff)
   }
}

proc WidgetPath { Frame } {
   global GDefs
   global Lbl

   checkbutton $Frame.model -variable GDefs(Model) -onvalue True -offvalue False -text "Modelisation" -command "WidgetEnable $Frame"
   pack $Frame.model -side top -pady 10 -padx 5 -anchor w

   frame $Frame.loc
      label $Frame.loc.lbl -text Experiments -width 8 -anchor w
      entry $Frame.loc.ent -textvariable GDefs(DirData) -width 25 -bd 1 -highlightthickness 0 -bg $GDefs(ColorLight)
      button $Frame.loc.but -bitmap @$GDefs(Dir)/Resources/Bitmap/directory.ico -bd 1 -highlightthickness 0 -relief groove -bd 2\
         -command { InstallPath GDefs(DirData) [FileBox::Create . $GDefs(DirData) Path ""] }
      pack $Frame.loc.lbl $Frame.loc.ent $Frame.loc.but -side left -fill y
   pack $Frame.loc -side top -padx 5 -pady 5 -anchor w

   frame $Frame.host
      label $Frame.host.lbl -text BackEnd -width 8 -anchor w
      entry $Frame.host.ent -textvariable GDefs(BackEndsaiph) -width 25 -bd 1 -highlightthickness 0 -bg $GDefs(ColorLight)
      pack $Frame.host.lbl $Frame.host.ent -side left
   pack $Frame.host -side top -padx 5 -anchor w

   WidgetEnable $Frame
}

#-------------------------------------------------------------------------------
# Nom      : <WidgetOther>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Creer l'onglet des autres parametres.
#
# Parametres :
#
# Remarques :
#
# Modifications :
#
#    Nom         : -
#    Date        : -
#    Description : -
#
#-------------------------------------------------------------------------------

proc WidgetOther { Frame } {
   global GDefs

   frame $Frame.deb
      label $Frame.deb.lbl -text "Sortie de la trace" -width 22 -anchor w
      ComboBox::Create $Frame.deb.out GDefs(DebugOutput) noedit sorted nodouble -1 \
          "stderr stdout" 6 5 ""
      ComboBox::Create $Frame.deb.lvl GDefs(DebugLevel) noedit sorted nodouble -1 \
          "0 1 2" 2 5 ""
      pack $Frame.deb.lbl $Frame.deb.out $Frame.deb.lvl -side left
   pack $Frame.deb -pady 10 -side top

   frame $Frame.print
      label $Frame.print.lbl -text "Imprimante par defaut" -width 22 -anchor w
      ComboBox::Create $Frame.print.ent GDefs(Printer) noedit sorted nodouble -1 \
          $GDefs(Printers) 10 5 ""
      pack $Frame.print.lbl $Frame.print.ent -side left
   pack $Frame.print -side top

   frame $Frame.lan
      label $Frame.lan.lbl -text "Language" -width 22 -anchor w
      ComboBox::Create $Frame.lan.ent GDefs(Langue) noedit sorted nodouble -1 \
          "Francais English" 10 5 ""
      pack $Frame.lan.lbl $Frame.lan.ent -side left
   pack $Frame.lan -side top

   frame $Frame.grp
      label $Frame.grp.lbl -text "Groupe" -width 22 -anchor w
      ComboBox::Create $Frame.grp.ent GDefs(Group) noedit sorted nodouble -1 \
          "[exec groups]" 10 5 ""
      pack $Frame.grp.lbl $Frame.grp.ent -side left
   pack $Frame.grp -side top
}

#-------------------------------------------------------------------------------
# Nom      : <InstallOutput>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Creer le fichiers de parametres.
#
# Parametres :
#
# Remarques :
#
# Modifications :
#
#    Nom         : -
#    Date        : -
#    Description : -
#
#-------------------------------------------------------------------------------

proc InstallOutput { } {
   global env
   global GDefs

   #----- Determiner la langue
   switch $GDefs(Langue) {
      Francais { set GDefs(Lang) 0 }
      English  { set GDefs(Lang) 1 }
   }

   #----- Mettre en place le printer default
   set idx [lsearch -exact $GDefs(Printers) $GDefs(Printer)]
   set GDefs(Printers) [lreplace $GDefs(Printers) $idx $idx]
   set GDefs(Printers) [linsert $GDefs(Printers) 0 $GDefs(Printer)]

   #----- Creer le fichier d'initialisations
   set f [open $env(HOME)/.eer_ToolDefs/$GDefs(DefFile) w+]

   puts $f "
#==============================================================================
# Environnement Canada
# Centre Meteorologique Canadien
# Dorval, Quebec
#
# Projet     : Interface de gestion des urgences.
# Nom        : <$GDefs(DefFile)>
# Creation   : Novembre 2002 - J.P. Gauthier - CMC/CMOE
#
# Description:
#
#    Definitions des divers repertoires et constantes relatives aux package
#    d'outils des urgences
#
# Remarques  :
#   Aucune.
#
# Modifications :
#
#   Nom         : -
#   Date        : -
#   Description : -
#
#=================================================================================

#----- Repertoire Generaux relatif a toutes les applications

set env(GDB_PATH)         SED_GEOPATH

set GDefs(Arch)           \$tcl_platform(os)
set GDefs(Host)           \[lindex \[split \[info hostname\] .\] 0\]
set GDefs(Ext)            \[info sharedlibextension\]

set GDefs(Lang)           $GDefs(Lang)
set GDefs(Group)          $GDefs(Group)
set GDefs(Model)          $GDefs(Model)
set GDefs(Printers)       \"$GDefs(Printers)\"

#----- Repertoires des experiences

set GDefs(DirEER)         \$env(HOME)/.eer_ToolDefs
set GDefs(DirWatch)       \"$GDefs(DirWatch)\"
set GDefs(DirData)        \"$GDefs(DirData)\"
set GDefs(DirObs)         \"/data/ade\"
set GDefs(DirCFSArch)     \"$GDefs(DirCFSArch)\"

#----- Repertoires plus specifiques

set GDefs(DirMsg)         \"$GDefs(DirMsg)\"
set GDefs(DBMet)           $GDefs(DBMet)

#----- Definitions pour les Frontends / Backends

set GDefs(FrontEnd)       $GDefs(FrontEnd)
set GDefs(FrontEndUser)   $GDefs(FrontEndUser)
set GDefs(TransmitUser)   $GDefs(TransmitUser)

set GDefs(FrontEnds)        { $GDefs(FrontEnds) }
set GDefs(BackEnds)         { $GDefs(BackEnds) }
set GDefs(BackEndmaia)      { $GDefs(BackEndmaia) }
set GDefs(BackEndsaiph)     { $GDefs(BackEndsaiph) }
set GDefs(BackEndzeta)      { $GDefs(BackEndzeta) }

#----- Definitions generale de Tk

catch {
   font create XFont6  -family Courier -weight bold -size -6
   font create XFont8  -family Courier -weight bold -size -8
   font create XFont9  -family Courier -weight bold -size -9
   font create XFont10 -family Courier -weight bold -size -10
   font create XFont12 -family Courier -weight bold -size -12
   font create XFont14 -family Courier -weight bold -size -14
   font create XFont16 -family Courier -weight bold -size -16
   font create XFont17 -family Courier -weight bold -size -17
   font create XFont18 -family Courier -weight bold -size -18
   font create XFont20 -family Courier -weight bold -size -20
   font create XFont24 -family Courier -weight bold -size -24

   set GDefs(Font)           XFont12
   set GDefs(Colors)         { #ffffff #000000 #bfbfbf #7f7f7f #404040 #ff0000 #ffc0c0 #006464 #00ffff #0000ff #b0d8d8  #ffff00 #ffa500 #ffd700 #ffa54f #ff1493 #da70d6 #ffbbff }
   set GDefs(ColorEmerg)     red
   set GDefs(ColorFrame)     gray80
   set GDefs(ColorLight)     gray70
   set GDefs(ColorOff)       gray60
   set GDefs(ColorHighLight) yellow

   option add *selectColor            \$GDefs(ColorHighLight)
   option add *background             \$GDefs(ColorFrame)
   option add *foreground             black
   option add *highlightThickness     0
   option add *font                   \$GDefs(Font)
   option add *Menu*activeBorderWidth 0
   option add *Menu*tearOff           0
   option add *Menu*borderWidth       1
}

#----- NVidia Antialiasing

set env(__GL_FSAA_MODE) 1 ;#2x Bilenear Multisampling
set env(__GL_FSAA_MODE) 2 ;#2x Quincunx Multisampling
set env(__GL_FSAA_MODE) 4 ;#2x2 SuperSampling (Best for Quadro2 Pro)
set env(__GL_FSAA_MODE) 5 ;#4x Gaussian Multisampling (Best for QuadroFX)
"
   close $f

   file mkdir $env(HOME)/.eer_ToolDefs/eer_Trace
   file mkdir $env(HOME)/.eer_ToolDefs/eer_Tmp
   file mkdir $env(HOME)/.eer_ToolDefs/eer_Layout
   file mkdir $env(HOME)/.eer_ToolDefs/eer_Map
   file mkdir $env(HOME)/.eer_ToolDefs/eer_Scenario
   file mkdir $env(HOME)/.eer_ToolDefs/Macro

   #----- Installer les fichiers de definitions
   foreach file { eer_GRIBDict eer_AutoList eer_FileBoxPath eer_Host eer_SatDomain eer_WEBPath eer_ProjCam  } {
      if { ![file exists $env(HOME)/.eer_ToolDefs/$file] } {
         Log::Print INFO "Copying setup file : $file"
         file copy -force $GDefs(Dir)/Setup/Data/$file $env(HOME)/.eer_ToolDefs/$file
      }
   }

   foreach file { eer_Default  } {
      Log::Print INFO "Copying setup file : $file"
      file copy -force $GDefs(Dir)/Setup/Data/$file $env(HOME)/.eer_ToolDefs/$file
   }

   foreach file { eer_Map eer_Scenario } {
      Log::Print INFO "Copying setup file : $file"
      exec cp -r $GDefs(Dir)/Setup/Data/$file $env(HOME)/.eer_ToolDefs
   }

   Log::Print INFO "Installation done"
}

#----- Initialize environnement
set GDefs(Ver)           [regsub \[:alpha:\]+ [lindex [split [lindex [split [file dirname [file normalize [info script]]] /] end-1] -] end] ""]
set GDefs(DefFile)       .eer_Defs-$GDefs(Ver)
set GDefs(FrontEnd)      alef
set GDefs(FrontEndUser)  $env(USER)
set GDefs(TransmitUser)  afsiops
set GDefs(Model)         False

set GDefs(FrontEnds)        { alef dorval-ib }
set GDefs(BackEnds)         { saiph zeta maia }
set GDefs(BackEndmaia)      { { development } /home/binops/afse/eer /fs/ops/eer/afsr005 }
set GDefs(BackEndsaiph)     { { development } /home/binops/afse/eer /fs/ops/eer/afsr005 }
set GDefs(BackEndzeta)      { { development } /home/binops/afse/eer /fs/ops/eer/afsr005 }

set GDefs(DirData)        ""
set GDefs(DirCFSArch)     ""
set GDefs(DirMsg)         ""
set GDefs(DirWatch)       ""
set GDefs(Printers)       ""
set GDefs(Lang)           0

#----- Setup parameters
GetPrinters
GetPrevious

#----- Set default values
set GDefs(Arch)  [exec uname -s]
set GDefs(Dir)   [file dirname [info script]]/..
set GDefs(Group) [lindex [exec groups] 0]
set GDefs(DBMet) /data/gridpt/dbase

set GDefs(Colors)         { #ffffff #000000 #bfbfbf #7f7f7f #ff0000 #006464 #00ffff #0000ff  #ffff00 #ffa500 #ff1493 #da70d6 }
set GDefs(ColorEmerg)     red
set GDefs(ColorFrame)     gray80
set GDefs(ColorLight)     gray70
set GDefs(ColorOff)       gray60
set GDefs(ColorHighLight) yellow
set GDefs(Font)           -*-courier-*-r-*-*-12-*-*-*-*-*-*-*

option add *background $GDefs(ColorFrame)
option add *foreground black
option add *font       $GDefs(Font)

#----- Include needed packages
package require Icons
package require TabFrame
package require ComboBox
package require Dialog
package require FileBox
package require Log

#----- Crank it up
InstallWindow
