#===============================================================================
# Environnement Canada
# Centre Meteorologique Canadian
# 2100 Trans-Canadienne
# Dorval, Quebec
#
# Projet   : Librairie de "Widget" Tk.
# Fichier  : Mapper.int
# Creation : Juillet 2004 - J.P.Gauthier - CMC/CMOE
#
# Description:
#    Permet d'afficher un selecteur de donnees geographiques.
#
# Remarques :
#
#===============================================================================

#----------------------------------------------------------------------------
# Nom      : <Mapper::Window>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Affiche une boite permettant de selectionner des fichiers geotiff.
#
# Parametres   :
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc Mapper::Window { } {
   global GDefs
   variable Lbl
   variable Bubble
   variable Data
   variable Param

   set Data(Active) 1
   set Data(Canvas) $Page::Data(Canvas)
   set Data(Frame)  $Page::Data(Frame)

   set Param(Window) .mapper

   if { ![winfo exists .mapper] } {

      if { $Param(Dock) } {
         frame .mapper
         SPI::Dock .mapper
      } else {
         toplevel         .mapper
         wm title         .mapper "[lindex $SPI::Title(SPI) $GDefs(Lang)] $GDefs(Version) ([lindex $Param(Title) $GDefs(Lang)] $Param(Version))"
         eval wm geometry .mapper $Param(Geom)
         wm resizable     .mapper 1 1
         wm protocol      .mapper WM_DELETE_WINDOW { Mapper::Close }
      }

      frame .mapper.dock -relief raised -bd 1
         button .mapper.dock.sel -image DOCK -anchor w -relief flat -bd 0 -overrelief raised -command { SPI::DockTool Mapper }
         button .mapper.dock.del -image DOCKDELETE -anchor w -relief flat -bd 0 -overrelief raised -command Mapper::Close
         label .mapper.dock.info -textvariable Mapper::Data(Job) -relief sunken -bd 1 -anchor w -width 31 -bg $GDefs(ColorLight)
         pack .mapper.dock.sel .mapper.dock.del -side left
         pack .mapper.dock.info -side left -fill x -expand true
      pack .mapper.dock -side bottom -fill x

      panedwindow .mapper.data -orient vertical -showhandle False -opaqueresize True -bd 0
      pack .mapper.data -side top -fill both -expand true

      TabFrame::Create .mapper.data.tab1 1 ""
      TabFrame::Create .mapper.data.tab2 1 ""
      set Data(Tab1) [TabFrame::Add .mapper.data.tab1 1 [lindex $Lbl(Layer) $GDefs(Lang)] True]
      set Data(Tab2) [TabFrame::Add .mapper.data.tab2 1 [lindex $Lbl(Dig) $GDefs(Lang)] True]

      frame $Data(Tab2).head
         checkbutton $Data(Tab2).head.mode -variable Page::Data(ToolMode) -onvalue Mapper::DepotWare -offvalue SPI \
            -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
            -command  { SPI::ToolMode $Page::Data(ToolMode) Data True }
         button $Data(Tab2).head.reset -image RESET -bd 0 -relief flat -overrelief raised -relief raised -command "Mapper::DepotWare::Reset"
         button $Data(Tab2).head.params -image PARAMS -bd 0 -relief flat -overrelief raised -command "Mapper::DepotWare::Params"
         button $Data(Tab2).head.add -image PLUS -bd 0 -relief flat -overrelief raised -relief raised -command "Mapper::DepotWare::Window"
         button $Data(Tab2).head.del -image DELETE -bd 1 -relief flat -overrelief raised -command "Mapper::DepotWare::Del \$Mapper::DepotWare::Data(Select)"
         pack $Data(Tab2).head.mode $Data(Tab2).head.reset $Data(Tab2).head.params -side left -padx 2
         pack $Data(Tab2).head.del $Data(Tab2).head.add -side right -padx 2
      pack $Data(Tab2).head -side top -fill x

      frame $Data(Tab2).list
         canvas $Data(Tab2).list.canvas -bg white -relief sunken -bd 1 -yscrollcommand "$Data(Tab2).list.scrolly set" \
           -xscrollcommand "$Data(Tab2).list.scrollx set" -scrollregion "1 1 250 500" -width 1 -height 1
         scrollbar $Data(Tab2).list.scrolly -orient vertical -bd 1 -width 10 -command "$Data(Tab2).list.canvas yview"
         scrollbar $Data(Tab2).list.scrollx -orient horizontal -bd 1 -width 10 -command "$Data(Tab2).list.canvas xview"
         pack $Data(Tab2).list.scrollx -side bottom -fill x
         pack $Data(Tab2).list.canvas -side left -fill both -expand true
         pack $Data(Tab2).list.scrolly -side left -fill y
      pack $Data(Tab2).list -side top -fill both -expand true

      bind $Data(Tab2).list.canvas <Button-4> "$Data(Tab2).list.canvas yview scroll -1 units"
      bind $Data(Tab2).list.canvas <Button-5> "$Data(Tab2).list.canvas yview scroll 1 units"

      frame $Data(Tab1).head
         checkbutton $Data(Tab1).head.mode -variable Page::Data(ToolMode) -onvalue Mapper -offvalue SPI \
            -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
            -command  { SPI::ToolMode $Page::Data(ToolMode) Data True }
         button $Data(Tab1).head.open -image OPEN -bd 0 -relief flat -overrelief raised -relief raised -command "Mapper::Read \[FileBox::Create . \"\" LoadPath \[concat \[list \$FileBox::Type(ALL)\] \$Mapper::Data(GDALFormats) \$Mapper::Data(OGRFormats)\]\]"
         button $Data(Tab1).head.params -image PARAMS -bd 0 -relief flat -overrelief raised -command "Mapper::Params True"
         button $Data(Tab1).head.loc -image FINGER -bd 0 -relief flat -overrelief raised -command "Mapper::Locate"
         button $Data(Tab1).head.zoom -image MODEZOOM -bd 0 -relief flat -overrelief raised -command "Mapper::Zoom"
         checkbutton $Data(Tab1).head.ref -variable Mapper::Data(GeoRef) -offvalue "" -command "Mapper::SetGeoRef" \
            -image RESET -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame)
         pack $Data(Tab1).head.mode $Data(Tab1).head.open $Data(Tab1).head.params $Data(Tab1).head.loc $Data(Tab1).head.zoom -side left -padx 2

         frame $Data(Tab1).head.idx
            button $Data(Tab1).head.idx.down -image UP -bd 0 -relief flat -overrelief raised -command "Mapper::Scroll -1"
            button $Data(Tab1).head.idx.up -image DOWN -bd 0 -relief flat -overrelief raised -command "Mapper::Scroll 1"
            pack $Data(Tab1).head.idx.down $Data(Tab1).head.idx.up -side top -fill y -expand true -ipadx 3
         button $Data(Tab1).head.front -image FRONT -bd 0 -relief flat -overrelief raised -command "Mapper::Scroll F"
         button $Data(Tab1).head.back -image BACK -bd 0 -relief flat -overrelief raised -command "Mapper::Scroll B"
         button $Data(Tab1).head.del -image DELETE -bd 1 -relief flat -overrelief raised -command "Mapper::Del"
         pack $Data(Tab1).head.del $Data(Tab1).head.idx $Data(Tab1).head.back $Data(Tab1).head.front -side right -padx 2
      pack $Data(Tab1).head -side top -fill x

      frame $Data(Tab1).select
         listbox $Data(Tab1).select.list -relief sunken -bd 1 -selectmode single -width 1 -height 1 -background white \
            -yscrollcommand "$Data(Tab1).select.scrolly set" -xscrollcommand "$Data(Tab1).select.scrollx set" \
            -listvar Viewport::Data(Data) -selectbackground $GDefs(ColorHighLight) -selectforeground black
         scrollbar $Data(Tab1).select.scrolly -relief sunken -command "$Data(Tab1).select.list yview" -bd 1 -width 10
         scrollbar $Data(Tab1).select.scrollx -relief sunken -command "$Data(Tab1).select.list xview" -bd 1 -width 10 -orient horizontal
         pack $Data(Tab1).select.scrollx -side bottom -fill x
         pack $Data(Tab1).select.list -side left -fill both -expand true
         pack $Data(Tab1).select.scrolly -side left -fill y
      pack $Data(Tab1).select -side top -fill both -expand true

      .mapper.data add .mapper.data.tab1 -height 400
      .mapper.data add .mapper.data.tab2

      Mapper::DepotWare::Create
   }

   if { ![winfo exists .mappermenu] } {
      menu .mappermenu -type normal
         .mappermenu add command -image FINGER -compound left -label [lindex $Lbl(Locate) $GDefs(Lang)] -command "Mapper::Locate"
         .mappermenu add command -image MODEZOOM -compound left -label [lindex $Lbl(Zoom) $GDefs(Lang)] -command "Mapper::Zoom"
         .mappermenu add command -image LOCATION -compound left -label [lindex $Lbl(ZoomAll) $GDefs(Lang)] -command "Mapper::Zoom True"
         .mappermenu add command -image MODEZOOM -compound left -label [lindex $Lbl(ZoomFull) $GDefs(Lang)] -command "Mapper::ZoomFull"
         .mappermenu add separator
         .mappermenu add checkbutton -label [lindex $Lbl(Active) $GDefs(Lang)] -variable Mapper::Data(Active) -onvalue 1 -offvalue 0 \
            -command "Mapper::Toggle"
         .mappermenu add separator
         .mappermenu add command -image WORLD  -compound left -label [lindex $Lbl(GeoRef) $GDefs(Lang)] -command "Mapper::SetGeoRef"
         .mappermenu add command -image PARAMS -compound left -label [lindex $Lbl(Params) $GDefs(Lang)] -command "Mapper::Params True"
         .mappermenu add separator
         .mappermenu add command -image DELETE -compound left -label [lindex $Lbl(Del) $GDefs(Lang)] -command "Mapper::Del"
   }

   bind $Mapper::Data(Tab1).select.list <Button-3>  { Mapper::PopUp %X %Y %y }
   bind $Mapper::Data(Tab1).select.list <ButtonRelease-1>  { Mapper::Params }
   bind $Mapper::Data(Tab1).select.list <Double-ButtonRelease-1>  { Mapper::Params True }

   Bubble::Create .mapper.dock.sel          $Bubble(Dock)
   Bubble::Create .mapper.dock.del          $Bubble(Close)
   Bubble::Create $Data(Tab2).head.mode     $Bubble(DepotSelect)
   Bubble::Create $Data(Tab2).head.reset    $Bubble(DepotReset)
   Bubble::Create $Data(Tab2).head.add      $Bubble(DepotAdd)
   Bubble::Create $Data(Tab2).head.del      $Bubble(DepotDel)
   Bubble::Create $Data(Tab2).head.params   $Bubble(DepotParams)

   Bubble::Create $Data(Tab1).head.idx.up   $Bubble(Down)
   Bubble::Create $Data(Tab1).head.idx.down $Bubble(Up)
   Bubble::Create $Data(Tab1).head.del      $Bubble(Del)
   Bubble::Create $Data(Tab1).head.front    $Bubble(Front)
   Bubble::Create $Data(Tab1).head.back     $Bubble(Back)
   Bubble::Create $Data(Tab1).head.params   $Bubble(Params)
   Bubble::Create $Data(Tab1).head.loc      $Bubble(Locate)
   Bubble::Create $Data(Tab1).head.zoom     $Bubble(Zoom)
   Bubble::Create $Data(Tab1).head.mode     $Bubble(Mode)
   Bubble::Create $Data(Tab1).head.open     $Bubble(File)

   trace add variable Viewport::Data(Data) write Mapper::Toggler
   Viewport::FollowerAdd Mapper
}

proc Mapper::PopUp { X Y y } {
   variable Data

   $Data(Tab1).select.list selection clear 0 end;
   $Data(Tab1).select.list selection set [$Data(Tab1).select.list nearest $y]
   Mapper::Params

   set Data(Object) [$Data(Tab1).select.list get [$Data(Tab1).select.list nearest $y]]

   if { [ogrlayer is $Data(Object)] } {
      set Data(Active) [ogrlayer define $Data(Object) -active]
   } elseif { [gdalband is $Data(Object)] } {
      set Data(Active) [gdalband define $Data(Object) -active]
   } elseif { [model is $Data(Object)] } {
      set Data(Active) [model define $Data(Object) -active]
   } else {
      set Data(Active) 0
   }
   tk_popup .mappermenu $X $Y 0
}

proc Mapper::Toggle { } {
   variable Data

   if { [ogrlayer is $Data(Object)] } {
      ogrlayer define $Data(Object) -active $Data(Active)
   } elseif { [gdalband is $Data(Object)] } {
      gdalband define $Data(Object) -active $Data(Active)
   } elseif { [model is $Data(Object)] } {
      model define $Data(Object) -active $Data(Active)
   }
   Mapper::Toggler 0 0 0
   Page::Update $Page::Data(Frame)
}

proc Mapper::Toggler { Var1 Var2 Op } {
   variable Data

   set i      0
   set active 0

   foreach obj $Viewport::Data(Data) {
      if { [ogrlayer is $obj] } {
         set active [ogrlayer define $obj -active]
      } elseif { [gdalband is $obj] } {
         set active [gdalband define $obj -active]
      } elseif { [model is $obj] } {
         set active [model define $obj -active]
      }

      if { $active } {
         catch { $Data(Tab1).select.list itemconfigure $i -foreground black -selectforeground black }
      } else {
         catch { $Data(Tab1).select.list itemconfigure $i -foreground gray75 -selectforeground gray75 }
      }
      incr i
   }
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::ParamsClose>
# Creation :  2007 - Maxime Samuel - CMC/CMOE -
#
# But      : Détruire la fenetre des paramètres
#
# Parametres :
#
#-------------------------------------------------------------------------------

proc Mapper::ParamsClose { } {
    variable Data

    if { $Page::Data(ToolMode)=="Mapper::GeoLocator" } {
      SPI::ToolMode SPI Zoom
    }

    [Page::Canvas $Data(Frame)] delete MAPPERGEOLOCATOR
    Viewport::FollowerRemove Mapper::GeoLocator
    Page::Destroy $Data(Frame4).georef.hfrm.map
    Page::Update  $Data(Frame)
    destroy .mapperparams
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::Params>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE -
#
# But      : Selectionner la fenetre de parametres selon le type de donnees
#
# Parametres :
#
#-------------------------------------------------------------------------------

proc Mapper::Params { { Display False } } {
   variable Data

   if { ![winfo exists .mapperparams] && !$Display } {
      return
   }

   $Data(Canvas) configure -cursor watch
   .mapper configure -cursor watch
   update idletasks;
   if { [set idx [$Data(Tab1).select.list curselection]]!="" } {
      set obj [$Data(Tab1).select.list get $idx]
      if { $obj!=$Data(Object) || ![winfo exists .mapperparams] } {
         set Data(Object) $obj

         if { [gdalband is $Data(Object)] } {
            Mapper::ParamsGDALGet $Data(Object)
            Mapper::ParamsGDAL $Data(Object) 0
         } elseif { [ogrlayer is $Data(Object)] } {
            Mapper::ParamsOGRGet $Data(Object)
            Mapper::ParamsOGR $Data(Object) 0
         } elseif { [model is $Data(Object)] } {
            Mapper::ParamsModelGet $Data(Object)
            Mapper::ParamsModel $Data(Object) 0
         }
      }
   } else {
      destroy .mapperparams
   }

   $Data(Canvas) configure -cursor left_ptr
   .mapper configure -cursor left_ptr
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::ParamsGDAL>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Parametres des objets de type GDAL
#
# Parametres :
#   <Object> : Donnee geographique a parametrer
#   <Tab>    : Onglet par defaut
#
# Retour    :
#
# Remarque :
#
#-------------------------------------------------------------------------------

proc Mapper::ParamsGDAL { Object Tab } {
   global   GDefs
   variable Lbl
   variable Bubble
   variable Data

   if { [winfo exists .mapperparams] && $Data(Mode)!="gdalband" } {
      set pos [wm geometry .mapperparams]
      destroy .mapperparams
   } else {
      set pos 500x475
   }

   set Data(Mode)     gdalband
   set Data(RealTime) [expr $OpenGL::Param(Res)<=1]

   if { ![winfo exists .mapperparams] } {
      toplevel .mapperparams

      wm title     .mapperparams "[lindex $Lbl(GDAL) $GDefs(Lang)]"
      wm transient .mapperparams .
      wm geometry  .mapperparams =$pos
      wm protocol  .mapperparams WM_DELETE_WINDOW { Mapper::ParamsClose }

      TabFrame::Create .mapperparams.tab 1 Mapper::TabGDALSelect
      pack .mapperparams.tab -side top -fill both -expand true -padx 2 -pady 2

      set Data(Frame2) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Display) $GDefs(Lang)] False ""]

         labelframe $Data(Frame2).col -text [lindex $Lbl(ColorRef) $GDefs(Lang)]
            frame $Data(Frame2).col.sel
               checkbutton $Data(Frame2).col.sel.lock -image VCRLOCK -relief sunken -bd 1 -overrelief raised -offrelief flat -width 2 \
                  -variable Mapper::Data(Lock) -indicatoron False -onvalue True -offvalue False \
                  -command { Mapper::CurveDefine $Mapper::Data(Object) $Mapper::Data(Bands$Mapper::Data(Object)) }
               radiobutton $Data(Frame2).col.sel.red -relief sunken -bd 1 -overrelief raised -offrelief flat -width 2 -bg red \
                  -value red -variable Mapper::Data(Band) -indicatoron False -selectcolor red \
                  -command { Mapper::CurveSelect $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) }
               radiobutton  $Data(Frame2).col.sel.green -relief sunken -bd 1 -overrelief raised -offrelief flat -width 2 -bg green \
                  -value green -variable Mapper::Data(Band) -selectcolor green -indicatoron False \
                  -command { Mapper::CurveSelect $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) }
               radiobutton  $Data(Frame2).col.sel.blue -relief sunken -bd 1 -overrelief raised -offrelief flat -width 2 -bg blue \
                  -value blue -variable Mapper::Data(Band) -selectcolor blue -indicatoron False \
                  -command { Mapper::CurveSelect $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) }
               radiobutton  $Data(Frame2).col.sel.alpha -relief sunken -bd 1 -overrelief raised -offrelief flat -width 2 -bg white \
                  -value alpha -variable Mapper::Data(Band) -selectcolor white -indicatoron False \
                  -command { Mapper::CurveSelect $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) }
               pack  $Data(Frame2).col.sel.lock -side top -padx 5 -fill x
               pack $Data(Frame2).col.sel.red $Data(Frame2).col.sel.green $Data(Frame2).col.sel.blue \
                  $Data(Frame2).col.sel.alpha -side top -fill y -expand true -padx 5 -pady 5

            frame $Data(Frame2).col.def
               frame $Data(Frame2).col.def.curve
                  label $Data(Frame2).col.def.curve.lbl -text [lindex $Lbl(Curve) $GDefs(Lang)] -width 14 -anchor w
                  ComboBox::Create $Data(Frame2).col.def.curve.sel Mapper::Data(Curve) noedit unsorted nodouble -1 $Data(Curves) 1 8 { Mapper::CurveSet $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) }
                  pack $Data(Frame2).col.def.curve.lbl -side left
                  pack $Data(Frame2).col.def.curve.sel -side left -fill x -expand True
                frame $Data(Frame2).col.def.invert
                  label $Data(Frame2).col.def.invert.lbl -text [lindex $Lbl(Invert) $GDefs(Lang)] -width 14 -anchor w
                  checkbutton $Data(Frame2).col.def.invert.x -text " X " -width 1 -anchor w -indicatoron 0 \
                     -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) -variable Mapper::Data(CurveInvertX) \
                     -onvalue True -offvalue False -command { Mapper::CurveInvert $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) X $Mapper::Data(CurveInvertX) }
                     pack $Data(Frame2).col.def.curve.lbl -side left
                  checkbutton $Data(Frame2).col.def.invert.y -text " Y " -width 1 -anchor w -indicatoron 0 \
                     -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) -variable Mapper::Data(CurveInvertY) \
                     -onvalue True -offvalue False -command { Mapper::CurveInvert $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) Y $Mapper::Data(CurveInvertY) }
                  pack $Data(Frame2).col.def.invert.lbl -side left
                  pack $Data(Frame2).col.def.invert.x $Data(Frame2).col.def.invert.y -side left -fill x -expand True
              frame $Data(Frame2).col.def.tran
                  label $Data(Frame2).col.def.tran.lbl -text [lindex $Lbl(Tran) $GDefs(Lang)] -width 14 -anchor w
                  label $Data(Frame2).col.def.tran.txt -textvariable Mapper::Data(Tran) -width 3 -anchor w -relief sunken -bd 1
                  scale $Data(Frame2).col.def.tran.val -bd 1 -relief flat -width 15 -sliderlength 10 -from 0 -to 100 -variable Mapper::Data(Tran) -orient horizontal \
                     -showvalue False -command { if { $Mapper::Data(RealTime) } { Mapper::ParamsGDALSet $Mapper::Data(Object) }; catch }
                  pack $Data(Frame2).col.def.tran.lbl $Data(Frame2).col.def.tran.txt -side left
                  pack $Data(Frame2).col.def.tran.val -side left -fill x -expand True
              frame $Data(Frame2).col.def.res
                  label $Data(Frame2).col.def.res.lbl -text [lindex $Lbl(Resolution) $GDefs(Lang)] -width 14 -anchor w
                  label $Data(Frame2).col.def.res.txt -textvariable Mapper::Data(Resolution) -width 3 -anchor w -relief sunken -bd 1
                  scale $Data(Frame2).col.def.res.val -bd 1 -relief flat -width 15 -sliderlength 10 -from 1 -to 32 -variable Mapper::Data(Resolution) -orient horizontal -resolution 1\
                     -showvalue False -command { if { $Mapper::Data(RealTime) } { Mapper::ParamsGDALSet $Mapper::Data(Object) }; catch }
                  pack $Data(Frame2).col.def.res.lbl $Data(Frame2).col.def.res.txt -side left
                  pack $Data(Frame2).col.def.res.val -side left -fill x -expand True
               frame $Data(Frame2).col.def.interp
                  label $Data(Frame2).col.def.interp.lbl -text [lindex $Lbl(Interp) $GDefs(Lang)] -width 14 -anchor w
                  ComboBox::Create $Data(Frame2).col.def.interp.val Mapper::Data(Interp) noedit sorted nodouble -1 $Data(Interps) 1 8 "if { \$Mapper::Data(RealTime) } { Mapper::ParamsGDALSet \$Mapper::Data(Object) }"
                  pack $Data(Frame2).col.def.interp.lbl -side left
                  pack $Data(Frame2).col.def.interp.val -side left -fill x -expand True
               frame $Data(Frame2).col.def.nodata
                  label $Data(Frame2).col.def.nodata.lbl -text [lindex $Lbl(NoData) $GDefs(Lang)] -width 14 -anchor w
                  entry $Data(Frame2).col.def.nodata.sel -textvariable Mapper::Data(NoData) -bd 1 -bg $GDefs(ColorLight)
                  pack $Data(Frame2).col.def.nodata.lbl -side left
                  pack $Data(Frame2).col.def.nodata.sel -side left -fill x -expand True
               pack $Data(Frame2).col.def.curve $Data(Frame2).col.def.invert $Data(Frame2).col.def.tran $Data(Frame2).col.def.res \
                   $Data(Frame2).col.def.interp $Data(Frame2).col.def.nodata -side top -fill x
            frame $Data(Frame2).col.curve
               button $Data(Frame2).col.curve.map -bd 2 -relief groove -image GDALMAPImg \
                  -command  { MapBox::Create $Mapper::Data(Frame2).col.curve.map "colormap image \$Mapper::Data(ColorMap) GDALMAPImg; Mapper::ParamsGDALSet \$Mapper::Data(Object); if { ![glrender -shaderavailable] } { gdalband clean \$Mapper::Data(Object) }; Page::Update \$Page::Data(Frame)" $Mapper::Data(ColorMap) }
                canvas $Data(Frame2).col.curve.cv -relief sunken -bd 1 -bg $GDefs(ColorLight) -width 256 -height 128
               frame $Data(Frame2).col.curve.info
                  entry $Data(Frame2).col.curve.info.min -textvariable Mapper::Data(CurveMinD) -width 8 -bd 1
                  entry $Data(Frame2).col.curve.info.now -textvariable Mapper::Data(CurveValue) -width 1 -relief sunken -bd 1
                  label $Data(Frame2).col.curve.info.nb -textvariable Mapper::Data(CurveNb) -width 6 -relief sunken -bd 1
                  entry $Data(Frame2).col.curve.info.max -textvariable Mapper::Data(CurveMaxD) -width 8 -bd 1
                  pack $Data(Frame2).col.curve.info.min -side left
                  pack $Data(Frame2).col.curve.info.now -side left -fill x -expand True
                  pack $Data(Frame2).col.curve.info.nb -side left
                  pack $Data(Frame2).col.curve.info.max -side left
               frame $Data(Frame2).col.curve.limit
                  entry $Data(Frame2).col.curve.limit.min -textvariable Mapper::Data(CurveMin) -width 8 -state disabled -bd 1
                  entry $Data(Frame2).col.curve.limit.max -textvariable Mapper::Data(CurveMax) -width 8 -state disabled -bd 1
                  checkbutton $Data(Frame2).col.curve.limit.histo -variable Mapper::Data(HistoShow) -onvalue True -offvalue False \
                     -text [lindex $Lbl(Histo) $GDefs(Lang)] -relief sunken -bd 1 -overrelief raised -offrelief flat \
                     -indicatoron False -command { Mapper::CurveHisto }
                  pack $Data(Frame2).col.curve.limit.min -side left
                  pack $Data(Frame2).col.curve.limit.histo -side left -fill x -expand True
                  pack $Data(Frame2).col.curve.limit.max -side right
               pack $Data(Frame2).col.curve.map -side top -fill x
               pack $Data(Frame2).col.curve.cv -side top -fill both
               pack $Data(Frame2).col.curve.info $Data(Frame2).col.curve.limit -side top -fill x
            pack $Data(Frame2).col.curve -side left -padx 5
            pack $Data(Frame2).col.sel -side left -fill y
            pack $Data(Frame2).col.def -side left -padx 5 -fill x -anchor nw -expand True
         pack $Data(Frame2).col -side top -fill x -padx 5 -pady 5 -ipady 5

         labelframe $Data(Frame2).band -text [lindex $Lbl(Band) $GDefs(Lang)]
            frame $Data(Frame2).band.red
               label $Data(Frame2).band.red.lbl -text [lindex $Lbl(Red) $GDefs(Lang)] -width 6 -anchor w
               ComboBox::Create $Data(Frame2).band.red.val Mapper::Data(Red) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame2).band.red.lbl -side left
               pack $Data(Frame2).band.red.val -side left -fill x -expand true
            frame $Data(Frame2).band.green
               label $Data(Frame2).band.green.lbl -text [lindex $Lbl(Green) $GDefs(Lang)] -width 6 -anchor w
               ComboBox::Create $Data(Frame2).band.green.val Mapper::Data(Green) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame2).band.green.lbl -side left
               pack $Data(Frame2).band.green.val -side left -fill x -expand true
            frame $Data(Frame2).band.blue
               label $Data(Frame2).band.blue.lbl -text [lindex $Lbl(Blue) $GDefs(Lang)] -width 6 -anchor w
               ComboBox::Create $Data(Frame2).band.blue.val Mapper::Data(Blue) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame2).band.blue.lbl -side left
               pack $Data(Frame2).band.blue.val -side left -fill x -expand true
            frame $Data(Frame2).band.alpha
               label $Data(Frame2).band.alpha.lbl -text [lindex $Lbl(Alpha) $GDefs(Lang)] -width 6 -anchor w
               ComboBox::Create $Data(Frame2).band.alpha.val Mapper::Data(Alpha) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame2).band.alpha.lbl -side left
               pack $Data(Frame2).band.alpha.val -side left -fill x -expand true
            pack $Data(Frame2).band.red -side top -fill x -padx 5
            pack $Data(Frame2).band.green -side top -fill x -padx 5
            pack $Data(Frame2).band.blue -side top -fill x -padx 5
            pack $Data(Frame2).band.alpha -side top -fill x -padx 5
         pack $Data(Frame2).band -side top -fill x -padx 5 -pady 5 -ipady 2

         labelframe $Data(Frame2).pos -text [lindex $Lbl(Position) $GDefs(Lang)]
            frame $Data(Frame2).pos.topo
               label $Data(Frame2).pos.topo.lbl -text  [lindex $Lbl(Topo) $GDefs(Lang)] -width 12 -anchor nw
               ComboBox::Create $Data(Frame2).pos.topo.sel Mapper::Data(Topo) noedit unsorted nodouble -1 "" 1 8 ""
               label $Data(Frame2).pos.topo.x -text "x" -width 1 -anchor nw
               entry $Data(Frame2).pos.topo.fac  -bg $GDefs(ColorLight) -textvariable Mapper::Data(TopoFactor) -bd 1 -width 5
               pack $Data(Frame2).pos.topo.lbl $Data(Frame2).pos.topo.fac $Data(Frame2).pos.topo.x -side left -fill both
               pack $Data(Frame2).pos.topo.sel -side left -fill x -expand true
            frame $Data(Frame2).pos.sample
               label $Data(Frame2).pos.sample.lbl -text [lindex $Lbl(Sample) $GDefs(Lang)] -width 12 -anchor w
               label $Data(Frame2).pos.sample.txt -textvariable Mapper::Data(Sample) -width 5 -anchor w -relief sunken -bd 1
               scale $Data(Frame2).pos.sample.val -bd 1 -relief flat -width 15 -sliderlength 10 -from 2 -to 32 -variable Mapper::Data(Sample) -orient horizontal -showvalue False
               pack $Data(Frame2).pos.sample.lbl $Data(Frame2).pos.sample.txt  -side left
               pack $Data(Frame2).pos.sample.val -side left -fill x -expand true
            pack  $Data(Frame2).pos.topo $Data(Frame2).pos.sample -side top -fill x -padx 5
            frame $Data(Frame2).pos.crop
               label $Data(Frame2).pos.crop.lbl -text [lindex $Lbl(Cut) $GDefs(Lang)] -width 12 -anchor w
               checkbutton $Data(Frame2).pos.crop.active -text [lindex $Lbl(Clip) $GDefs(Lang)] -width 1 -anchor w -indicatoron 0 \
                  -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) -variable Mapper::Data(Cut) \
                  -onvalue True -offvalue False -command { Mapper::ParamsGDALSet $Mapper::Data(Object) }
               checkbutton $Data(Frame2).pos.crop.show -text [lindex $Lbl(Show) $GDefs(Lang)] -anchor w -indicatoron 0 \
                  -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) -variable Mapper::Data(CutShow) \
                  -onvalue True -offvalue False -command { Mapper::UpdateItems $Mapper::Data(Frame) }
               checkbutton $Data(Frame2).pos.crop.mode -variable Page::Data(ToolMode) -onvalue Mapper::Cutter -offvalue SPI \
                  -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
                  -command { SPI::ToolMode $Page::Data(ToolMode) Draw True; set Mapper::Data(CutShow) True }
               pack $Data(Frame2).pos.crop.lbl $Data(Frame2).pos.crop.mode $Data(Frame2).pos.crop.show -side left -fill y
               pack $Data(Frame2).pos.crop.active -side left -fill both -expand True
            pack $Data(Frame2).pos.crop -side top -fill x -padx 5
         pack $Data(Frame2).pos -side top -fill x -padx 5 -pady 5 -ipady 2

         Bubble::Create $Data(Frame2).col.curve.map $Bubble(ColorMap)
         Bubble::Create $Data(Frame2).col.sel $Bubble(CurveBand)
         Bubble::Create $Data(Frame2).col.def.curve $Bubble(Curve)
         Bubble::Create $Data(Frame2).col.def.tran $Bubble(Transparency)
         Bubble::Create $Data(Frame2).col.def.res $Bubble(Resolution)
         Bubble::Create $Data(Frame2).col.def.interp $Bubble(Interp)
         Bubble::Create $Data(Frame2).col.def.nodata $Bubble(NoData)
         Bubble::Create $Data(Frame2).col.curve.info.min $Bubble(CurveMin)
         Bubble::Create $Data(Frame2).col.curve.limit.histo  $Bubble(Histo)
         Bubble::Create $Data(Frame2).col.curve.info.now $Bubble(CurveNow)
         Bubble::Create $Data(Frame2).col.curve.info.nb $Bubble(CurveNb)
         Bubble::Create $Data(Frame2).col.curve.info.max $Bubble(CurveMax)
         Bubble::Create $Data(Frame2).col.curve.limit.min $Bubble(CurveLimitMin)
         Bubble::Create $Data(Frame2).col.curve.limit.max $Bubble(CurveLimitMax)
         Bubble::Create $Data(Frame2).band $Bubble(Band)
         Bubble::Create $Data(Frame2).pos.topo.fac $Bubble(TopoFactor)
         Bubble::Create $Data(Frame2).pos.topo.sel $Bubble(Topo)
         Bubble::Create $Data(Frame2).pos.sample.val $Bubble(Sample)
         Bubble::Create $Data(Frame2).pos.crop.active $Bubble(CropTool)
         Bubble::Create $Data(Frame2).pos.crop.show $Bubble(CropShow)
         Bubble::Create $Data(Frame2).pos.crop.mode $Bubble(CropDo)

      set Data(Frame1) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Ref) $GDefs(Lang)] False ""]

        frame $Data(Frame1).projhead
            label $Data(Frame1).projhead.lbl -text [lindex $Lbl(Projection) $GDefs(Lang)] -width 10 -anchor nw
            button $Data(Frame1).projhead.file -image OPEN -relief flat -bd 0 -overrelief raised \
               -command "Mapper::ProjFile $Data(Frame1).proj.val \[FileBox::Create . \"\" Load \[list \$FileBox::Type(PROJ) \$FileBox::Type(TXT)\]\]"
            button $Data(Frame1).projhead.tab -image INTEROGATE -relief flat -bd 0 -overrelief raised \
               -command { set Mapper::Data(Proj) [Mapper::WKT::Param $Mapper::Data(Proj)]; $Mapper::Data(Frame1).proj.val delete 0.0 end ; $Mapper::Data(Frame1).proj.val insert end $Mapper::Data(Proj); Mapper::ParamsGDALSet $Mapper::Data(Object) }
            pack $Data(Frame1).projhead.lbl -side left
            pack $Data(Frame1).projhead.tab $Data(Frame1).projhead.file -side left -padx 2
         labelframe $Data(Frame1).proj -labelwidget $Data(Frame1).projhead
            text $Data(Frame1).proj.val -bd 1 -bg $GDefs(ColorLight) -height 5 -width 25
            pack $Data(Frame1).proj.val -side right -fill both -expand true -padx 5 -pady 5
         pack  $Data(Frame1).proj -side top -fill both -expand true -padx 5 -pady 5

         labelframe $Data(Frame1).pos -text [lindex $Lbl(Position) $GDefs(Lang)]
            frame $Data(Frame1).pos.x
               label $Data(Frame1).pos.x.lbl -text X -width 6 -anchor w
               ComboBox::Create $Data(Frame1).pos.x.val Mapper::Data(BandX) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame1).pos.x.lbl -side left
               pack $Data(Frame1).pos.x.val -side left -fill x -expand true
            frame $Data(Frame1).pos.y
               label $Data(Frame1).pos.y.lbl -text Y -width 6 -anchor w
               ComboBox::Create $Data(Frame1).pos.y.val Mapper::Data(BandY) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame1).pos.y.lbl -side left
               pack $Data(Frame1).pos.y.val -side left -fill x -expand true
            pack $Data(Frame1).pos.x -side top -fill x -padx 5
            pack $Data(Frame1).pos.y -side top -fill x -padx 5
         pack $Data(Frame1).pos -side top -fill x -padx 5 -pady 5 -ipady 2

         labelframe $Data(Frame1).trans -text [lindex $Lbl(Trans) $GDefs(Lang)]
            frame $Data(Frame1).trans.fwd
               label $Data(Frame1).trans.fwd.lbl -text [lindex $Lbl(Forward) $GDefs(Lang)] -width 8 -anchor nw
               entry $Data(Frame1).trans.fwd.entry -textvariable Mapper::Data(Trans) -bd 1 -bg $GDefs(ColorLight) -width 25
               pack $Data(Frame1).trans.fwd.lbl -side left
               pack $Data(Frame1).trans.fwd.entry -fill x -expand true -side left
            frame $Data(Frame1).trans.bck
               label $Data(Frame1).trans.bck.lbl -text [lindex $Lbl(Backward) $GDefs(Lang)] -width 8 -anchor nw
               entry $Data(Frame1).trans.bck.entry -textvariable Mapper::Data(InvTrans) -bd 1 -bg $GDefs(ColorLight) -width 25
               pack $Data(Frame1).trans.bck.lbl -side left
               pack $Data(Frame1).trans.bck.entry -fill x -expand true -side left
            pack $Data(Frame1).trans.fwd $Data(Frame1).trans.bck -fill x -side top -padx 5
         pack $Data(Frame1).trans -side top -fill x -padx 5 -pady 5 -ipady 2

         Bubble::Create $Data(Frame1).projhead.file $Bubble(ProjLoad)
         Bubble::Create $Data(Frame1).projhead.tab  $Bubble(ProjRef)
         Bubble::Create $Data(Frame1).proj $Bubble(Projection)
         Bubble::Create $Data(Frame1).trans $Bubble(Transform)
         Bubble::Create $Data(Frame1).pos   $Bubble(PosArray)

      set Data(Frame3) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Meta) $GDefs(Lang)] False ""]
         frame $Data(Frame3).meta
            text $Data(Frame3).meta.text -relief sunken -bd 1 -wrap none -bg $GDefs(ColorLight) -yscrollcommand "$Data(Frame3).meta.scroll set" -width 30
            scrollbar $Data(Frame3).meta.scroll -relief sunken -command "$Data(Frame3).meta.text yview" -bd 1 -width 10
            pack $Data(Frame3).meta.text -side left -fill both -expand true
            pack $Data(Frame3).meta.scroll -side left -fill y
         pack $Data(Frame3).meta -side top -fill both -expand true -padx 5 -pady 5

      set Data(Frame4) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(GeoLoc) $GDefs(Lang)] False ""]

         frame $Data(Frame4).georef
            frame $Data(Frame4).georef.tool
               button $Data(Frame4).georef.tool.add -image PINADD -relief raised -bd 0 -overrelief raised -command Mapper::GeoLocator::CoordAdd
               button $Data(Frame4).georef.tool.del -image PINDEL -relief raised -bd 0 -overrelief raised -command Mapper::GeoLocator::CoordDel
               checkbutton $Data(Frame4).georef.tool.mode -variable Page::Data(ToolMode) -onvalue Mapper::GeoLocator -offvalue SPI \
                  -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
                  -command { SPI::ToolMode $Page::Data(ToolMode) Data True }
               button $Data(Frame4).georef.tool.calcul -text [lindex $Lbl(Calcul) $GDefs(Lang)] -relief raised -bd 0 -overrelief raised -command Mapper::GeoLocator::CoordSet
               ComboBox::Create $Data(Frame4).georef.tool.fit Mapper::GeoLocator::Data(Mode) noedit unsorted nodouble -1 $Mapper::GeoLocator::Data(Modes) 10 10 ""
               pack $Data(Frame4).georef.tool.mode $Data(Frame4).georef.tool.add $Data(Frame4).georef.tool.del \
                  $Data(Frame4).georef.tool.fit $Data(Frame4).georef.tool.calcul -side left -padx 2
            pack $Data(Frame4).georef.tool -side top -fill x

            frame $Data(Frame4).georef.pt
               listbox $Data(Frame4).georef.pt.list -width 16 -height 1 -bd 1 -relief sunken -exportselection False \
                  -listvariable Mapper::GeoLocator::Data(GCPS) -yscrollcommand "$Data(Frame4).georef.pt.scroll set" -selectmode single -bg white
               bind $Data(Frame4).georef.pt.list <ButtonRelease-1> { Mapper::GeoLocator::CoordView }
               scrollbar $Data(Frame4).georef.pt.scroll -relief sunken -command "$Data(Frame4).georef.pt.list yview" -bd 1 -width 10
               pack $Data(Frame4).georef.pt.list -side left -fill y
               pack $Data(Frame4).georef.pt.scroll -side right -fill y
            pack $Data(Frame4).georef.pt -side left -fill y -padx 2 -pady 5

            frame $Data(Frame4).georef.hfrm
               Page::Create $Data(Frame4).georef.hfrm.map -1 -1
               pack $Data(Frame4).georef.hfrm.map -side top -expand true -fill both

               frame $Data(Frame4).georef.hfrm.info -relief sunken -bd 1
                  frame $Data(Frame4).georef.hfrm.info.xy
                     label $Data(Frame4).georef.hfrm.info.xy.n -relief raised -bd 1 -text "    "
                     label $Data(Frame4).georef.hfrm.info.xy.x -relief raised -bd 1 -text X
                     label $Data(Frame4).georef.hfrm.info.xy.y -relief raised -bd 1 -text Y
                     pack $Data(Frame4).georef.hfrm.info.xy.n $Data(Frame4).georef.hfrm.info.xy.x $Data(Frame4).georef.hfrm.info.xy.y \
                        -side top -fill both
                  frame $Data(Frame4).georef.hfrm.info.img
                     label $Data(Frame4).georef.hfrm.info.img.lbl -text [lindex $Lbl(Image) $GDefs(Lang)] -width 5 -anchor w -relief raised -bd 1
                     label $Data(Frame4).georef.hfrm.info.img.x -textvariable Mapper::GeoLocator::Data(X) -width 15 -bg $GDefs(ColorLight) -bd 1
                     label $Data(Frame4).georef.hfrm.info.img.y -textvariable Mapper::GeoLocator::Data(Y) -width 15 -bg $GDefs(ColorLight) -bd 1
                     pack  $Data(Frame4).georef.hfrm.info.img.lbl $Data(Frame4).georef.hfrm.info.img.x $Data(Frame4).georef.hfrm.info.img.y \
                        -side top -fill x
                  frame $Data(Frame4).georef.hfrm.info.ref
                     label $Data(Frame4).georef.hfrm.info.ref.lbl -text [lindex $Lbl(Ref) $GDefs(Lang)] -width 5 -anchor w -relief raised -bd 1
                     label $Data(Frame4).georef.hfrm.info.ref.x -textvariable Mapper::GeoLocator::Data(RefX) -width 15 -bg $GDefs(ColorLight) -bd 1
                     label $Data(Frame4).georef.hfrm.info.ref.y -textvariable Mapper::GeoLocator::Data(RefY) -width 15 -bg $GDefs(ColorLight) -bd 1
                     pack  $Data(Frame4).georef.hfrm.info.ref.lbl $Data(Frame4).georef.hfrm.info.ref.x $Data(Frame4).georef.hfrm.info.ref.y \
                        -side top -fill x
                  frame $Data(Frame4).georef.hfrm.info.coo
                     label $Data(Frame4).georef.hfrm.info.coo.lbl -text [lindex $Lbl(Coord) $GDefs(Lang)] -width 5 -anchor w -relief raised -bd 1
                     label $Data(Frame4).georef.hfrm.info.coo.x -textvariable Mapper::GeoLocator::Data(Lon) -width 15 -bg $GDefs(ColorLight) -bd 1
                     label $Data(Frame4).georef.hfrm.info.coo.y -textvariable Mapper::GeoLocator::Data(Lat) -width 15 -bg $GDefs(ColorLight) -bd 1
                     pack  $Data(Frame4).georef.hfrm.info.coo.lbl $Data(Frame4).georef.hfrm.info.coo.x $Data(Frame4).georef.hfrm.info.coo.y \
                        -side top -fill x
                  pack $Data(Frame4).georef.hfrm.info.xy -side left
                  pack $Data(Frame4).georef.hfrm.info.img $Data(Frame4).georef.hfrm.info.ref $Data(Frame4).georef.hfrm.info.coo \
                     -side left -fill x -expand True
               pack $Data(Frame4).georef.hfrm.info -side top -fill x
            pack $Data(Frame4).georef.hfrm -side left -fill both -expand True -padx 2 -pady 5
         pack $Data(Frame4).georef -side top -expand true -fill both -padx 5 -pady 5

         Bubble::Create $Data(Frame4).georef.tool.add    $Bubble(RefAdd)
         Bubble::Create $Data(Frame4).georef.tool.del    $Bubble(RefDel)
         Bubble::Create $Data(Frame4).georef.tool.mode   $Bubble(RefMode)
         Bubble::Create $Data(Frame4).georef.tool.calcul $Bubble(RefCalcul)
         Bubble::Create $Data(Frame4).georef.tool.fit    $Bubble(RefFit)

      frame .mapperparams.com
         checkbutton .mapperparams.com.real -image DOCSEL -bd 1 -relief raised \
            -variable Mapper::Data(RealTime) -onvalue 1 -offvalue 0 -indicatoron false
         button .mapperparams.com.apply  -text [lindex $Lbl(Apply) $GDefs(Lang)] -bd 1 -command { Mapper::ParamsGDALSet $Mapper::Data(Object) }
         button .mapperparams.com.cancel -text [lindex $Lbl(Close) $GDefs(Lang)] -bd 1 -command { Mapper::ParamsClose }
         pack .mapperparams.com.cancel .mapperparams.com.apply .mapperparams.com.real -side right
      pack .mapperparams.com -side top -fill x -padx 2 -pady 2

      TabFrame::Select .mapperparams.tab $Tab
   }

   $Data(Frame1).proj.val delete 0.0 end
   $Data(Frame1).proj.val insert end $Data(Proj)

   $Data(Frame3).meta.text delete 0.0 end
   $Data(Frame3).meta.text insert 0.0 $Data(Meta)

   ComboBox::DelAll  $Data(Frame2).pos.topo.sel False
   ComboBox::AddList  $Data(Frame2).pos.topo.sel [concat $Mapper::Data(Topos) $Mapper::Data(Colors) [gdalband all]]

   ComboBox::DelAll $Data(Frame2).band.red.val False
   ComboBox::Add $Data(Frame2).band.red.val ""
   ComboBox::AddList $Data(Frame2).band.red.val $Data(Band$Object)
   ComboBox::DelAll $Data(Frame2).band.green.val False
   ComboBox::Add $Data(Frame2).band.green.val ""
   ComboBox::AddList $Data(Frame2).band.green.val $Data(Band$Object)
   ComboBox::DelAll $Data(Frame2).band.blue.val False
   ComboBox::Add $Data(Frame2).band.blue.val ""
   ComboBox::AddList $Data(Frame2).band.blue.val $Data(Band$Object)
   ComboBox::DelAll $Data(Frame2).band.alpha.val False
   ComboBox::Add $Data(Frame2).band.alpha.val ""
   ComboBox::AddList $Data(Frame2).band.alpha.val $Data(Band$Object)

   ComboBox::DelAll $Data(Frame1).pos.x.val False
   ComboBox::Add $Data(Frame1).pos.x.val ""
   ComboBox::AddList $Data(Frame1).pos.x.val $Data(Band$Object)
   ComboBox::DelAll $Data(Frame1).pos.y.val False
   ComboBox::Add $Data(Frame1).pos.y.val ""
   ComboBox::AddList $Data(Frame1).pos.y.val $Data(Band$Object)

   bind $Data(Frame2).col.curve.info.min <Any-KeyRelease>  { catch { Mapper::CurveSelect $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) False } }
   bind $Data(Frame2).col.curve.info.max <Any-KeyRelease>  { catch { Mapper::CurveSelect $Mapper::Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band) False } }

   Mapper::CurveDefine $Object $Data(Bands$Object)
}

proc Mapper::TabGDALSelect { Frame No } {
   variable Data

   if { $No==3 } {
      Mapper::GeoLocator::Activate $Data(Object)
   } else {
   }
}

proc Mapper::CurveDefine { Object Bands } {
   variable Data

   set Data(MMSet) 0
   set Data(Band) red

   set canvas $Data(Frame2).col.curve.cv
   $Data(Frame2).col.sel.red   configure -state disabled -bg gray75
   $Data(Frame2).col.sel.green configure -state disabled -bg gray75
   $Data(Frame2).col.sel.blue  configure -state disabled -bg gray75
   $Data(Frame2).col.sel.alpha configure -state disabled -bg gray75

   #----- Setup colormap

   set Data(ColorMap) [gdalband configure $Object -colormap]
   colormap image $Data(ColorMap) GDALMAPImg

   #----- Setup curves

   $canvas delete all
   $canvas create polygon 0 0 0 0 -fill gray50 -outline gray25 -width 1 -tags "histo"

   if { ![set Data(Indexed) [gdalband define $Object -indexed]] } {
      if { $Mapper::Data(Red)!="" } {
         $Data(Frame2).col.sel.red configure -state normal -bg red
         $canvas create line 0 0 0 0 -fill red -width 1 -tags "curve red"
      }
      if { $Mapper::Data(Green)!="" && !$Data(Lock) } {
         $Data(Frame2).col.sel.green configure -state normal -bg green
         $canvas create line 0 0 0 0 -fill green -width 1 -tags "curve green"
      }
      if { $Mapper::Data(Blue)!="" && !$Data(Lock) } {
         $Data(Frame2).col.sel.blue configure -state normal -bg blue
         $canvas create line 0 0 0 0 -fill blue -width 1 -tags "curve blue"
      }
      if { $Mapper::Data(Alpha)!="" && !$Data(Lock) } {
         $Data(Frame2).col.sel.alpha configure -state normal -bg white
         $canvas create line 0 0 0 0 -fill white -width 1 -tags "curve alpha"
      }

      $canvas create line 1   0 1   128 1   64  10  64 -fill black  -width 2 -tags "min" -arrow last -arrowshape { 10 10 10 }
      $canvas create line 256 0 256 128 256 64  246 64 -fill black  -width 2 -tags "max" -arrow last -arrowshape { 10 10 10 }

      #----- Bind canvas events

      $canvas bind min <ButtonPress-1>   "set Mapper::Data(MMSet) 1; $canvas configure -cursor hand1"
      $canvas bind max <ButtonPress-1>   "set Mapper::Data(MMSet) 1; $canvas configure -cursor hand1"
      $canvas bind min <B1-Motion>       "Mapper::CurveRange $canvas \$Mapper::Data(ColorMap) \$Mapper::Data(Band) min %x"
      $canvas bind max <B1-Motion>       "Mapper::CurveRange $canvas \$Mapper::Data(ColorMap) \$Mapper::Data(Band) max %x"
      $canvas bind min <ButtonRelease-1> "set Mapper::Data(MMSet) 0; $canvas configure -cursor left_ptr"
      $canvas bind max <ButtonRelease-1> "set Mapper::Data(MMSet) 0; $canvas configure -cursor left_ptr"

      bind $canvas <B1-Motion>       "if { !\$Mapper::Data(MMSet) } {  Mapper::CurvePoint $canvas \$Mapper::Data(ColorMap) \$Mapper::Data(Band) %x %y }"
      bind $canvas <ButtonPress-2>   "set Mapper::Data(CurveX) %x; $canvas configure -cursor hand1"
      bind $canvas <B2-Motion>       "if { !\$Mapper::Data(MMSet) } { Mapper::CurveTranslate $canvas \$Mapper::Data(ColorMap) \$Mapper::Data(Band) %x }"
      bind $canvas <ButtonRelease-2> "set Mapper::Data(CurveX) %x; $canvas configure -cursor left_ptr"
   }
   bind $canvas <Motion> { Mapper::CurveValue %x }

   Mapper::CurveSelect $canvas $Data(ColorMap) red
   Mapper::Curve       $canvas $Data(ColorMap) $Bands
}

proc Mapper::CurveInvert { Canvas Map Band Axis Value } {
   variable Data

   if { [llength $Data(Bands$Data(Object))]==1 || $Data(Lock) } {
      set band rgba
   } else {
      set band $Band
   }

   if { $Axis=="X" } {
      colormap configure $Map -invertx $band $Value
   } else {
      colormap configure $Map -inverty $band $Value
   }
   if { ![glrender -shaderavailable] } { gdalband clean $Data(Object) }
   Mapper::Curve $Canvas $Mapper::Data(ColorMap) $Mapper::Data(Band)
}

proc Mapper::CurveHisto { } {
   variable Data

   . configure -cursor watch
   .mapper configure -cursor watch
   .mapperparams configure -cursor watch
   update idletasks

   if { [set idx [lsearch -exact { red green blue alpha } $Data(Band)]]!=-1 } {
      if { ![info exists Data(Histo$Data(Object)$idx)] } {
         set Data(Histo$Data(Object)$idx) [gdalband stats $Data(Object) -histogram $idx]
      }
   }
   set Data(HistoShow) False
   Mapper::CurveSelect $Data(Frame2).col.curve.cv $Mapper::Data(ColorMap) $Mapper::Data(Band)

   . configure -cursor left_ptr
   .mapper configure -cursor left_ptr
   .mapperparams configure -cursor left_ptr
}

proc Mapper::CurveSelect { Canvas Map Band { MinMax True } } {
   variable Data

   if { [set idx [lsearch -exact { red green blue alpha } $Band]]!=-1 } {

      #----- Reconfigure curve display

      $Canvas itemconfigure curve -width 1
      $Canvas itemconfigure $Band -width 2
      $Canvas raise $Band
      $Canvas raise min
      $Canvas raise max

      #----- Setup min-max

      set Data(CurveMin) [lindex [gdalband stats $Data(Object) -min $idx] 0]
      set Data(CurveMax) [lindex [gdalband stats $Data(Object) -max $idx] 0]

      if { $Data(CurveMin)>[colormap configure $Map -min $Band] } {
         colormap configure $Map -min $Band $Data(CurveMin)
         catch { unset Data(Histo$Data(Object)$idx) }
      }
      if { $Data(CurveMax)<[colormap configure $Map -max $Band] } {
         colormap configure $Map -max $Band $Data(CurveMax)
         catch { unset Data(Histo$Data(Object)$idx) }
      }

      if { $Data(CurveMin)=="" || $Data(CurveMax)=="" } {
         return
      }

      #----- Setup histogram

      set coords { }
      set Data(Histo)    {}
      set Data(HistoMin) 1e300
      set Data(HistoMax) -1e300

      if { [info exists Data(Histo$Data(Object)$idx)] } {
         set Data(Histo) $Data(Histo$Data(Object)$idx)
      }

      foreach h $Data(Histo) {
         set Data(HistoMax) [expr $Data(HistoMax)>$h?$Data(HistoMax):$h]
         set Data(HistoMin) [expr $Data(HistoMin)<$h?$Data(HistoMin):$h]
      }

      if { [llength $Data(Histo)] && $Data(HistoMax)!=$Data(HistoMin) } {
         set dy [expr 128.0/($Data(HistoMax)-$Data(HistoMin))]
         set x -1
         lappend coords 0 128
         foreach h $Data(Histo) {
            lappend coords [incr x] [expr 128.0-($h-$Data(HistoMin))*$dy]
         }
         lappend coords 256 128
      } else {
         set coords  { 0 0 0 0 0 0 0 0}
      }

      $Canvas coords histo $coords

      #----- Setup curves

      set Data(CurveInvertX) [colormap configure $Map -invertx $Band]
      set Data(CurveInvertY) [colormap configure $Map -inverty $Band]

      if { $MinMax } {
         set Data(CurveMinD) [colormap configure $Map -min $Band]
         set Data(CurveMaxD) [colormap configure $Map -max $Band]
      } else {
         colormap configure $Map -min $Band $Data(CurveMinD)
         colormap configure $Map -max $Band $Data(CurveMaxD)
      }

      if { $Data(CurveMax)!=$Data(CurveMin) } {
         set d [expr 256.0/($Data(CurveMax)-$Data(CurveMin))]
         set Data(CurveMinX) [expr ($Data(CurveMinD)-$Data(CurveMin))*$d]
         $Canvas coords min $Data(CurveMinX) 1 $Data(CurveMinX) 128 $Data(CurveMinX) 64 [expr $Data(CurveMinX)+10] 64
         set Data(CurveMaxX) [expr ($Data(CurveMaxD)-$Data(CurveMin))*$d]
         $Canvas coords max $Data(CurveMaxX) 1 $Data(CurveMaxX) 128 $Data(CurveMaxX) 64 [expr $Data(CurveMaxX)-10] 64
      }
      set Data(Curve) [colormap configure $Map -curve $Band]
      if { ![glrender -shaderavailable] } { gdalband clean $Data(Object) }

      if  { !$MinMax } {
         Mapper::Curve $Canvas $Map $Band
      }
   }
}

proc Mapper::CurveRange { Canvas Map Band Side X } {
   variable Data

   Mapper::CurveValue $X

   if { [llength $Data(Bands$Data(Object))]==1 || $Data(Lock) } {
      set band rgba
   } else {
      set band $Band
   }

   if { $X>=0 && $X<=256 } {
      if { $Side=="min" } {
         if { $Data(CurveValue) < [colormap configure $Map -max $Band] } {
            set Data(CurveMinX) $X
            set Data(CurveMinD) $Data(CurveValue)
            $Canvas coords min $X 1 $X 128 $X 64 [expr $X+10] 64
            colormap configure $Map -min $band $Data(CurveValue)
          }
      } else {
         if { $Data(CurveValue) > [colormap configure $Map -min $Band] } {
            set Data(CurveMaxX) $X
            set Data(CurveMaxD) $Data(CurveValue)
            $Canvas coords max $X 1 $X 128 $X 64 [expr $X-10] 64
            colormap configure $Map -max $band $Data(CurveValue)
         }
      }
      if { ![glrender -shaderavailable] } { gdalband clean $Data(Object)}
      Mapper::Curve $Canvas $Map $band
   }
}

proc Mapper::CurveTranslate { Canvas Map Band X } {
   variable Data

   Mapper::CurveValue $X

   set dx [expr $X-$Data(CurveX)]

   if { [llength $Data(Bands$Data(Object))]==1 || $Data(Lock) } {
      set band rgba
   } else {
      set band $Band
   }

   if { [expr $Data(CurveMinX)+$dx]>=0 && [expr $Data(CurveMaxX)+$dx]<=256 } {
      set Data(CurveMinX) [expr $Data(CurveMinX)+$dx]
      set Data(CurveMaxX) [expr $Data(CurveMaxX)+$dx]
      $Canvas coords min $Data(CurveMinX) 1 $Data(CurveMinX) 128 $Data(CurveMinX) 64 [expr $Data(CurveMinX)+10] 64
      colormap configure $Map -min $band [set Data(CurveMinD) [Mapper::CurveValue $Data(CurveMinX)]]
      $Canvas coords max $Data(CurveMaxX) 1 $Data(CurveMaxX) 128 $Data(CurveMaxX) 64 [expr $Data(CurveMaxX)-10] 64
      colormap configure $Map -max $band [set Data(CurveMaxD) [Mapper::CurveValue $Data(CurveMaxX)]]

      if { ![glrender -shaderavailable] } { gdalband clean $Data(Object) }
      Mapper::Curve $Canvas $Map $band
   }
   set Data(CurveX) $X
}

proc Mapper::CurveValue { X } {
   variable Data

   set Data(CurveNb) [lindex $Data(Histo) [expr int($X)]]
   return [set Data(CurveValue) [expr $Data(CurveMin)+($Data(CurveMax)-$Data(CurveMin))*$X/256.0]]
}

proc Mapper::CurvePoint { Canvas Map Band X Y } {
   variable Data

   Mapper::CurveValue $X

   set Data(Curve) ""
   colormap configure $Map -curve $Band ""

   set d  [expr 256.0/($Data(CurveMaxX)-$Data(CurveMinX))]
   set dx [expr int($d)]
   set X  [expr int(($X-$Data(CurveMinX))*$d)-1]
   set Y  [expr ((128-$Y)*2)-1]

   if { [llength $Data(Bands$Data(Object))]==1 || $Data(Lock) } {
      set band rgba
   } else {
      set band $Band
   }

   if { [expr $X-$dx]>=0 && [expr $X+$dx]<=255 && $Y>=0 && $Y<=255 } {
      for { set x [expr $X-$dx] } { $x<[expr $X+$dx] } { incr x } {
         colormap configure $Map -curvepoint $band $x $Y
       }
   }
   if { ![glrender -shaderavailable] } { gdalband clean $Data(Object) }
   Mapper::Curve $Canvas $Map $band
}

proc Mapper::CurveSet { Canvas Map } {
   variable Data

   colormap control $Map -update

   if { [llength $Data(Bands$Data(Object))]==1 || $Data(Lock) } {
      colormap configure $Map -curve rgba $Mapper::Data(Curve)
   } else {
      colormap configure $Map -curve $Mapper::Data(Band) $Mapper::Data(Curve)
   }

   if { ![glrender -shaderavailable] } { gdalband clean $Data(Object) }
   Mapper::Curve $Canvas $Map $Mapper::Data(Band)
}

proc Mapper::Curve { Canvas Map Bands } {
   variable Data

   if { $Bands=="rgba" } {
      set Bands { red green blue alpha }
   }

   foreach band $Bands {
      set ys  [colormap configure $Map -curvepoint $band]
      set ny  [llength $ys]
      set min [colormap configure $Map -min $band]
      set max [colormap configure $Map -max $band]

      #----- Calculate offset and scaling on range

      if { $Data(CurveMax)!=$Data(CurveMin) } {
         set d  [expr ($Data(CurveMax)-$Data(CurveMin))]
         set dx [expr ($min-$Data(CurveMin))*$ny/$d]
         set fx [expr ($max-$min)/$d]

         #----- Set curve coordinate

         set coords ""
         for { set i 0 } { $i < $ny } { incr i } {
            append coords "[expr $i*$fx+$dx] [expr 128.0-[lindex $ys $i]/2.0] "
         }
      } else {
          set coords { 0 0 0 0 }
      }
      eval $Canvas coords $band $coords
#      update

      if { $Data(RealTime) } {
        Page::Update $Page::Data(Frame)
      }
   }
   colormap image $Map GDALMAPImg
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::ParamsOGR>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Parametres des objets de type OGR
#
# Parametres :
#   <Object> : Donnee geographique a parametrer
#   <Tab>    : Onglet par defaut
#
# Retour    :
#
# Remarque :
#
#-------------------------------------------------------------------------------

proc Mapper::ParamsOGR { Object Tab } {
   global   GDefs
   variable Lbl
   variable Bubble
   variable Data

   if { [winfo exists .mapperparams] && $Data(Mode)!="ogrlayer" } {
      set pos [wm geometry .mapperparams]
      set force True
      Mapper::ParamsClose
      destroy .mapperparams
   } else {
      set pos 500x425
      set force False
   }
   set Data(Mode) ogrlayer

   if { ![winfo exists .mapperparams] } {
      toplevel .mapperparams

      wm title     .mapperparams "[lindex $Lbl(OGR) $GDefs(Lang)]"
      wm transient .mapperparams .
      wm geometry  .mapperparams =$pos
      wm protocol  .mapperparams WM_DELETE_WINDOW { destroy .mapperparams }

      TabFrame::Create .mapperparams.tab 1 ""
      pack .mapperparams.tab -side top -fill both -expand true -padx 2 -pady 2

      set Data(Frame2) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Display) $GDefs(Lang)] False ""]
         labelframe $Data(Frame2).pos -text [lindex $Lbl(Position) $GDefs(Lang)]
            frame  $Data(Frame2).pos.extr
               label $Data(Frame2).pos.extr.lbl -text  [lindex $Lbl(Extrude) $GDefs(Lang)] -width 14 -anchor nw
               label $Data(Frame2).pos.extr.x -text "x" -width 1 -anchor nw
               ComboBox::Create $Data(Frame2).pos.extr.sel Mapper::Data(Extr) noedit unsorted nodouble -1 "" 1 8 ""
               entry  $Data(Frame2).pos.extr.fac -bg $GDefs(ColorLight) -textvariable Mapper::Data(ExtrFactor) -bd 1 -width 5
               pack  $Data(Frame2).pos.extr.lbl $Data(Frame2).pos.extr.fac $Data(Frame2).pos.extr.x -side left -fill y
               pack  $Data(Frame2).pos.extr.sel -side left -fill both -expand true
            frame  $Data(Frame2).pos.topo
               label $Data(Frame2).pos.topo.lbl -text  [lindex $Lbl(Topo) $GDefs(Lang)] -width 14 -anchor nw
               label $Data(Frame2).pos.topo.x -text "x" -width 1 -anchor nw
               entry  $Data(Frame2).pos.topo.fac  -bg $GDefs(ColorLight) -textvariable Mapper::Data(TopoFactor) -bd 1 -width 5
               ComboBox::Create $Data(Frame2).pos.topo.sel Mapper::Data(Topo) noedit unsorted nodouble -1 "" 1 8 ""
               pack  $Data(Frame2).pos.topo.lbl $Data(Frame2).pos.topo.fac $Data(Frame2).pos.topo.x -side left -fill y
               pack  $Data(Frame2).pos.topo.sel -side left -fill x -expand true
            pack $Data(Frame2).pos.extr $Data(Frame2).pos.topo -side top -fill x -padx 5

         labelframe $Data(Frame2).shape -text [lindex $Lbl(Shape) $GDefs(Lang)]
            frame $Data(Frame2).shape.fill
               label $Data(Frame2).shape.fill.lbl -text [lindex $Lbl(Fill) $GDefs(Lang)] -width 12 -anchor w
               ColorBox::CreateSel  $Data(Frame2).shape.fill.val Mapper::Data(Fill) Mapper::ParamsOGRSet \$Mapper::Data(Object)
               checkbutton $Data(Frame2).shape.fill.sel -variable Mapper::Data(FillSel) -relief raised -bd 1 \
                  -bitmap @$GDefs(Dir)/Resources/Bitmap/zeroth.xbm -indicatoron false \
                  -command { Mapper::ParamsOGRSet $Mapper::Data(Object) } -selectcolor "" -relief groove -bd 1
               pack $Data(Frame2).shape.fill.lbl $Data(Frame2).shape.fill.val $Data(Frame2).shape.fill.sel -side left -anchor w -padx 5

            frame $Data(Frame2).shape.icon
               label $Data(Frame2).shape.icon.lbl -text [lindex $Lbl(Icon) $GDefs(Lang)] -width 12 -anchor w
               IcoMenu::CreateDef $Data(Frame2).shape.icon.sel $GDefs(Dir)/Resources/Bitmap \
                  "zeroth.xbm stri.xbm ssquare.xbm scircle.xbm slos.xbm shbar.xbm svbar.xbm spenta.xbm shexa.xbm slight.xbm sx.xbm s+.xbm" $Data(Icons) \
                  Mapper::Data(Icon) { Mapper::ParamsOGRSet $Mapper::Data(Object) } $Mapper::Data(Icon) -relief groove -bd 2
               scale $Data(Frame2).shape.icon.sz -bd 1 -relief flat -width 15 -sliderlength 10 -from 1 -to 25 -variable Mapper::Data(Size) -orient horizontal -showvalue False
               entry $Data(Frame2).shape.icon.val -bd 1 -textvariable Mapper::Data(Size) -bg $GDefs(ColorLight) -width 5
               pack $Data(Frame2).shape.icon.lbl $Data(Frame2).shape.icon.sel $Data(Frame2).shape.icon.val $Data(Frame2).shape.icon.sz -side left -anchor w -padx 5

            frame $Data(Frame2).shape.out
               label $Data(Frame2).shape.out.lbl -text [lindex $Lbl(Out) $GDefs(Lang)] -width 12 -anchor w
               ColorBox::CreateSel $Data(Frame2).shape.out.col Mapper::Data(Color) Mapper::ParamsOGRSet \$Mapper::Data(Object)
               IcoMenu::Create $Data(Frame2).shape.out.width $GDefs(Dir)/Resources/Bitmap \
                  "zeroth.xbm width1.xbm width2.xbm width3.xbm width4.xbm width5.xbm" "0 1 2 3 4 5" \
                  Mapper::Data(Width) { Mapper::ParamsOGRSet $Mapper::Data(Object) } $Mapper::Data(Width) -relief groove -bd 2
               IcoMenu::CreateDef $Data(Frame2).shape.out.dash $GDefs(Dir)/Resources/Bitmap \
                { dash0.xbm dash1.xbm dash2.xbm dash3.xbm dash4.xbm dash5.xbm } { "" . - .- .-- .-. } \
                Mapper::Data(Dash) { Mapper::ParamsOGRSet $Mapper::Data(Object) } $Mapper::Data(Dash) -relief groove -bd 2
               pack $Data(Frame2).shape.out.lbl $Data(Frame2).shape.out.col $Data(Frame2).shape.out.width $Data(Frame2).shape.out.dash -side left -anchor w -padx 5

            frame $Data(Frame2).shape.mask
               label $Data(Frame2).shape.mask.lbl -text [lindex $Lbl(Mask) $GDefs(Lang)] -width 12 -anchor w
               checkbutton $Data(Frame2).shape.mask.sel -variable Mapper::Data(Mask) -relief raised -bd 1 \
                  -bitmap @$GDefs(Dir)/Resources/Bitmap/zeroth.xbm -indicatoron false \
                  -command { Mapper::ParamsOGRSet $Mapper::Data(Object) } -selectcolor "" -relief groove -bd 1
               pack $Data(Frame2).shape.mask.lbl $Data(Frame2).shape.mask.sel -side left -anchor w -padx 5

            frame $Data(Frame2).shape.burn
               label $Data(Frame2).shape.burn.lbl -text [lindex $Lbl(Burn) $GDefs(Lang)] -width 12 -anchor w
               checkbutton $Data(Frame2).shape.burn.sel -variable Mapper::Data(Burn) -relief raised -bd 1 \
                  -bitmap @$GDefs(Dir)/Resources/Bitmap/zeroth.xbm -indicatoron false -onvalue -1 -offvalue 1 \
                  -command { Mapper::ParamsOGRSet $Mapper::Data(Object) } -selectcolor "" -relief groove -bd 1
               pack $Data(Frame2).shape.burn.lbl $Data(Frame2).shape.burn.sel -side left -anchor w -padx 5

            frame $Data(Frame2).shape.tran
               label $Data(Frame2).shape.tran.lbl -text [lindex $Lbl(Tran) $GDefs(Lang)] -width 12 -anchor w
               scale $Data(Frame2).shape.tran.sca -bd 1 -relief flat -width 15 -sliderlength 10 -from 0 -to 100 -variable Mapper::Data(Tran) -orient horizontal -showvalue False
               entry $Data(Frame2).shape.tran.val -bd 1 -textvariable Mapper::Data(Tran) -bg $GDefs(ColorLight) -width 5
               pack $Data(Frame2).shape.tran.lbl $Data(Frame2).shape.tran.val -side left  -padx 5
               pack $Data(Frame2).shape.tran.sca -side left -fill x -expand true -padx 5

             frame $Data(Frame2).shape.label
               label $Data(Frame2).shape.label.lbl -text [lindex $Lbl(Label) $GDefs(Lang)] -width 12 -anchor w
               ComboBox::Create $Data(Frame2).shape.label.val Mapper::Data(Label) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame2).shape.label.lbl -side left -anchor w -padx 5
               pack $Data(Frame2).shape.label.val -side left -fill x -padx 5 -expand true

            pack $Data(Frame2).shape.out $Data(Frame2).shape.fill $Data(Frame2).shape.icon $Data(Frame2).shape.mask $Data(Frame2).shape.burn\
                $Data(Frame2).shape.tran $Data(Frame2).shape.label -side top -anchor w -fill x

         labelframe $Data(Frame2).inter -text [lindex $Lbl(Interval) $GDefs(Lang)]
            frame $Data(Frame2).inter.order
               label $Data(Frame2).inter.order.lbl -text [lindex $Lbl(Value) $GDefs(Lang)] -width 13 -anchor w
               ComboBox::Create $Data(Frame2).inter.order.val Mapper::Data(Order) noedit unsorted nodouble -1 $FSTD::Param(Orders) 1 4 ""
               spinbox $Data(Frame2).inter.order.prec -textvariable Mapper::Data(Mantisse) -width 2 -from 0 -to 30 -wrap 1 -bd 1 \
                  -command "" -bg $GDefs(ColorLight)
               button $Data(Frame2).inter.order.font -relief groove -bd 2 -bitmap @$GDefs(Dir)/Resources/Bitmap/font.ico\
                  -command "FontBox::Create $Data(Frame2).inter.order.font \"Mapper::ParamsOGRSet \$Mapper::Data(Object)\" OGRFONT"
               pack $Data(Frame2).inter.order.lbl -side left -padx 5
               pack $Data(Frame2).inter.order.font $Data(Frame2).inter.order.prec -side left
               pack $Data(Frame2).inter.order.val -side left -fill x -expand True -padx 5

            frame $Data(Frame2).inter.map
               label $Data(Frame2).inter.map.lbl -text [lindex $Lbl(Map) $GDefs(Lang)] -width 12 -anchor w
               button $Data(Frame2).inter.map.val -bd 1 -relief flat -image OGRMAPImg -command "MapBox::Create $Data(Frame2).inter.map.val \
                   \"colormap image \$Mapper::Data(ColorMap) OGRMAPImg ; Mapper::ParamsOGRSet \$Mapper::Data(Object); Page::Update \$Page::Data(Frame)\" \$Mapper::Data(ColorMap)"
               pack $Data(Frame2).inter.map.lbl $Data(Frame2).inter.map.val -side left -padx 5

            frame $Data(Frame2).inter.fld
               label $Data(Frame2).inter.fld.lbl -text [lindex $Lbl(Field) $GDefs(Lang)] -width 12 -anchor w
               ComboBox::Create $Data(Frame2).inter.fld.val Mapper::Data(VarMap) noedit sorted nodouble -1 "" 1 8 ""
               pack $Data(Frame2).inter.fld.lbl -side left -padx 5
               pack $Data(Frame2).inter.fld.val -side left -fill x -padx 5 -expand true

            frame $Data(Frame2).inter.desc
              label $Data(Frame2).inter.desc.lbl -text [lindex $Lbl(Interval) $GDefs(Lang)] -width 12 -anchor w
              ComboBox::Create $Data(Frame2).inter.desc.val Mapper::Data(Intervals) edit sorted nodouble -1 \
                  "" 1 6 "Mapper::ParamsOGRSet \$Mapper::Data(Object)"
              pack $Data(Frame2).inter.desc.lbl -side left -padx 5
              pack $Data(Frame2).inter.desc.val -side left -padx 5 -fill x -expand true

            pack $Data(Frame2).inter.order $Data(Frame2).inter.map $Data(Frame2).inter.fld $Data(Frame2).inter.desc \
               -side top -fill x -expand true
         pack $Data(Frame2).shape $Data(Frame2).pos $Data(Frame2).inter -side top -padx 5 -pady 5 -ipady 2 -fill both

      set Data(Frame1) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Ref) $GDefs(Lang)] False ""]
         frame $Data(Frame1).projhead
            label $Data(Frame1).projhead.lbl -text [lindex $Lbl(Projection) $GDefs(Lang)] -width 10 -anchor nw
            button $Data(Frame1).projhead.file -image OPEN -relief flat -bd 0 -overrelief raised \
               -command "Mapper::ProjFile $Data(Frame1).proj.val \[FileBox::Create . \"\" Load \[list \$FileBox::Type(PROJ) \$FileBox::Type(TXT)\]\]"
            button $Data(Frame1).projhead.tab -image INTEROGATE -relief flat -bd 0 -overrelief raised \
               -command { set Mapper::Data(Proj) [Mapper::WKT::Param $Mapper::Data(Proj)]; $Mapper::Data(Frame1).proj.val delete 0.0 end ; $Mapper::Data(Frame1).proj.val insert end $Mapper::Data(Proj); Mapper::ParamsOGRSet $Mapper::Data(Object) }
            pack $Data(Frame1).projhead.lbl -side left
            pack $Data(Frame1).projhead.tab $Data(Frame1).projhead.file  -side left -padx 2
         labelframe $Data(Frame1).proj -labelwidget $Data(Frame1).projhead
            text $Data(Frame1).proj.val -bd 1 -bg $GDefs(ColorLight) -height 5 -width 25
            pack $Data(Frame1).proj.val -side right -fill both -expand true -padx 5 -pady 5
         pack  $Data(Frame1).proj -side top -fill both -expand true -padx 5 -pady 5

         Bubble::Create $Data(Frame1).projhead.file $Mapper::Bubble(ProjLoad)
         Bubble::Create $Data(Frame1).projhead.tab  $Mapper::Bubble(ProjRef)

      set Data(Frame3) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Feature) $GDefs(Lang)] False ""]

         frame $Data(Frame3).sel
            label $Data(Frame3).sel.lbl -text [lindex $Lbl(Index) $GDefs(Lang)] -width 7 -anchor w
            spinbox $Data(Frame3).sel.val -textvariable Mapper::Data(Index) -width 20 -from 0 -to 0 -wrap True -bd 1 \
               -command { Mapper::FeatureOGR $Mapper::Data(Object) %s True } -bg $GDefs(ColorLight)
            pack $Data(Frame3).sel.lbl -side left
            pack $Data(Frame3).sel.val -side left -fill x -expand true
         frame $Data(Frame3).meta
            text $Data(Frame3).meta.text -relief sunken -bd 1 -wrap none -bg $GDefs(ColorLight) -yscrollcommand "$Data(Frame3).meta.scroll set" -width 0 -height 0
            scrollbar $Data(Frame3).meta.scroll -relief sunken -command "$Data(Frame3).meta.text yview" -bd 1 -width 10
            pack $Data(Frame3).meta.text -side left -fill both -expand true
            pack $Data(Frame3).meta.scroll -side left -fill y
         pack $Data(Frame3).sel -side top -fill x -padx 5 -pady 5
         pack $Data(Frame3).meta -side top -fill both -expand true -padx 5 -pady 5

      set Data(Frame4) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Select) $GDefs(Lang)] False ""]
         frame $Data(Frame4).meta
            text $Data(Frame4).meta.text -relief sunken -bd 1 -wrap none -bg $GDefs(ColorLight) -yscrollcommand "$Data(Frame4).meta.scroll set" -width 0 -height 0
            scrollbar $Data(Frame4).meta.scroll -relief sunken -command "$Data(Frame4).meta.text yview" -bd 1 -width 10
            pack $Data(Frame4).meta.text -side left -fill both -expand true
            pack $Data(Frame4).meta.scroll -side left -fill y
         pack $Data(Frame4).meta -side top -fill both -expand true -padx 5 -pady 5
         button $Data(Frame4).reset -text [lindex $Lbl(Clear) $GDefs(Lang)] -relief groove -bd 2 -command { Mapper::SelectOGRClear $Mapper::Data(Object) }
         pack $Data(Frame4).reset -side top -fill x -padx 5 -pady 5

      set Data(Frame5) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Table) $GDefs(Lang)] False ""]
         frame $Data(Frame5).meta -relief sunken -bd 1
            scrollbar $Data(Frame5).meta.scrolly -relief sunken -command "$Data(Frame5).meta.table yview" -bd 1 -width 10
            scrollbar $Data(Frame5).meta.scrollx -relief sunken -command "$Data(Frame5).meta.table xview" -bd 1 -width 10 -orient horizontal
            table $Data(Frame5).meta.table -relief sunken -bd 1 -bg $GDefs(ColorLight) -titlecols 1 -titlerows 1 -variable Mapper::Table \
            -resizeborders col -anchor e -highlightbackground $GDefs(ColorHighLight)  -rows 1 -cols 1 -multiline False -drawmode fast \
            -yscrollcommand "$Data(Frame5).meta.scrolly set" -xscrollcommand "$Data(Frame5).meta.scrollx set" -width 1 -height 1 -selectmode extended
            pack $Data(Frame5).meta.scrollx -side bottom -fill x
            pack $Data(Frame5).meta.scrolly -side left -fill y
            pack $Data(Frame5).meta.table -side left -fill both -expand true -before $Data(Frame5).meta.scrolly
         pack $Data(Frame5).meta -side top -fill both -expand true -padx 5 -pady 5

      frame .mapperparams.com
         button .mapperparams.com.apply  -text [lindex $Lbl(Apply) $GDefs(Lang)] -bd 1 -command { Mapper::ParamsOGRSet $Mapper::Data(Object) }
         button .mapperparams.com.cancel -text [lindex $Lbl(Close) $GDefs(Lang)] -bd 1 -command { destroy .mapperparams }
         pack .mapperparams.com.cancel .mapperparams.com.apply -side right
      pack .mapperparams.com -side top -fill x -padx 2 -pady 2

      TabFrame::Select .mapperparams.tab $Tab
   }

   $Data(Frame1).proj.val delete 0.0 end
   $Data(Frame1).proj.val insert end $Data(Proj)

   ColorBox::ConfigNoColor $Data(Frame2).shape.out.col $Data(Color)
   ColorBox::ConfigNoColor $Data(Frame2).shape.fill.val $Data(Fill)

   $Data(Frame3).sel.val configure -to [expr [ogrlayer define $Mapper::Data(Object) -nb]-1]

   ComboBox::DelAll $Data(Frame2).shape.label.val False
   ComboBox::Add $Data(Frame2).shape.label.val {}
   ComboBox::AddList $Data(Frame2).shape.label.val $Mapper::Data(Fields)

   ComboBox::DelAll $Data(Frame2).inter.fld.val False
   ComboBox::Add $Data(Frame2).inter.fld.val {}
   ComboBox::AddList $Data(Frame2).inter.fld.val $Mapper::Data(Fields)

   ComboBox::DelAll  $Data(Frame2).pos.extr.sel False
   ComboBox::Add  $Data(Frame2).pos.extr.sel {}
   ComboBox::AddList  $Data(Frame2).pos.extr.sel $Mapper::Data(Fields)

   ComboBox::DelAll  $Data(Frame2).pos.topo.sel False
   ComboBox::AddList  $Data(Frame2).pos.topo.sel [concat $Mapper::Data(Topos) $Data(Fields)]

   IcoMenu::Set $Data(Frame2).shape.out.width $Data(Width)
   catch { IcoMenu::Set $Data(Frame2).shape.out.dash  $Data(Dash) }

   catch { colormap image $Mapper::Data(Object) OGRMAPImg }

   Mapper::SelectOGR $Object
   Mapper::FeatureOGR $Object $Data(Index) False
   Mapper::TableOGR $Object $force

   bind $Data(Frame3).sel.val <Return> { Mapper::FeatureOGR $Mapper::Data(Object) $Mapper::Data(Index) True }
}

proc Mapper::SelectOGR { Object } {
   global GDefs
   variable Lbl
   variable Bubble
   variable Data

   set w 0
   foreach field [ogrlayer define $Object -field] {
      if { [string length $field]>$w } {
         set w [string length $field]
      }
   }
   incr w

   set n 0
   $Data(Frame4).meta.text delete 0.0 end

   foreach field [ogrlayer define $Object -field] {
      destroy $Data(Frame4).fr$n $Data(Frame4).op$n.menu

      frame $Data(Frame4).fr$n -relief sunken -bd 1
         label $Data(Frame4).fr$n.lbl -text $field -bd 1 -anchor w -justify left -relief raised -bd 1 -width $w
         entry $Data(Frame4).fr$n.sel -textvariable Mapper::Select(Val$Object$field) -bg $GDefs(ColorLight) -width 30 -relief flat
         menubutton $Data(Frame4).fr$n.op -textvariable Mapper::Select(Op$Object$field) -menu $Data(Frame4).fr$n.op.menu \
            -relief raised -width 2 -bd 1
         pack $Data(Frame4).fr$n.lbl $Data(Frame4).fr$n.op $Data(Frame4).fr$n.sel -side left -fill y

         Bubble::Create $Data(Frame4).fr$n.op $Bubble(Op)

      menu $Data(Frame4).fr$n.op.menu
         $Data(Frame4).fr$n.op.menu add command -label ""   -command "set Mapper::Select(Op$Object$field) \"\""
         $Data(Frame4).fr$n.op.menu add command -label "~=" -command "set Mapper::Select(Op$Object$field) ~="
         $Data(Frame4).fr$n.op.menu add command -label "==" -command "set Mapper::Select(Op$Object$field) =="
         $Data(Frame4).fr$n.op.menu add command -label "!=" -command "set Mapper::Select(Op$Object$field) !="
         $Data(Frame4).fr$n.op.menu add command -label "<"  -command "set Mapper::Select(Op$Object$field) <"
         $Data(Frame4).fr$n.op.menu add command -label "<=" -command "set Mapper::Select(Op$Object$field) <="
         $Data(Frame4).fr$n.op.menu add command -label ">"  -command "set Mapper::Select(Op$Object$field) >"
         $Data(Frame4).fr$n.op.menu add command -label ">=" -command "set Mapper::Select(Op$Object$field) >="
         $Data(Frame4).fr$n.op.menu add command -label "<>" -command "set Mapper::Select(Op$Object$field) <>"
         $Data(Frame4).fr$n.op.menu add command -label "\[\]" -command "set Mapper::Select(Op$Object$field) \\\[\\\]"

      $Data(Frame4).meta.text window create end -window $Data(Frame4).fr$n
      $Data(Frame4).meta.text insert end "\n"
      incr n
   }
}

proc Mapper::SelectOGRApply { Object } {
   variable Select

   set select {}
   foreach field [ogrlayer define $Object -field] {
      if { $Select(Op$Object$field)!="" && $Select(Val$Object$field)!="" } {
         lappend select [list $field $Select(Op$Object$field) $Select(Val$Object$field)]
      }
   }

   if { ![info exists Select(String$Object)] || $Select(String$Object)!=$select } {
      set Select(String$Object) $select
      ogrlayer define $Object -featureselect $select
      ogrlayer define $Object -featurehighlight {}
      Mapper::TableOGR $Object True
   }
}

proc Mapper::SelectOGRClear { Object } {
   variable Select

   foreach field [ogrlayer define $Object -field] {
      set Select(Op$Object$field) ""
      set Select(Val$Object$field) ""
   }
   ogrlayer define $Object -featurehighlight ""

   Mapper::TableOGR $Object True
   Page::Update $Page::Data(Frame)
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::FeatureOGR>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Recuperer les donnees d'une feature specifique
#
# Parametres :
#   <Object> : Donnee geographique a parametrer
#   <Index>  : Index de l'item
#   <Locate> : Centre sur l'item
#
# Retour    :
#
# Remarque :
#
#-------------------------------------------------------------------------------

proc Mapper::FeatureOGR { Object Index Locate } {
   global   GDefs
   variable Data

   if { $Index=="" } {
      return
   }

   set w 0
   foreach field [ogrlayer define $Object -field] {
      if { [string length $field]>$w } {
         set w [string length $field]
      }
   }

   if { [winfo exists $Data(Frame4).meta.table] && $Data(TableSel)!=$Index } {
      $Data(Frame5).meta.table yview $Index
   }
   $Data(Frame3).meta.text delete 0.0 end

   if { ![catch { set infos [ogrlayer define $Object -feature $Index] }] } {

      set i 0
      foreach info $infos {
         eval $Data(Frame3).meta.text insert end \[format \"%-${w}s : %-50s\n\" \[lindex \$info 0\] \[lindex \$info 1\]\]
         incr i
      }

      ogrlayer define $Object -featurehighlight $Index
      set coords [ogrlayer define $Object -centroid $Index]

      if { [llength $coords] } {
         set coords [ogrlayer project $Object [lindex $coords 0] [lindex $coords 1]]
      }

      if { $Locate && [llength $coords] } {
         SPI::Locate [lindex $coords 0] [lindex $coords 1]
      } else {
         Page::Update $Page::Data(Frame)
      }
   }
   set Data(Index)    $Index
   set Data(TableSel) $Index
}

proc Mapper::TableOGR { Object { Force False } } {
   global GDefs
   variable Data
   variable Table

   if { $Object==$Data(TableObject) && !$Force } {
      return
   }

   if { [catch { ogrlayer define $Object -feature 0 $Data(TableSort) }] } {
      set Data(TableSort) ""
   }

   set rs [lindex [$Data(Frame5).meta.table configure -rows] 4]
   set cs [lindex [$Data(Frame5).meta.table configure -cols] 4]
   $Data(Frame5).meta.table configure -cursor watch -rows 1 -cols 1
   update idletasks

   set Data(TableObject) $Object
   eval ogrlayer stats \$Object -table Mapper::Table $Data(TableSort) 0 $Data(TableMax)
#   eval ogrlayer stats $Object -table Mapper::Table $Data(TableSort)

   if { ![winfo exists $Data(Frame5).meta.table] } {
      return
   }

   set col [expr [llength [ogrlayer define $Object -field]]+1]
   set row [expr [array size Mapper::Table]/$col]
   set row [expr $row>$Data(TableMax)?$Data(TableMax):$row]

   $Data(Frame5).meta.table configure -rows $row -cols $col

   for { set c 1 } { $c<$col } { incr c } {
      if {![winfo exists $Data(Frame5).meta.table.c$c] } {
         radiobutton $Data(Frame5).meta.table.c$c -variable Mapper::Data(TableSort) -indicatoron False -bd 1
      }
      $Data(Frame5).meta.table.c$c configure -text $Mapper::Table(0,$c) -value $Mapper::Table(0,$c) -command "Mapper::TableOGR $Object True"
      $Data(Frame5).meta.table window configure 0,$c -window $Data(Frame5).meta.table.c$c -sticky nsew
   }
   for { set c $c } { $c<$cs } { incr c } {
      destroy radiobutton $Data(Frame5).meta.table.c$c
   }

   for { set r 1 } { $r<$row } { incr r } {
      if {![winfo exists $Data(Frame5).meta.table.r$r] } {
         radiobutton $Data(Frame5).meta.table.r$r -variable Mapper::Data(TableSel) -indicatoron False -bd 1
      }
      catch {  $Data(Frame5).meta.table.r$r configure -text $Mapper::Table($r,0) -value $Mapper::Table($r,0) \
         -command "$Data(Frame5).meta.table selection clear all; $Data(Frame5).meta.table selection set $r,1 $r,$col;
            Mapper::FeatureOGR \$Mapper::Data(Object) $Mapper::Table($r,0) True" }
      $Data(Frame5).meta.table window configure $r,0 -window $Data(Frame5).meta.table.r$r -sticky nsew
   }
   for { set r $r } { $r<$rs } { incr r } {
      destroy radiobutton $Data(Frame5).meta.table.r$r
   }
   $Data(Frame5).meta.table configure -cursor left_ptr
}

#-------------------------------------------------------------------------------
# Nom      : <Mapper::ParamsModel>
# Creation : Juillet 2004 - J.P. Gauthier - CMC/CMOE
#
# But      : Parametres des objets de type Model
#
# Parametres :
#   <Object> : Donnee geographique a parametrer
#   <Tab>    : Onglet par defaut
#
# Retour    :
#
# Remarque :
#
#-------------------------------------------------------------------------------

proc Mapper::ParamsModel { Object Tab } {
   global   GDefs
   variable Lbl
   variable Bubble
   variable Data

   if { [winfo exists .mapperparams] && $Data(Mode)!="model" } {
      set pos [wm geometry .mapperparams]
      Mapper::ParamsClose
      destroy .mapperparams
   } else {
      set pos 500x425
   }
   set Data(Mode) model

   if { ![winfo exists .mapperparams] } {
      toplevel .mapperparams

      wm title     .mapperparams "[lindex $Lbl(Model) $GDefs(Lang)]"
      wm transient .mapperparams .
      wm geometry  .mapperparams =$pos
      wm protocol  .mapperparams WM_DELETE_WINDOW { destroy .mapperparams }

      TabFrame::Create .mapperparams.tab 1 ""
      pack .mapperparams.tab -side top -fill both -expand true -padx 2 -pady 2

      frame .mapperparams.com
         button .mapperparams.com.apply  -text [lindex $Lbl(Apply) $GDefs(Lang)] -bd 1 -command { Mapper::ParamsModelSet $Mapper::Data(Object) }
         button .mapperparams.com.cancel -text [lindex $Lbl(Close) $GDefs(Lang)] -bd 1 -command { destroy .mapperparams }
         pack .mapperparams.com.cancel .mapperparams.com.apply -side right
      pack .mapperparams.com -side top -fill x -padx 2 -pady 2

      set Data(Frame2) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Display) $GDefs(Lang)] False ""]
         labelframe $Data(Frame2).mat -text [lindex $Lbl(Material) $GDefs(Lang)]
            frame $Data(Frame2).mat.ambi
               label $Data(Frame2).mat.ambi.lbl -text [lindex $Lbl(Ambi) $GDefs(Lang)] -width 12 -anchor w
               ColorBox::CreateSel $Data(Frame2).mat.ambi.col Mapper::Data(Ambi) Mapper::ParamsModelSet \$Mapper::Data(Object)
               pack $Data(Frame2).mat.ambi.lbl $Data(Frame2).mat.ambi.col -side left
            frame $Data(Frame2).mat.emis
               label $Data(Frame2).mat.emis.lbl -text [lindex $Lbl(Emis) $GDefs(Lang)] -width 12 -anchor w
               ColorBox::CreateSel $Data(Frame2).mat.emis.col Mapper::Data(Emis) Mapper::ParamsModelSet \$Mapper::Data(Object)
               pack $Data(Frame2).mat.emis.lbl $Data(Frame2).mat.emis.col -side left
            frame $Data(Frame2).mat.diff
               label $Data(Frame2).mat.diff.lbl -text [lindex $Lbl(Diff) $GDefs(Lang)] -width 12 -anchor w
               ColorBox::CreateSel $Data(Frame2).mat.diff.col Mapper::Data(Diff) Mapper::ParamsModelSet \$Mapper::Data(Object)
               pack $Data(Frame2).mat.diff.lbl $Data(Frame2).mat.diff.col -side left
            frame $Data(Frame2).mat.spec
               label $Data(Frame2).mat.spec.lbl -text [lindex $Lbl(Spec) $GDefs(Lang)] -width 12 -anchor w
               ColorBox::CreateSel $Data(Frame2).mat.spec.col Mapper::Data(Spec) Mapper::ParamsModelSet \$Mapper::Data(Object)
               pack $Data(Frame2).mat.spec.lbl $Data(Frame2).mat.spec.col -side left
            frame $Data(Frame2).mat.shin
               label $Data(Frame2).mat.shin.lbl -text [lindex $Lbl(Shin) $GDefs(Lang)] -width 12 -anchor w
               entry $Data(Frame2).mat.shin.val -bd 1 -textvariable Mapper::Data(Shin) -bg $GDefs(ColorLight) -width 5
               scale $Data(Frame2).mat.shin.sc -bd 1 -relief flat -width 15 -sliderlength 10 -resolution 0.1 -from 0 -to 1 -variable Mapper::Data(Shin) -orient horizontal \
                  -showvalue False -command { if { $Mapper::Data(RealTime) } { Mapper::ParamsModelSet $Mapper::Data(Object) }; catch }
               pack $Data(Frame2).mat.shin.lbl $Data(Frame2).mat.shin.val -side left
               pack $Data(Frame2).mat.shin.sc -side left -fill x -expand True
            frame $Data(Frame2).mat.tran
               label $Data(Frame2).mat.tran.lbl -text [lindex $Lbl(Tran) $GDefs(Lang)] -width 12 -anchor w
               entry $Data(Frame2).mat.tran.val -bd 1 -textvariable Mapper::Data(Tram) -bg $GDefs(ColorLight) -width 5
               scale $Data(Frame2).mat.tran.sc -bd 1 -relief flat -width 15 -sliderlength 10 -resolution 0.1 -from 0 -to 1 -variable Mapper::Data(Tram) -orient horizontal \
                  -showvalue False -command { if { $Mapper::Data(RealTime) } { Mapper::ParamsModelSet $Mapper::Data(Object) }; catch }
               pack $Data(Frame2).mat.tran.lbl $Data(Frame2).mat.tran.val -side left
               pack $Data(Frame2).mat.tran.sc -side left -fill x -expand True
            pack $Data(Frame2).mat.ambi $Data(Frame2).mat.emis $Data(Frame2).mat.diff $Data(Frame2).mat.spec $Data(Frame2).mat.shin $Data(Frame2).mat.tran \
               -side top -padx 5 -fill x -anchor w

         labelframe $Data(Frame2).mode -text [lindex $Lbl(Mode) $GDefs(Lang)]
            frame $Data(Frame2).mode.out
               label $Data(Frame2).mode.out.lbl -text [lindex $Lbl(Out) $GDefs(Lang)] -width 12 -anchor w
               ColorBox::CreateSel $Data(Frame2).mode.out.col Mapper::Data(Color) Mapper::ParamsModelSet \$Mapper::Data(Object)
               IcoMenu::Create $Data(Frame2).mode.out.width $GDefs(Dir)/Resources/Bitmap \
                  "zeroth.xbm width1.xbm width2.xbm width3.xbm width4.xbm width5.xbm" "0 1 2 3 4 5" \
                  Mapper::Data(Width) { Mapper::ParamsModelSet $Mapper::Data(Object) } $Mapper::Data(Width) -relief groove -bd 2
               IcoMenu::CreateDef $Data(Frame2).mode.out.dash $GDefs(Dir)/Resources/Bitmap \
                { dash0.xbm dash1.xbm dash2.xbm dash3.xbm dash4.xbm dash5.xbm } { "" . - .- .-- .-. } \
                Mapper::Data(Dash) { Mapper::ParamsModelSet $Mapper::Data(Object) } $Mapper::Data(Dash) -relief groove -bd 2
               pack $Data(Frame2).mode.out.lbl $Data(Frame2).mode.out.col $Data(Frame2).mode.out.width $Data(Frame2).mode.out.dash -side left -anchor w
            frame $Data(Frame2).mode.text
               label $Data(Frame2).mode.text.lbl -text [lindex $Lbl(Texture) $GDefs(Lang)] -width 12 -anchor w
               checkbutton $Data(Frame2).mode.text.val -variable Mapper::Data(Texture) -relief raised -bd 1 -onvalue 1 -offvalue 0  -selectcolor "" \
                  -bitmap @$GDefs(Dir)/Resources/Bitmap/zeroth.xbm -indicatoron false -command { Mapper::ParamsModelSet $Mapper::Data(Object) }
               pack $Data(Frame2).mode.text.lbl $Data(Frame2).mode.text.val -side left
            frame $Data(Frame2).mode.light
               label $Data(Frame2).mode.light.lbl -text [lindex $Lbl(Light) $GDefs(Lang)] -width 12 -anchor w
               checkbutton $Data(Frame2).mode.light.val -variable Mapper::Data(Light) -relief raised -bd 1 -onvalue 1 -offvalue 0  -selectcolor "" \
                  -bitmap @$GDefs(Dir)/Resources/Bitmap/zeroth.xbm -indicatoron false -command { Mapper::ParamsModelSet $Mapper::Data(Object) }
               pack $Data(Frame2).mode.light.lbl $Data(Frame2).mode.light.val -side left
            pack $Data(Frame2).mode.out $Data(Frame2).mode.text $Data(Frame2).mode.light -side top -anchor w -padx 5
         pack $Data(Frame2).mat $Data(Frame2).mode -side top -fill x -padx 5 -pady 5 -ipady 2

      set Data(Frame1) [TabFrame::Add .mapperparams.tab 1 [lindex $Lbl(Ref) $GDefs(Lang)] False ""]
         frame $Data(Frame1).projhead
            label $Data(Frame1).projhead.lbl -text [lindex $Lbl(Projection) $GDefs(Lang)] -width 10 -anchor nw
            button $Data(Frame1).projhead.file -image OPEN -relief flat -bd 0 -overrelief raised \
               -command "Mapper::ProjFile $Data(Frame1).proj.val \[FileBox::Create . \"\" Load \[list \$FileBox::Type(PROJ) \$FileBox::Type(TXT)\]\]"
            button $Data(Frame1).projhead.tab -image INTEROGATE -relief flat -bd 0 -overrelief raised \
               -command { set Mapper::Data(Proj) [Mapper::WKT::Param $Mapper::Data(Proj)]; $Mapper::Data(Frame1).proj.val delete 0.0 end ; $Mapper::Data(Frame1).proj.val insert end $Mapper::Data(Proj); Mapper::ParamsModelSet $Mapper::Data(Object) }
            pack $Data(Frame1).projhead.lbl -side left
            pack $Data(Frame1).projhead.tab $Data(Frame1).projhead.file -side left -padx 2
         labelframe $Data(Frame1).proj -labelwidget $Data(Frame1).projhead
            text $Data(Frame1).proj.val -bd 1 -bg $GDefs(ColorLight) -height 5 -width 25
            pack $Data(Frame1).proj.val -side right -fill both -expand true -padx 5 -pady 5
         pack  $Data(Frame1).proj -side top -fill both -expand true -padx 5 -pady 5

         Bubble::Create $Data(Frame1).projhead.file $Mapper::Bubble(ProjLoad)
         Bubble::Create $Data(Frame1).projhead.tab  $Mapper::Bubble(ProjRef)

         labelframe $Data(Frame1).matrix -text [lindex $Lbl(Matrix) $GDefs(Lang)]
            frame $Data(Frame1).matrix.loc
               label $Data(Frame1).matrix.loc.lbl -text [lindex $Lbl(Translate) $GDefs(Lang)] -width 14 -anchor w
               entry $Data(Frame1).matrix.loc.lat -bd 1 -textvariable Mapper::Data(Lat) -bg $GDefs(ColorLight) -width 8
               entry $Data(Frame1).matrix.loc.lon -bd 1 -textvariable Mapper::Data(Lon) -bg $GDefs(ColorLight) -width 8
               entry $Data(Frame1).matrix.loc.ele -bd 1 -textvariable Mapper::Data(Ele) -bg $GDefs(ColorLight) -width 8
               pack $Data(Frame1).matrix.loc.lbl -side left
               pack $Data(Frame1).matrix.loc.lat $Data(Frame1).matrix.loc.lon $Data(Frame1).matrix.loc.ele -side left -fill x -expand True
            frame $Data(Frame1).matrix.rot
               label $Data(Frame1).matrix.rot.lbl -text [lindex $Lbl(Rotation) $GDefs(Lang)] -width 14 -anchor w
               entry $Data(Frame1).matrix.rot.x -bd 1 -textvariable Mapper::Data(RX) -bg $GDefs(ColorLight) -width 8
               entry $Data(Frame1).matrix.rot.y -bd 1 -textvariable Mapper::Data(RY) -bg $GDefs(ColorLight) -width 8
               entry $Data(Frame1).matrix.rot.z -bd 1 -textvariable Mapper::Data(RZ) -bg $GDefs(ColorLight) -width 8
               pack $Data(Frame1).matrix.rot.lbl -side left
               pack $Data(Frame1).matrix.rot.x $Data(Frame1).matrix.rot.y $Data(Frame1).matrix.rot.z -side left -fill x -expand True
            frame $Data(Frame1).matrix.sca
               label $Data(Frame1).matrix.sca.lbl -text [lindex $Lbl(Scale) $GDefs(Lang)] -width 14 -anchor w
               entry $Data(Frame1).matrix.sca.x -bd 1 -textvariable Mapper::Data(SX) -bg $GDefs(ColorLight) -width 8
               entry $Data(Frame1).matrix.sca.y -bd 1 -textvariable Mapper::Data(SY) -bg $GDefs(ColorLight) -width 8
               entry $Data(Frame1).matrix.sca.z -bd 1 -textvariable Mapper::Data(SZ) -bg $GDefs(ColorLight) -width 8
               pack $Data(Frame1).matrix.sca.lbl -side left
               pack $Data(Frame1).matrix.sca.x $Data(Frame1).matrix.sca.y $Data(Frame1).matrix.sca.z -side left -fill x -expand True
            pack $Data(Frame1).matrix.loc $Data(Frame1).matrix.rot $Data(Frame1).matrix.sca -side top -fill x -padx 5
         pack $Data(Frame1).matrix -side top -fill x -padx 5 -pady 5 -ipady 2 -anchor nw

      TabFrame::Select .mapperparams.tab $Tab
   }

   $Data(Frame1).proj.val delete 0.0 end
   $Data(Frame1).proj.val insert end $Data(Proj)

   $Data(Frame2).mat.ambi.col configure -fg $Data(Ambi)
   $Data(Frame2).mat.emis.col configure -fg $Data(Emis)
   $Data(Frame2).mat.diff.col configure -fg $Data(Diff)
   $Data(Frame2).mat.spec.col configure -fg $Data(Spec)
}
