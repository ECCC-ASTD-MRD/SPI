#============================================================================
# Environnement Canada
# Centre Meteorologique Canadien
# 2100 Trans-Canadienne
# Dorval, Quebec
#
# Projet     : Interface pour la gestion des experiences.
# Fichier    : <CANERM.int>
# Creation   : Octobre 1999 - J.P. Gauthier - CMC/CMOE
#
# Description: Description des interfaces et procedures relatives a
#              celles-ci pour le module CANERM.
#
# Remarques  :
#
#============================================================================

#-------------------------------------------------------------------------------
# Nom      : <CANERM::Params>
# Creation : Octobre 1999 - J.P. Gauthier - CMC/CMOE -
#
# But      : Creation d'une nouvelle simulation CANERM.
#
# Parametres :
#   <Tab>    : Boite d'onglet dans laquelle ajoute les onglets
#
# Retour :
#
# Remarques :
#
#-------------------------------------------------------------------------------

proc CANERM::ParamsNew { Tab } {
   global   GDefs
   variable Sim
   variable Msg
   variable Lbl
   variable Bubble

   wm geom [winfo toplevel $Tab] =320x310

   set t [TabFrame::Add $Tab 1 [lindex $Lbl(Params) $GDefs(Lang)] False]

   #----- Simulation Starting Time.
   labelframe $t.d -text "[lindex $Lbl(Start) $GDefs(Lang)]"

      #----- Simulation Date Hour and minutes.
      Calendar::Create $t.d.date "" CANERM::Sim(AccSeconds) -1
      Clock::Create $t.d.time [lindex $Lbl(At) $GDefs(Lang)] CANERM::Sim(AccHour)
      pack $t.d.date -side left -padx 2 -fill x -expand true
      pack $t.d.time -side left -padx 2
   pack $t.d -side top -side top -padx 5 -pady 5 -ipady 2 -fill x

   Bubble::Create $t.d.date [lindex $Bubble(Date) $GDefs(Lang)]
   Bubble::Create $t.d.time [lindex $Bubble(Time) $GDefs(Lang)]

   #----- Model Integration (data output time step and model time step).
   labelframe $t.integ -text "[lindex $Lbl(Integration) $GDefs(Lang)]"
      Option::Create $t.integ.dur [lindex $Lbl(SimDuration) $GDefs(Lang)] CANERM::Sim(Duration) 1 -1 $CANERM::Sim(ListSimDuration) ""
      Option::Create $t.integ.ots [lindex $Lbl(OutputTimeStepHour) $GDefs(Lang)] CANERM::Sim(ISauve) 0 -1 $CANERM::Sim(ListISauve) ""
      pack $t.integ.dur $t.integ.ots -side top -fill x -padx 2
   pack $t.integ -side top -padx 5 -ipady 2 -fill x

   Bubble::Create $t.integ.dur "[lindex $Bubble(SimDuration) $GDefs(Lang)]"
   Bubble::Create $t.integ.ots "[lindex $Bubble(OutputTimeStepHour) $GDefs(Lang)]"
   Bubble::Create $t.integ "[lindex $Bubble(SimIntegration) $GDefs(Lang)]"

   #----- Selection des parametres
   labelframe $t.fr -text "[lindex $Lbl(OtherParameters) $GDefs(Lang)]"
      Option::Create $t.fr.event [lindex $Lbl(Event) $GDefs(Lang)] CANERM::Sim(Event) 1 -1 \
          $CANERM::Sim(ListEvent) ""

      #----- Grid Resolution.
      frame $t.fr.grid
         Option::Create $t.fr.grid.res [lindex $Lbl(Grid) $GDefs(Lang)] CANERM::Sim(Scale) 0 7 $CANERM::Sim(ListScale) "Model::ParamsGridDefine CANERM"
         Option::Create $t.fr.grid.src "" CANERM::Sim(GridSrc) 0 14 $Sim(Pos) \
            "set CANERM::Sim(GridLat) \[lindex \$CANERM::Sim(GridSrc) 1\];
             set CANERM::Sim(GridLon) \[lindex \$CANERM::Sim(GridSrc) 2\];
             Model::ParamsGridDefine CANERM"
         checkbutton $t.fr.grid.sel -variable Page::Data(ToolMode) -onvalue CANERM -offvalue SPI \
            -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
            -command { SPI::ToolMode $Page::Data(ToolMode) Data True }
         pack $t.fr.grid.res -side left -ipady 1
         pack $t.fr.grid.src -side left -ipady 1
         pack $t.fr.grid.sel -side left

      #----- Meteorological Model.
      frame $t.fr.model
         Option::Create $t.fr.model.met [lindex $Lbl(Meteo) $GDefs(Lang)] CANERM::Sim(Meteo) 0 7 $CANERM::Sim(ListMeteo) "Model::ParamsMetDataDir CANERM"
         Option::Create $t.fr.model.delta [lindex $Lbl(MeteoDelta) $GDefs(Lang)] CANERM::Sim(Delta) 0 6 [list 1 3 6] ""
         button $t.fr.model.path -image OPEN -relief flat -bd 0 -overrelief raised -command "Model::ParamsMetPath"
         pack $t.fr.model.met -side left -ipady 1
         pack $t.fr.model.delta -side left -ipady 1
         pack $t.fr.model.path -side left
      pack $t.fr.event $t.fr.grid $t.fr.model -side top -padx 2 -fill x
   pack $t.fr -side top -padx 5 -pady 5 -ipady 2 -fill x

   Bubble::Create $t.fr.event        [lindex $Bubble(Event) $GDefs(Lang)]
   Bubble::Create $t.fr.model.met    [lindex $Bubble(Meteo) $GDefs(Lang)]
   Bubble::Create $t.fr.model.delta  [lindex $Bubble(Meteo) $GDefs(Lang)]
   Bubble::Create $t.fr.model.path   [lindex $Bubble(Path) $GDefs(Lang)]
   Bubble::Create $t.fr.grid         [lindex $Bubble(Scale) $GDefs(Lang)]
}

proc CANERM::ParamsEmission { Tab } {
   global   GDefs
   variable Sim
   variable Msg
   variable Lbl
   variable Bubble

   wm geom [winfo toplevel $Tab] =320x310

   set Sim(Iso)          [lindex $Sim(IsoName) 0]
   set Sim(Intensity)    [lindex $Sim(IsoRelease) 0]
   set Sim(IsoNb)        [llength $Sim(IsoName)]
   set Sim(EmHeightUnit) METRES

   if { $Sim(FnVert)<0 } {
      set Sim(FnVertDesc) "GAUSS"
   } elseif { $Sim(FnVert)>0 } {
      set Sim(FnVertDesc) "EMPIRICAL"
   } else {
      set Sim(FnVertDesc) "CONSTANT"
   }

   set t [TabFrame::Add $Tab 1 [lindex $Lbl(Emission) $GDefs(Lang)] False]

   #----- Selection des polluants
   labelframe $t.iso -text [lindex $Lbl(Pollutant) $GDefs(Lang)]

      frame $t.iso.type -bd 1 -relief sunken
         frame $t.iso.type.lbl
            label $t.iso.type.lbl.nb -text "Nb" -relief raised -bd 1 -width 3
            button $t.iso.type.lbl.add -text [lindex $Lbl(Add) $GDefs(Lang)] \
               -relief raised -bd 1 -width 10 -command "IsoBox::Create $t.iso \"CANERM::SpeciesFormat $t.iso\""
            button $t.iso.type.lbl.del -text [lindex $Lbl(Suppress) $GDefs(Lang)] \
               -relief raised -bd 1 -width 11 -command "CANERM::SpeciesDelete $t.iso"
            label $t.iso.type.lbl.intensity -text [lindex $Lbl(Intensity) $GDefs(Lang)] \
               -relief raised -bd 1 -width 10
            pack $t.iso.type.lbl.nb $t.iso.type.lbl.add $t.iso.type.lbl.del -side left -fill both -expand true
            pack $t.iso.type.lbl.intensity -side left -fill both
         pack $t.iso.type.lbl -side top -fill x

         frame $t.iso.type.entry
            label $t.iso.type.entry.nb -textvariable CANERM::Sim(IsoNb) \
               -relief sunken -bd 1 -width 3 -bg $GDefs(ColorLight)
            ComboBox::Create $t.iso.type.entry.polluant CANERM::Sim(Iso) noedit unsorted \
               nodouble $CANERM::Sim(MaxIso) $CANERM::Sim(IsoName) 25 5 \
               "set CANERM::Sim(Intensity) \[lindex \$CANERM::Sim(IsoRelease) \[lsearch -exact \$CANERM::Sim(IsoName) \$CANERM::Sim(Iso)\]\]
                $t.iso.type.entry.intensity config -state normal"
            entry $t.iso.type.entry.intensity -textvariable CANERM::Sim(Intensity) \
               -relief sunken -bd 1 -width 10 -bg $GDefs(ColorLight)
             pack $t.iso.type.entry.nb $t.iso.type.entry.polluant $t.iso.type.entry.intensity -side left -fill y

         pack $t.iso.type.entry -side top -fill y
      pack  $t.iso.type -side left -padx 2 -fill x
   pack $t.iso -side top -padx 5 -pady 5 -ipady 2 -fill x

   $t.iso.type.entry.polluant.select config -state disabled
   bind $t.iso.type.entry.intensity <Leave> "focus ."
   bind $t.iso.type.entry.intensity <FocusOut> "CANERM::CheckIntensity $t.iso.type.entry.polluant"

   Bubble::Create $t.iso.type.lbl.nb         [lindex $Bubble(Nb) $GDefs(Lang)]
   Bubble::Create $t.iso.type.lbl.add        [lindex $Bubble(Add) $GDefs(Lang)]
   Bubble::Create $t.iso.type.lbl.del        [lindex $Bubble(Del) $GDefs(Lang)]
   Bubble::Create $t.iso.type.lbl.intensity  [lindex $Bubble(Intensity) $GDefs(Lang)]
   Bubble::Create $t.iso.type.entry.polluant [lindex $Bubble(Pol) $GDefs(Lang)]

   #----- Selection de la fonction
   labelframe $t.func -text [lindex $Lbl(Function) $GDefs(Lang)]

      frame $t.func.type -bd 1 -relief sunken
         EntryMenu::Create $t.func.type.elev edit [lindex $Lbl(Elevation) $GDefs(Lang)] \
            top 2 $GDefs(ColorLight) SteelBlue CANERM::Sim(EmHeight) "0.0 10000.0" "; "
         frame $t.func.type.vert
            button $t.func.type.vert.select -text [lindex $Lbl(Vertical) $GDefs(Lang)] -relief raised -bd 1 -command "CANERM::GraphVertInit $t.func.type.vert.select"
            entry $t.func.type.vert.entry -textvariable CANERM::Sim(FnVertDesc) -relief sunken -bd 1 -width 9 -background $GDefs(ColorLight) \
               -disabledbackground $GDefs(ColorLight) -disabledforeground black -state disabled
            pack $t.func.type.vert.select -side top -fill both -expand true
            pack $t.func.type.vert.entry -side top -fill both -expand true

         frame $t.func.type.time
            button $t.func.type.time.select -text [lindex $Lbl(Time) $GDefs(Lang)] -relief raised -bd 1 -command "CANERM::GraphTimeInit $t.func.type.time.select"
            entry $t.func.type.time.entry -textvariable CANERM::Sim(FnTime) -relief sunken -bd 1 -width 9 -background $GDefs(ColorLight) \
               -disabledbackground $GDefs(ColorLight) -disabledforeground black -state disabled
            pack $t.func.type.time.select -side top -fill both -expand true
            pack $t.func.type.time.entry -side top -fill both -expand true

         #----- On ajoute une partie au widget EntryMenu
         entry $t.func.type.time.hour -width 3 -bg $GDefs(ColorLight) -relief sunken -bd 1 \
            -textvariable CANERM::Sim(EmDuration) -state disabled  -disabledbackground $GDefs(ColorLight) -disabledforeground black
         pack  $t.func.type.time.hour -after $t.func.type.time.select -side right
         pack $t.func.type.elev $t.func.type.vert $t.func.type.time -side left -fill both -expand true

      pack  $t.func.type -side left -padx 2 -fill x -expand true
   pack $t.func -side top -padx 5 -ipady 2 -fill x

   Bubble::Create $t.func.type.elev      [lindex $Bubble(Height) $GDefs(Lang)]
   Bubble::Create $t.func.type.vert      [lindex $Bubble(Vertical) $GDefs(Lang)]
   Bubble::Create $t.func.type.time      [lindex $Bubble(Temporal) $GDefs(Lang)]
   Bubble::Create $t.func.type.time.hour [lindex $Bubble(Delai) $GDefs(Lang)]
}

#-------------------------------------------------------------------------------
# Nom      : <CANERM::SimCont>
# Creation : Octobre 1999 - J.P. Gauthier - CMC/CMOE -
#
# But      : Continuation d'une simulation.
#
# Parametres :
#   <Tab>    : Boite d'onglet dans laquelle ajoute les onglets
#
# Retour :
#
# Remarques :
#
#-------------------------------------------------------------------------------

proc CANERM::ParamsCont { Tab } {
   global GDefs
   variable Sim
   variable Lbl
   variable Msg
   variable Bubble

   wm geom [winfo toplevel $Tab] =320x220

   #---- Grid Def has an imprecision when converting back and forth so set to previous grid
   set Sim(Grid) $Sim(PGrid)

   set t [TabFrame::Add $Tab 1 [lindex $Lbl(Params) $GDefs(Lang)] False]

   #----- Model Integration (data output time step and model time step).
   labelframe $t.integ -text "[lindex $Lbl(Integration) $GDefs(Lang)]"
      Option::Create $t.integ.dur [lindex $Lbl(SimDuration) $GDefs(Lang)] CANERM::Sim(Duration) 1 -1 $CANERM::Sim(ListSimDuration) ""
      Option::Create $t.integ.ots [lindex $Lbl(OutputTimeStepHour) $GDefs(Lang)] CANERM::Sim(ISauve) 0 -1 $CANERM::Sim(ListISauve) ""
      pack $t.integ.dur $t.integ.ots -side top -fill x -padx 2
   pack $t.integ -side top -padx 5 -ipady 2 -fill x

   Bubble::Create $t.integ.dur "[lindex $Bubble(SimDuration) $GDefs(Lang)]"
   Bubble::Create $t.integ.ots "[lindex $Bubble(OutputTimeStepHour) $GDefs(Lang)]"
   Bubble::Create $t.integ "[lindex $Bubble(SimIntegration) $GDefs(Lang)]"

   #----- Selection des parametres
   labelframe $t.fr -text "[lindex $Lbl(OtherParameters) $GDefs(Lang)]"
      Option::Create $t.fr.event [lindex $Lbl(Event) $GDefs(Lang)] CANERM::Sim(Event) 1 -1 \
          $CANERM::Sim(ListEvent) ""
      Option::Create $t.fr.grid [lindex $Lbl(Grid) $GDefs(Lang)] CANERM::Sim(Scale) 0 -1 \
          $CANERM::Sim(ListScale) "Model::ParamsGridDefine CANERM"
       checkbutton $t.fr.grid.sel -variable Page::Data(ToolMode) -onvalue CANERM -offvalue SPI \
         -image ARROW -indicatoron 0 -relief sunken -bd 1 -overrelief raised -offrelief flat -selectcolor $GDefs(ColorFrame) \
         -command { SPI::ToolMode $Page::Data(ToolMode) Data True }

      #----- Meteorological Model.
      frame $t.fr.model
         Option::Create $t.fr.model.met [lindex $Lbl(Meteo) $GDefs(Lang)] CANERM::Sim(Meteo) 0 7 $CANERM::Sim(ListMeteo) "Model::ParamsMetDataDir CANERM"
         Option::Create $t.fr.model.delta [lindex $Lbl(MeteoDelta) $GDefs(Lang)] CANERM::Sim(Delta) 0 6 [list 1 3 6] ""
         button $t.fr.model.path -image OPEN -relief flat -bd 0 -overrelief raised -command "Model::ParamsMetPath"
         pack $t.fr.model.met -side left -ipady 1
         pack $t.fr.model.delta -side left -ipady 1
         pack $t.fr.model.path  -side left
      pack $t.fr.event $t.fr.grid $t.fr.model -padx 2 -side top -fill x
      pack $t.fr.grid.sel -side left -ipadx 1
   pack $t.fr -side top -padx 5 -pady 5 -ipady 2 -fill x

   Bubble::Create $t.fr.event        [lindex $Bubble(Event) $GDefs(Lang)]
   Bubble::Create $t.fr.model.met    [lindex $Bubble(Meteo) $GDefs(Lang)]
   Bubble::Create $t.fr.model.delta  [lindex $Bubble(Meteo) $GDefs(Lang)]
   Bubble::Create $t.fr.model.path   [lindex $Bubble(Path) $GDefs(Lang)]
   Bubble::Create $t.fr.grid         [lindex $Bubble(Scale) $GDefs(Lang)]
}

#----------------------------------------------------------------------------
# Nom      : <GraphVertInit>
# Creation : Decembre 1999 - J.P. Gauthier - CMC/CMOE
#
# But      : Initialise le canvas de la fonction de distribution verticale
#
# Parametres :
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc CANERM::GraphVertInit { Parent } {
   global GDefs
   variable Sim
   variable Lbl

   toplevel     .expgraph
   wm title     .expgraph ""
   wm transient .expgraph $Parent
   wm resizable .expgraph 0 0
   wm geometry  .expgraph +[winfo rootx $Parent]+[expr [winfo rooty $Parent] + [winfo height $Parent]]
   wm protocol  .expgraph WM_DELETE_WINDOW { }

   canvas .expgraph.graph -height 261 -width 250 -bg white -bd 1 -relief sunken
   button .expgraph.close -text [lindex $Lbl(Close) $GDefs(Lang)] -bd 1 -command { destroy .expgraph }
   pack .expgraph.graph -side top
   pack .expgraph.close -side top -fill x

   scale .expgraph.aval -relief raised -bd 1 -variable CANERM::Sim(FnVert) -from -0.5 -to 10.0 -resolution 0.01 \
      -bg white -bd 1 -fg red -troughcolor white -orient horizontal -label AVal \
      -width 15 -sliderlength 10 -length 200 -showvalue True -relief flat -command "CANERM::GraphVertRefresh .expgraph.graph" -font XFont10

   switch "$Sim(FnVertDesc)" {
      "GAUSS"     { .expgraph.aval configure -from -0.5 -to -0.01 }
      "EMPIRICAL" { .expgraph.aval configure -from 0.0 -to 10.0 }
      "CONSTANT"  { .expgraph.aval configure -from 0.0 -to 0.0  }
   }

   frame .expgraph.func
      radiobutton .expgraph.func.linear -text "CONSTANT" -variable CANERM::Sim(FnVertDesc) -value CONSTANT \
         -indicatoron false -font XFont10 -bd 1 -bg white  \
         -command ".expgraph.aval configure -from 0.0 -to 0.0 ; set CANERM::Sim(FnVert) 0.0 ; CANERM::GraphVertRefresh .expgraph.graph"
      radiobutton .expgraph.func.gauss -text "GAUSS" -variable CANERM::Sim(FnVertDesc) -value GAUSS \
         -indicatoron false -font XFont10 -bd 1 -bg white \
         -command ".expgraph.aval configure -from -0.5 -to -0.01 ; set CANERM::Sim(FnVert) -0.1 ; CANERM::GraphVertRefresh .expgraph.graph"
      radiobutton .expgraph.func.expon -text "EMPIRICAL" -variable CANERM::Sim(FnVertDesc) -value EMPIRICAL \
         -indicatoron false -font XFont10 -bd 1 -bg white \
         -command ".expgraph.aval configure -from 0.0 -to 10.0 ; set CANERM::Sim(FnVert) 4.0 ; CANERM::GraphVertRefresh .expgraph.graph"
      pack .expgraph.func.linear .expgraph.func.gauss .expgraph.func.expon -side left -ipadx 2

   .expgraph.graph create window 125 261 -window .expgraph.aval -anchor s
   .expgraph.graph create window 125 5  -window .expgraph.func -anchor n

   .expgraph.graph create line 25 25 25 225 -fill black -tags ASCALE
   .expgraph.graph create line 25 225 225 225 -fill black -tags ASCALE
   .expgraph.graph create text 25 25 -text " <--- Max Elevation" -fill SteelBlue -tags ASRC -anchor w -font XFont10
   .expgraph.graph create text 227 225 -text P -fill black -tags ASCALE -anchor w  -font XFont10
   .expgraph.graph create text 25  23  -text Z/h -fill black -tags ASCALE -anchor e  -font XFont10

   grab .expgraph
}

#-------------------------------------------------------------------------------
# Nom      : <GraphVertRefresh>
# Creation : Decembre 1999 - J.P. Gauthier - CMC/CMOE
#
# But      : Dessinne le graph selon les parametres de Sim(FnVert)
#            d'affichage.
#
# Parametres  :
#   <Canvas>  : Path du canvas
#
# Retour    :
#
# Remarque :
#
#-------------------------------------------------------------------------------

proc CANERM::GraphVertRefresh { Canvas args } {
   variable Sim

   $Canvas delete ALINE

   set line ""

   switch $Sim(FnVertDesc) {
      "EMPIRICAL" {
         $Canvas coord ASRC 25 30

         #----- Fonction de poids
         for { set i 0.0 } { $i <= 1.0 } { set i [expr $i+0.01] } {

            set zh [expr 1-$i]
            set x [expr 25+400*$Sim(FnVert)*($zh)*exp(-$Sim(FnVert)*$zh)]
            set y [expr 225-200*$i]

            set line "$line $x $y"
         }
         set line "$line $x $y 25 25"
      }

      "GAUSS" {
         $Canvas coord ASRC 25 125

         #----- Fonction Gaussienne
         set sg [expr 10000*pow(abs($Sim(FnVert)/100.0),2)]

         for { set i 0.0 } { $i <= 1.0 } { set i [expr $i+0.01] } {

            set x [expr 25+exp(-pow(($i-0.5)*5.0,2))/$sg]
            set y [expr 225-200*($i)]

            set line "$line $x $y"
         }
      }

      "CONSTANT" {
         $Canvas coord ASRC 25 30

         set line "75 225 75 25"
      }
   }

   #----- Creer la fonction
   eval $Canvas create line $line -tags ALINE -fill red
}

#----------------------------------------------------------------------------
# Nom      : <GraphTimeInit>
# Creation : Decembre 1999 - J.P. Gauthier - CMC/CMOE
#
# But      : Initialise le canvas de la fonction de distribution temporelle
#
# Parametres :
#
# Retour:
#
# Remarques :
#
#----------------------------------------------------------------------------

proc CANERM::GraphTimeInit { Parent } {
   global GDefs
   variable Sim
   variable Lbl

   toplevel     .expgraph
   wm title     .expgraph ""
   wm transient .expgraph $Parent
   wm resizable .expgraph 0 0
   wm geometry  .expgraph +[winfo rootx $Parent]+[expr [winfo rooty $Parent] + [winfo height $Parent]]
   wm protocol  .expgraph WM_DELETE_WINDOW { }

   canvas .expgraph.graph -height 261 -width 250 -bg white -bd 1 -relief sunken
   button .expgraph.close -text [lindex $Lbl(Close) $GDefs(Lang)]  -bd 1 -command { destroy .expgraph }
   pack .expgraph.graph -side top
   pack .expgraph.close -side top -fill x

   frame .expgraph.func
      radiobutton .expgraph.func.linear -text "CONSTANT" -variable CANERM::Sim(FnTime) -value CONSTANT \
         -indicatoron false -font XFont10 -bd 1 -bg white \
         -command "CANERM::GraphTimeRefresh .expgraph.graph ; .expgraph.delai configure -state disabled -fg white"
      radiobutton .expgraph.func.gauss -text "GAUSS" -variable CANERM::Sim(FnTime) -value GAUSS \
         -indicatoron false -font XFont10 -bd 1 -bg white \
         -command "CANERM::GraphTimeRefresh .expgraph.graph ; .expgraph.delai configure -state normal -fg red"
      radiobutton .expgraph.func.expon -text "EXPONENTIAL" -variable CANERM::Sim(FnTime) -value EXPONENTIAL \
         -indicatoron false -font XFont10 -bd 1 -bg white \
         -command "CANERM::GraphTimeRefresh .expgraph.graph ; .expgraph.delai configure -state disabled -fg white"
      pack .expgraph.func.linear .expgraph.func.gauss .expgraph.func.expon -side left -ipadx 2

   scale .expgraph.xperiod -relief raised -bd 1 -variable CANERM::Sim(EmDuration) -from 1 -to 144 -resolution 1 \
      -bg white -fg red -troughcolor white -orient horizontal \
      -label "[lindex $Lbl(Duration) $GDefs(Lang)] (Hrs)" \
      -width 15 -sliderlength 10 -length 180 -showvalue True -relief flat -command "CANERM::GraphTimeRefresh .expgraph.graph" -font XFont10
   scale .expgraph.delai -relief raised -bd 1 -variable CANERM::Sim(Delai) -from 0 -to 12 -resolution 1  -state disabled\
      -bg white -fg white -troughcolor white -orient horizontal -label Delai\
      -width 15 -sliderlength 10 -length 20 -showvalue True -relief flat -command "CANERM::GraphTimeRefresh .expgraph.graph" -font XFont10

   .expgraph.graph create window 225 261 -window .expgraph.xperiod -anchor se
   .expgraph.graph create window 25  261 -window .expgraph.delai   -anchor sw
   .expgraph.graph create window 125 5   -window .expgraph.func    -anchor n

   .expgraph.graph create line 25 25 25 225       -fill black
   .expgraph.graph create line 25 225 225 225     -fill black
   .expgraph.graph create text 227 225 -text "T"  -fill black -anchor w  -font XFont10
   .expgraph.graph create text 25  23  -text M    -fill black -anchor e  -font XFont10

   grab .expgraph
}

#-------------------------------------------------------------------------------
# Nom      : <GraphTimeRefresh>
# Creation : Decembre 1999 - J.P. Gauthier - CMC/CMOE
#
# But      : Dessine le graph selon les parametres de Sim
#            d'affichage.
#
# Parametres  :
#   <Canvas>  : Path du canvas
#
# Retour    :
#
# Remarque :
#
#-------------------------------------------------------------------------------

proc CANERM::GraphTimeRefresh { Canvas args } {
   variable Sim

   $Canvas delete ALINE

   set line   ""
   set deltat [expr ($Sim(EmDuration)+72.0)/216.0]

   switch $Sim(FnTime) {

      "EXPONENTIAL" {
         for { set i 0.0 } { $i <= 1.0 } { set i [expr $i+0.01] } {

            set y [expr 225-200*exp(-$i*5)]
            set x [expr 25+200*$i*$deltat]

            set line "$line $x $y"
         }
      }
      "GAUSS" {
         set delai [expr $Sim(Delai)/50.0]
         for { set i 0.0 } { $i <= [expr 1.0+$delai] } { set i [expr $i+0.01] } {

            set y [expr 225-200*exp(-pow(-$i+$delai,2)*5)]
            set x [expr 25+200*$i*$deltat]

            set line "$line $x $y"
         }
      }
      "CONSTANT" {
         set x [expr 25+200*$deltat]

         set line "25 125 $x 125"
      }
   }

   #----- Creer la fonction
   eval $Canvas create line $line -tags ALINE -fill red
}

#----------------------------------------------------------------------------
# Nom      : <CANERM::Product>
# Creation : Septembre 2005 - J.P. Gauthier - CMC/CMOE
#
# But      : Lire les donnees et initialiser le produit
#
# Parametres  :
#    <Frame>  : Page
#    <Type>   : Type de donnees
#    <Layout> : Layout (produit)
#
# Retour :
#
# Remarques :
#
#----------------------------------------------------------------------------

proc CANERM::CallProduct { Frame Type Layout } {

   foreach box [FieldBox::Get] {
      FieldBox::Close $box
   }
   foreach box [TrajBox::Get] {
      TrajBox::Close $box
   }

   if { $Type!="" } {
      CANERM::Result $Type
   }
   SPI::LayoutLoad $Frame $Layout
}

#----------------------------------------------------------------------------
# Nom      : <CANERM::PopUp>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Afficher le popup contextuel des simulations.
#
# Parametres    :
#    <X>        : ...
#    <Y>        : ...
#
# Retour :
#
# Remarques :
#
#----------------------------------------------------------------------------

proc CANERM::PopUp { X Y } {
   global   GDefs
   variable Lbl
   variable Sim
   variable Tmp

   if { ![winfo exists .canermpopup] } {
      menu .canermpopup -tearoff 0 -bd 1 -type normal -activeborderwidth 1
         .canermpopup add command -label CANERM -background $GDefs(ColorHighLight) \
            -activebackground $GDefs(ColorHighLight)
         .canermpopup add cascade -label [lindex $Lbl(Product) $GDefs(Lang)] -menu .canermpopup.product
         .canermpopup add cascade -label [lindex $Lbl(Result) $GDefs(Lang)] -menu .canermpopup.res
         .canermpopup add separator
         .canermpopup add command -label [lindex $Lbl(Continue) $GDefs(Lang)] \
            -command { Model::ParamsWindow CANERM CONT } -state disabled
         .canermpopup add command -label [lindex $Lbl(Relaunch) $GDefs(Lang)] \
            -command { Model::ParamsWindow CANERM RENEW } -state disabled
         .canermpopup add separator
         .canermpopup add command -label [lindex $Lbl(Suppress) $GDefs(Lang)] \
             -command { Model::Delete $Exp::Data(SelectSim) }

      menu .canermpopup.product -tearoff 0 -bd 1 -type normal -activeborderwidth 1
          .canermpopup.product add command -label "VAAC" -command { CANERM::CallProduct $Page::Data(Frame) result VAAC }
          .canermpopup.product add command -label "RSMC" -command { CANERM::CallProduct $Page::Data(Frame) all    RSMC }
          .canermpopup.product add command -label "INFO" -command { CANERM::CallProduct $Page::Data(Frame) all    INFO }
          .canermpopup.product add command -label "METF" -command { CANERM::CallProduct $Page::Data(Frame) metf   METF }
          .canermpopup.product add separator
          .canermpopup.product add command -label "SPI" -command { CANERM::CallProduct $Page::Data(Frame) "" SPI }

      menu .canermpopup.res -tearoff 0 -bd 1 -type normal -activeborderwidth 1
         .canermpopup.res add command -label "All" -command "CANERM::Result all"
         .canermpopup.res add separator
         .canermpopup.res add command -label "Standard" -command "CANERM::Result result"
         .canermpopup.res add command -label "Post" -command "CANERM::Result post"
         .canermpopup.res add command -label "MetFields" -command "CANERM::Result metf"
         .canermpopup.res add separator
         .canermpopup.res add command -label "Meteo" -command "CANERM::Result meteo"
   }

   #----- Verifier l'etat de la simulation et configurer le menu en consequence
   switch --  [Info::Strip $Exp::Data(SelectSim) State] {

      -1 {                                            #----- Error
         .canermpopup entryconfigure 1 -state disabled   ;#Product
         .canermpopup entryconfigure 2 -state disabled   ;#Data
         .canermpopup entryconfigure 4 -state disabled   ;#Continue
         .canermpopup entryconfigure 5 -state disabled   ;#Relaunch
         .canermpopup entryconfigure 7 -state normal     ;#Suppress
      }
      0 {                                             #----- Continuable
         .canermpopup entryconfigure 1 -state normal     ;#Product
         .canermpopup entryconfigure 2 -state normal     ;#Data
         .canermpopup entryconfigure 4 -state normal     ;#Continue
         .canermpopup entryconfigure 5 -state disabled   ;#Relaunch
         .canermpopup entryconfigure 7 -state normal     ;#Suppress
      }
      1 {                                             #----- Etat Termine
         .canermpopup entryconfigure 1 -state normal     ;#Product
         .canermpopup entryconfigure 2 -state normal     ;#Data
         .canermpopup entryconfigure 4 -state normal     ;#Continue
         .canermpopup entryconfigure 5 -state normal     ;#Relaunch
         .canermpopup entryconfigure 7 -state normal     ;#Suppress
      }
      2 {                                              #----- Etat d'execution
         .canermpopup entryconfigure 1 -state disabled   ;#Product
         .canermpopup entryconfigure 2 -state disabled   ;#Data
         .canermpopup entryconfigure 4 -state disabled   ;#Continue
         .canermpopup entryconfigure 5 -state disabled    ;#Relaunch
         .canermpopup entryconfigure 7 -state normal     ;#Suppress
      }
   }
   tk_popup .canermpopup $X $Y 0
}
