#===============================================================================
# Environnement Canada
# Centre Meteorologique Canadian
# 2100 Trans-Canadienne
# Dorval, Quebec
#
# Projet     : Interface pour la gestion des experiences.
# Fichier    : TRAJECT.int
# Creation   : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# Description: Description des interfaces et procedures relatives a
#              celles-ci pour le module TRAJECTOIRE.
#
# Remarques  :
#
#===============================================================================

#-------------------------------------------------------------------------------
# Nom      : <TRAJECT::ParamsNew>
# Creation : Octobre 1999 - J.P. Gauthier - CMC/CMOE
#
# But      : Interface de selection des parametre du modele TRAJECT.
#
# Parametres :
#   <Tab>    : Boite d'onglet dans laquelle ajoute les onglets
#   <Time>   : Selection du temps de depart
#
# Retour :
#
# Remarques :
#
#-------------------------------------------------------------------------------

proc TRAJECT::ParamsNew { Tab { Time True } } {
   global GDefs
   variable Lbl
   variable Msg
   variable Sim
   variable Bubble

   wm geom [winfo toplevel $Tab] =300x350
   set frame [TabFrame::Add $Tab 1 [lindex $Lbl(Params) $GDefs(Lang)] False]

   #----- Simulation Starting Time.
   if { $Time } {
      labelframe $frame.d -text "[lindex $Lbl(Start) $GDefs(Lang)]"

         #----- Simulation Date Hour and minutes.
         set TRAJECT::Sim(Second) [clock seconds]
         Calendar::Create $frame.d.date "" TRAJECT::Sim(Second) -1
         Clock::Create $frame.d.time [lindex $Lbl(At) $GDefs(Lang)] TRAJECT::Sim(AccHour)
         pack $frame.d.date -side left -fill x -expand true -padx 2
         pack $frame.d.time -side left -padx 2
      pack $frame.d -side top -padx 5 -pady 5 -ipady 2 -fill x

      Bubble::Create $frame.d.date [lindex $Bubble(Date) $GDefs(Lang)]
      Bubble::Create $frame.d.time [lindex $Bubble(Time) $GDefs(Lang)]
   }

   #----- Model Integration (data output time step and model time step).
   labelframe $frame.integ -text "[lindex $Lbl(Integration) $GDefs(Lang)]"
      Option::Create $frame.integ.method [lindex $Lbl(Method) $GDefs(Lang)] TRAJECT::Sim(Method) 0 -1 $TRAJECT::Sim(ListMethod) ""
      Option::Create $frame.integ.time [lindex $Lbl(TimeStep) $GDefs(Lang)] TRAJECT::Sim(TimeStep) 0 -1 $TRAJECT::Sim(ListTimeStep) ""
      Option::Create $frame.integ.dur [lindex $Lbl(SimDuration) $GDefs(Lang)] TRAJECT::Sim(Duration) 1 -1 $TRAJECT::Sim(ListSimDuration) ""
      Option::Create $frame.integ.res [lindex $Lbl(SimBatchStart) $GDefs(Lang)] TRAJECT::Sim(BatchStart) 1 -1 $TRAJECT::Sim(ListSimDuration) ""
      pack $frame.integ.method $frame.integ.time $frame.integ.dur $frame.integ.res -side top -fill x  -padx 2
   pack $frame.integ -side top -padx 5 -ipady 2 -fill x

   Bubble::Create $frame.integ        [lindex $Bubble(SimIntegration) $GDefs(Lang)]
   Bubble::Create $frame.integ.method [lindex $Bubble(Method) $GDefs(Lang)]
   Bubble::Create $frame.integ.time   [lindex $Bubble(Step) $GDefs(Lang)]
   Bubble::Create $frame.integ.dur    [lindex $Bubble(SimDuration) $GDefs(Lang)]
   Bubble::Create $frame.integ.res    [lindex $Bubble(SimBatchStart) $GDefs(Lang)]

  #----- Selection des niveaux
   labelframe $frame.l -text "[lindex  $Lbl(Parcels) $GDefs(Lang)]"
      frame $frame.l.lbl
         label $frame.l.lbl.l1 -text "[lindex $Lbl(Level) $GDefs(Lang)] 1"
         label $frame.l.lbl.l2 -text "[lindex $Lbl(Level) $GDefs(Lang)] 2"
         label $frame.l.lbl.l3 -text "[lindex $Lbl(Level) $GDefs(Lang)] 3"
         pack $frame.l.lbl.l1 $frame.l.lbl.l2 $frame.l.lbl.l3 -side top -ipadx 4
      pack $frame.l.lbl -side left

      frame $frame.l.sel
         EntryVar::Create $frame.l.sel.l1 TRAJECT::Sim(Level1) float "0" "" \
            -relief sunken -bd 1 -width 9 -bg $GDefs(ColorLight) -justify right
         EntryVar::Create $frame.l.sel.l2 TRAJECT::Sim(Level2) float "0" "" \
            -relief sunken -bd 1 -width 9 -bg $GDefs(ColorLight) -justify right
         EntryVar::Create $frame.l.sel.l3 TRAJECT::Sim(Level3) float "0" "" \
            -relief sunken -bd 1 -width 9 -bg $GDefs(ColorLight) -justify right
         pack $frame.l.sel.l1 $frame.l.sel.l2 $frame.l.sel.l3 -side top -fill y -expand true
      pack $frame.l.sel -side left

      scale $frame.l.bar -orient vertical -command "TRAJECT::LevelPack $frame" -relief flat -sliderlength 10 -width 10 -bd 1\
         -highlightthickness 0 -showvalue false -length 51 -from 0 -to 22 -resolution 1
      button $frame.l.type -relief groove -bd 2 -highlightthickness 0 -textvariable TRAJECT::Sim(LevelUnit) \
         -command "TRAJECT::SwitchElev"
      pack $frame.l.bar -side left
      pack $frame.l.type -side left -fill both -expand true -pady 5 -padx 2
   pack $frame.l -side top -padx 5 -pady 5 -ipady 2 -fill x

   Bubble::Create $frame.l.sel  [lindex $Bubble(Level) $GDefs(Lang)]
   Bubble::Create $frame.l.type [lindex $Bubble(Type) $GDefs(Lang)]

   #----- Selection des parametres
   labelframe $frame.fr -text "[lindex $Lbl(OtherParameters) $GDefs(Lang)]"

      #----- Meteorological Model.
      frame $frame.fr.model
         Option::Create $frame.fr.model.met [lindex $Lbl(Meteo) $GDefs(Lang)] TRAJECT::Sim(Meteo) 5 12 $TRAJECT::Sim(ListMeteo) "Model::ParamsMetDataDir TRAJECT"
         Option::Create $frame.fr.model.delta [lindex $Lbl(MeteoDelta) $GDefs(Lang)] TRAJECT::Sim(Delta) 0 5 [list 1 3 6] ""
         button $frame.fr.model.path -image OPEN -relief flat -bd 0 -overrelief raised \
                  -command "Model::ParamsMetPath"
         pack $frame.fr.model.met -side left -ipady 1 -fill x
         pack $frame.fr.model.delta -side left -ipady 1
         pack $frame.fr.model.path  -side left
         pack $frame.fr.model -side top -fill x -padx 2
   pack $frame.fr -side top -padx 5 -ipady 2 -fill x

   Bubble::Create $frame.fr.model.met    [lindex $Bubble(Meteo) $GDefs(Lang)]
   Bubble::Create $frame.fr.model.delta  [lindex $Bubble(Meteo) $GDefs(Lang)]
   Bubble::Create $frame.fr.model.path   [lindex $Bubble(Path) $GDefs(Lang)]
}

#----------------------------------------------------------------------------
# Nom      : <TRAJECT::LevelPack>
# Creation : Mars 2002 - J.P. Gauthier - CMC/CMOE
#
# But      : Effectuer un "scroll" de la liste de niveaux.
#
# Parametres    :
#    <Frame>    : Identificateur du frame
#    <Val>      : Position actuelle
#
# Retour :
#
# Remarques :
#
#----------------------------------------------------------------------------

proc TRAJECT::LevelPack { Frame Val } {
   global GDefs
   variable Lbl

   $Frame.l.lbl.l1 configure -text "[lindex $Lbl(Level) $GDefs(Lang)] [format "%02i" [expr $Val+1]]"
   $Frame.l.lbl.l2 configure -text "[lindex $Lbl(Level) $GDefs(Lang)] [format "%02i" [expr $Val+2]]"
   $Frame.l.lbl.l3 configure -text "[lindex $Lbl(Level) $GDefs(Lang)] [format "%02i" [expr $Val+3]]"

   eval $Frame.l.sel.l1 configure -textvariable TRAJECT::Sim(Level[expr $Val+1])
   eval $Frame.l.sel.l2 configure -textvariable TRAJECT::Sim(Level[expr $Val+2])
   eval $Frame.l.sel.l3 configure -textvariable TRAJECT::Sim(Level[expr $Val+3])
}

#----------------------------------------------------------------------------
# Nom      : <TRAJECT::Product>
# Creation : Septembre 2005 - J.P. Gauthier - CMC/CMOE
#
# But      : Lire les donnees et initialiser le produit
#
# Parametres  :
#    <Frame>  : Page
#    <Type>   : Type de donnees
#    <Layout> : Layout (produit)
#
# Retour :
#
# Remarques :
#
#----------------------------------------------------------------------------

proc TRAJECT::CallProduct { Frame Type Layout } {

   foreach box [FieldBox::Get] {
      FieldBox::Close $box
   }
   foreach box [TrajBox::Get] {
      TrajBox::Close $box
   }

   if { $Type!="" } {
      TRAJECT::Result
   }
   SPI::LayoutLoad $Frame $Layout

   Model::TypeSelect none 1 $Exp::Data(Name)
   SPI::Locate $Exp::Data(Lat) $Exp::Data(Lon)
}

#----------------------------------------------------------------------------
# Nom      : <TRAJECT::PopUp>
# Creation : Aout 2001 - J.P. Gauthier - CMC/CMOE
#
# But      : Afficher le popup contextuel des simulations.
#
# Parametres    :
#    <X>        : ...
#    <Y>        : ...
#
# Retour :
#
# Remarques :
#
#----------------------------------------------------------------------------

proc TRAJECT::PopUp { X Y } {
   global GDefs
   variable Lbl

   if { ![winfo exists .trajectpopup] } {

      menu .trajectpopup -tearoff 0 -bd 1 -type normal -activeborderwidth 1
         .trajectpopup add command -label "TRAJECT"  -command "" -background $GDefs(ColorHighLight) \
            -activebackground $GDefs(ColorHighLight)
         .trajectpopup add cascade -label [lindex $Lbl(Product) $GDefs(Lang)] -menu .trajectpopup.product
         .trajectpopup add command -label [lindex $Lbl(Result) $GDefs(Lang)]  -command { TRAJECT::Result }
         .trajectpopup add separator
         .trajectpopup add command -label [lindex $Lbl(Suppress) $GDefs(Lang)] \
             -command { Model::Delete $Exp::Data(SelectSim) }

      menu .trajectpopup.product -tearoff 0 -bd 1 -type normal -activeborderwidth 1
          .trajectpopup.product add command -label "TRAJ" -command {  TRAJECT::CallProduct $Page::Data(Frame) All TRAJ }
          .trajectpopup.product add separator
          .trajectpopup.product add command -label "SPI" -command { TRAJECT::CallProduct $Page::Data(Frame) "" SPI }
   }

   tk_popup .trajectpopup $X $Y 0
}
